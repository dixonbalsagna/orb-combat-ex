class_name AnimFighter
extends RefCounted
## The animator of one fighter (slice A1 of docs/animation/pose-pipeline.md): from the sim's state and events it solves a
## pose on rig R1 once a frame, and every pane's mannequin is written from it. Key poses come from data/anim/; between
## them it does the timing, easing, lag, overshoot and reactions. Render only: it reads the sim (fighter state, the running
## exchange's beats, events) and never writes it, draws no sim random numbers, and moves no anchor: a strike's contact
## pose lands on the beat's own time.
##
## The provisional part cue (until Combat's composer emits one): the running exchange is read straight from
## S.dirS.ex.beats. Its `strike` beats say when each blow lands and who throws it, its `rush` beats when a fighter
## closes, and every beat is scheduled before it fires, so the wind-up can start early enough. The pose layers, in order:
##   base (state, stance, movement, smoothed) -> cue pose -> approach or strike part -> beam -> reaction -> drift,
##   shiver and spring chains on the extras.
## What is not here yet (later slices): IK to the defender's socket and the ground, situation adaptation, the style
## modifier stack, inertialisation across parts, interpolation between tick states.

const DT := 1.0 / 60.0
const STANCES := ["aggressive", "defensive", "evasive", "escape"]

var slot: int = 0
var q: Array[Quaternion] = []
var hips := Vector3.ZERO
var curl := Vector2.ZERO
var root_off := Vector3.ZERO
var gq: Array[Quaternion] = []
var gp := PackedVector3Array()
var frame: int = -1
var version: int = 0                   # bumped by every real solve; a body skips its bone writes when it has this one
var _lag_k: float = -1.0
var _settle: int = 0
var _bkey: int = -1
var _bkey2: int = -1
var _was_down: bool = false              # polish: a get-up after a fall (the rise out of the crouch is slower when worn)
var _rise_t0: float = -1.0
var _rise_dur: float = 0.3
var _air_w: float = 0.0                  # hovering, for the bob
var _lean: float = 0.0                  # the lean into acceleration, smoothed (rad)
var _prev_x: float = 0.0
var _have_x: bool = false
var _prev_cx: float = 0.0
var _smear: float = 0.0                 # the contact catch: how far behind the anchor the body is drawn (model units), decaying
var _smear_t0: float = -10.0
var press: Dictionary = {}             # press styles (docs/animation/press-styles.md): the style of the blow playing and where it is, for VFX (empty when none)
var _gest: Dictionary = {}              # an intro gesture playing: {id, t0, dur, w, mask (group names), pose (a single pose, not a sequence)}
var riposte: Dictionary = {}           # a riposte blow playing (press-styles.md section 12): {kind light or heavy, reversal, sure, ticks_to_contact}, for VFX; empty when none
var _rip_cue: Dictionary = {}          # the riposte cue's word (kind, the tick it came, ticks ahead): shown to VFX until the blow's own beat takes over
var _stag_t0: float = -10.0            # when a stagger cue last played his stagger (the old stunTicks watch yields to it)
var zip: Dictionary = {}               # the LT zip (docs/animation/zip.md): its phase and look for VFX, empty when no zip is playing
var zip_path: Array = []               # where the zipping body was over the last solves: {T, x, y, phase}, newest last (the path of the body for the blur)
var _zip: Dictionary = {}              # the zip told to this body (AnimZip.start), cleared when it is over
var _zh: Dictionary = {}               # held by a zip (carried, lifted): {kind, t0, dur}
var _zip_tilt: float = 0.0             # the body turned along its path (rad), smoothed
var _zip_dy: float = 0.0               # the lifted fighter's cosmetic rise (model units)
var _zip_phase: String = ""
var press_ring: Array = []             # the last solved poses while a styled blow plays, newest last: {T, q, hips, root_off}: the afterimages (press_pose)
var press_path: Array = []             # the striking limb's tip over the same solves (model space, root_off included), newest last: {T, tip}
var _ci_latch: Array = [Vector2.ZERO, Vector2.ZERO]   # the contact point a styled blow landed on (the first limb, the second), kept through the hold
var _ci_latch_tc: Array = [-1.0, -1.0]
var _pr_cache: Dictionary = {}         # blow id -> its press style, decided the first time the blow is seen
var _pr_dx: float = 0.0                # the body drawn this far ahead (model units): the wind-up's lean back, the release's lunge
var _pr_q: Array[Quaternion] = []      # the pose of the last styled solve (before the contact solve) ...
var _pr_hips := Vector3.ZERO
var _pr_curl := Vector2.ZERO
var _pr_T: float = -10.0
var _pr_blow: int = -1
var _pc_q: Array[Quaternion] = []      # ... and the one the previous blow ended on, which a blow with `carry` blends out of
var _pc_hips := Vector3.ZERO
var _pc_curl := Vector2.ZERO
var _pc_ok: bool = false
var _full_fk: bool = false
const _LEG_BONES := [16, 17, 19, 20]                  # thigh_l, shin_l, thigh_r, shin_r
const LEG_CHAIN := [0, 1, 16, 17, 18, 19, 20, 21]   # root, pelvis, thigh, shin, foot of each leg
const SOCKET_CHAIN := [0, 1, 2, 3, 4, 5, 11, 12, 13, 14]   # root, pelvis, spines, neck, head, near clavicle, arm, forearm, hand
const SPINE_CHAIN: Array = [0, 1, 2, 3, 4, 5]   # root, pelvis, spine_1, spine_2, neck, head: the flight lead reads the spine
const SOCKET_SET := ["root", "pelvis", "spine_1", "spine_2", "neck", "head", "clavicle_r", "upper_arm_r", "forearm_r", "hand_r"]

var _base: Array[Quaternion] = []
var _base_hips := Vector3.ZERO
var _base_curl := Vector2(0.5, 0.5)
var _tq: Array[Quaternion] = []
var _last_T: float = -1.0
var _cue: Dictionary = {}
var _seq: Dictionary = {}          # a pose sequence of Encounter's step 3 cues (data/anim/waves/step3.*): {id, t0, dur}
var _gc_hold_t0: float = -1.0           # a held ground-contact pose (the brace of a tumble) from this time ...
var _ag_sq: Dictionary = {}            # the agency slice's own sequence (the embed: its own, so a ground contact's sequence at the same tick cannot replace it)
var _lead_w: float = 0.0               # the flight lead (docs 9.23): how hard it is chasing its aim now, and the turn it holds (a world angle added to the sim's own, radians)
var _lead_d: float = 0.0
var _lead_vm := Vector2.ZERO           # his velocity as measured over the last sim tick, and where he was
var _lead_px: float = 0.0
var _lead_py: float = 0.0
var _lead_tk: int = -1
var pair_key: String = ""              # the launch-pair fighter this body plays as (data/anim/fighters.json: protagonist, antihero), "" for none (docs/animation/pair-live.md)
var _taunt_last: String = ""           # the last far taunt gesture he played (never the same one twice running)
var _taunt_n: int = 0                  # how many far taunts he has played this match
var _taunt_id: String = ""             # the far taunt gesture playing (his own, or the shared shrug)
var _en_pose: String = ""              # the energy layer's held pose (a charge, a release, a shot into a crater ...), eased in and out like the agency hold
var _en_prev: String = ""              # ... the pose it took over from, crossfaded over 0.08 s
var _en_sw: float = -10.0
var _en_t0: float = -1.0
var _en_t1: float = -1.0               # (-1: open, a charge held until the shot, a cancel or the cap)
var _en_in: float = 0.1
var _en_out: float = 0.15
var _en_w: float = 0.9
var _en_last_bolt: int = -100000       # the tick of his last bolt: a bolt inside the spray gap of it is a spray beat
var _ag_hold: String = ""              # the agency slice's held pose (a knockback, a charge: docs 9.22) from _ag_t0 ...
var _ag_kind: String = ""              # ... of this kind (a key of data/anim/agency.json)
var _ag_t0: float = -1.0
var _ag_t1: float = -1.0               # ... until this time (-1: until the exchange, a fall or the cap ends it)
var _ag_w: float = 0.0
var _ag_in: float = 0.1
var _ag_out: float = 0.15
var _ls_t0: float = -1.0               # the last stand's window open from this time (the held resolve), -1 when closed ...
var _ls_t1: float = -1.0               # ... and closed at this time (the resolve fades out)
var _intro: Dictionary = {}           # the opening, from the sim's events: {on, fall_t, fall_dur, land_t, stare_t, stare_dur} (seconds on the sim's tick)
var _gc_hold_w: float = 0.5            # how much of the pose the brace is (set when it starts: firmer when slow and unworn, looser when fast and worn)
var _gc_hold_t1: float = -1.0           # ... until this one (-1 while it lasts)
var _stun_prev: int = 0
var _stun_watch: int = 0           # ticks left to see this fighter staggered after a perfect block or a reversal (a DEFLECT staggers nobody)
var _stun_seq: String = ""
var _stun_t0: float = 0.0
var _shove_watch: int = 0          # ticks left to see the burst shove this fighter (a jump in his own speed)
var _shove_vx: float = 0.0
var _reacts: Array = []
var _sx := PackedFloat32Array([0.0, 0.0, 0.0, 0.0, 0.0])
var _sv := PackedFloat32Array([0.0, 0.0, 0.0, 0.0, 0.0])
var _spring_dt: float = 0.0
var _spring_bones := PackedInt32Array()
var _lag := PackedFloat32Array()
var _prof: Dictionary = {}
var _part: String = ""                 # the key set playing this frame, "" for none (for tools)
var debug := {"contact_frames": 0, "contact_err_max": 0.0, "parts": 0, "nan": 0, "ik_frames": 0, "gap_max": 0.0, "gap_sum": 0.0, "gap_n": 0, "face_flips": 0, "gaps": [], "notes": [], "blows": 0, "late": 0, "late_notes": [], "variants": {}, "wound_strikes": 0, "wound_bad": 0, "skims": 0, "ground_events": 0, "rd_ticks": 0, "rd_usec": 0, "catches": 0, "slope_frames": 0, "aims": 0, "pre_brace": 0, "near_miss": 0, "guard_kicks": 0, "personality": 0, "feet_usec": 0, "feet_n": 0, "survey": 0, "ko_falls": 0, "leans": 0, "overcommits": 0, "blocked": 0, "turn_steps": 0, "bursts": 0, "brakes": 0, "shocks": 0}

## The facing the mannequin is drawn with (-1 or 1). The sim's `face` can lag a dodge warp or a swap of sides, so this one
## is derived from the opponent in an exchange and from the travel direction otherwise (A2, docs/animation section 9.3).
var vface: float = 1.0
var _face_key: int = -1
var _face_init: bool = false
var _face_want_t: float = -1.0
const IDLE_FAR := 700.0               # an idle fighter farther than this from the opponent relaxes
const FACE_DEAD := 14.0                # closer than this (on the shortest arc) the opponent gives no side
const FACE_HOLD := 0.03                # a new side must hold this long (s) before the turn starts
const TRAVEL_FACE := 250.0             # free of an exchange, faster than this faces the travel direction
const NEAR_LOOK := 900.0               # an idle fighter this close looks at the opponent

# the blow being thrown this frame, for the contact solve: weight, limb, target region, mirror, the defender
# Inertialisation (A2): when the solved pose jumps (a part starts or ends, a cue or a reaction lands, a mirrored key set
# follows its other-side twin), the jump is taken as an offset that decays over a short eased time, so nothing pops and the new
# pose is reached by itself. The offset is dropped on a blow's contact frame, so the contact key stays exact.
const INERTIA_JUMP := 0.5              # rad: a bone turning more than this in one solve is a join
const INERTIA_SNAPPY := 0.1            # s, how long a join takes to settle on a snappy fighter
const INERTIA_FLUID := 0.18
var _ip_raw: Array[Quaternion] = []
var _ip_off: Array[Quaternion] = []
var _ip_hraw := Vector3.ZERO
var _ip_hoff := Vector3.ZERO
var _ip_cur: Array[Quaternion] = []
var _ip_hcur := Vector3.ZERO
var _ip_t: float = 0.0
var _ip_acc: float = 0.0              # tick time since the last solve; a hit-stop still lets a join settle, at half speed
var _ip_dur: float = 0.1
var _ip_active: bool = false
var _ip_have: bool = false
var _ip_cos: float = cos(INERTIA_JUMP * 0.5)
# The active ragdoll (overhaul unit A): stepped once per sim tick from the fighter's own state, shown at out_w.
var _rd := AnimRagdoll.new()
var _rd_have: bool = false
var _rd_vw := Vector2.ZERO              # last tick's world velocity (for the acceleration)
var _rd_prev_state: String = ""
var _rd_prev_slide: bool = false
var _rd_prev_speed: float = 0.0
var _skim_t0: float = -1.0
var _shape_set: bool = false
var _skip_inertia: bool = false
var _blow_snap: bool = false             # this solve is the snap of a blow into its contact key: the authored jump, not a join to settle
var _lean_id: int = -1                  # the dodge already leaned away from
var _over_id: int = -1                  # the whiff already over-committed on
var _lean_t0: float = -10.0             # sim time of the lean-away, the over-commit and the blocked recoil
var _over_t0: float = -10.0
var _block_t0: float = -10.0
var _bank: float = 0.0                 # roll into a turn (flight overhaul, unit G), smoothed
var _burst_t0: float = -10.0           # a dash start and a stop: when they were seen (sim time)
var _brake_t0: float = -10.0
var _shock_t0: float = -10.0           # a nearby impact: when, and how hard
var _shock_amp: float = 0.0
var _gait_speed: float = 0.0
var _turn_t0: float = -10.0            # a turn-around is a step: when the visual facing last flipped, and to which side
var _turn_dir: float = 1.0
var _stance_prev: int = -1
var _base_r := PackedFloat32Array()
var _hit_w: float = 0.0                 # how much of the fighter's own flinch shape is pulled in after a hit (decays)
var _hit_crum: float = 0.0              # ... and how much of it is the crumple (a heavy blow) rather than the brace (a light one)
var _miss_id: int = -1                  # the last near miss flinched at
var _sp_vw := Vector2.ZERO
var _sp_have: bool = false
var _look_target: float = 0.0
var _look: float = 0.0                 # the head's pitch toward the opponent, smoothed
var _leg_dq: Array[Quaternion] = []
var _leg_tick: int = -10
var _gf_gtick: int = -100
var _gf_gx: float = 0.0
var _gf_g0c: float = 0.0
var _gf_x_valid: bool = false
var _gf_x: float = 0.0
var _gf_face: float = 1.0
var _gf_tick: int = -100
var _gf_g0: float = 0.0
var _gf_dF: float = 0.0
var _gf_dB: float = 0.0
var _gf_sl: float = 0.0
var _foot_w: float = 0.0               # how much the feet are placed on a slope now (eases in and out)
var _foot_shift: float = 0.0           # the pelvis shift of the last placement (for tools)
var _foot_dh := Vector2.ZERO           # the ground under each foot relative to under him (for tools)
var _skid_pitch: float = 0.0
var _hit_n: int = 0                     # hits taken (the variant hash)
var _hit_free: float = 0.0              # how loose a hit leaves the limbs; decays, slower with wear
var layers: String = ""                # tools: which layers shaped this frame (set only with debug_checks)
var _rushing: bool = false
var _contact_now: bool = false
# Battle damage (rule-of-cool feature 1), read from the wounds state the sim already has: how worn the fighter is (breathing),
# how close to the brink (the stagger and the sagging stance), and whether the arms or the legs are broken (one arm hangs, one
# leg is favoured; which one is a hash of the fighter's slot, since the sim keeps no side).
var _worn: float = 0.0
var _brinkp: float = 0.0
var audit: Dictionary = {}               # joint audit (RenderAnim.joint_audit): stage -> the violations found there (A before the contact solve, B after it, C after the ragdoll, D after every layer)
var _arm_broken: bool = false
var _leg_broken: bool = false
var _hang_right: bool = false
var _leg_right: bool = false
var _form: Dictionary = {}           # the running transformation: {t0, version}
var _seen_tc: int = -1
var _cx_n: int = -1                  # the parried exchange whose parry time is remembered
var _cx_t: float = 0.0               # ... its exchange time when the parry was first seen
var _tc_left: float = -1.0            # seconds to the next blow's contact, -1 when none is coming
var _ci_w: float = 0.0
var _ci_limb: String = "hand_r"
var _ci_target: String = "chest"
var _ci_side: bool = false
var _ci_limb2: String = ""             # a second striking limb of a two-limb blow (a key set's limb2), or ""
var _ci_step: float = -1.0              # the key set's own step-in limit (model units), or -1 for the limb's default in sockets.json
var _ci_opp = null
var _ci_tc: float = 0.0
var _ci_dmg: float = 0.0
var _gap_tc: float = -1.0
var _recoil_y: float = 0.0
var _recoil_x: float = 0.0
var _step_x: float = 0.0               # the step-in a blow takes toward a defender beyond the arm and the hips' lunge


func _init(slot_: int) -> void:
	slot = slot_
	_hang_right = (_hash(slot_, 11, 3) & 1) == 0
	_leg_right = (_hash(slot_, 12, 5) & 1) == 0
	q = AnimPose.identity_q()
	_base = AnimPose.identity_q()
	_tq = AnimPose.identity_q()
	gq.resize(AnimRig.N)
	gp.resize(AnimRig.N)
	for nm in ["x_pack", "x_sash_a1", "x_sash_a2", "x_sash_b1", "x_sash_b2"]:
		_spring_bones.append(AnimRig.index[nm])
	_lag.resize(AnimRig.N)


# ------------------------------------------------------------------ events (called once a tick by RenderAnim)

func on_tick(dt: float, frozen: bool, S: SimState = null, f = null) -> void:
	_spring_dt += dt * (0.1 if frozen else 1.0)
	_ip_acc += dt * (0.5 if frozen else 1.0)
	if S != null and f != null:
		update_face(S, f)   # the facing is decided once per sim tick too, so everything keyed on it replays
		_wound_read(f)   # the wear is read on the tick too (it was read at solve time, so at two ticks a frame the first tick stepped the ragdoll on the last tick's wear)
		# a stagger with no cue of its own (the 12 ticks a blocked string leaves its attacker, the loser of a finisher's set-up): a short one
		var sn: int = int(f.stunTicks)
		if sn > 0 and _stun_prev == 0 and RenderAnim.step3_cues and _seq.is_empty() and _stun_watch == 0 and f.state == "free" and AnimData.entries.has("s3.stagger_short"):
			_seq = {"id": "s3.stagger_short", "t0": S.T, "dur": clampf(float(sn), 8.0, 30.0) / 60.0}
			debug["step3"] = int(debug.get("step3", 0)) + 1
		_stun_prev = sn
		if _stun_watch > 0:
			_stun_watch -= 1
			if S.T - _stag_t0 < 0.5:
				_stun_watch = 0   # the stagger cue has played it, fitted to its ticks: the watch yields
			elif int(f.stunTicks) > 0 and AnimData.entries.has(_stun_seq):
				_seq = {"id": _stun_seq, "t0": _stun_t0, "dur": float(AnimData.entries[_stun_seq].dur) / 60.0}
				debug["step3"] = int(debug.get("step3", 0)) + 1
				_stun_watch = 0
		if _shove_watch > 0:
			_shove_watch -= 1
			if absf(f.vx - _shove_vx) > 250.0 and AnimData.cue_map.has("burst") and AnimData.entries.has(String(AnimData.cue_map.burst.get("other_if_shoved", ""))):
				var sid2: String = String(AnimData.cue_map.burst.other_if_shoved)
				_seq = {"id": sid2, "t0": S.T, "dur": float(AnimData.entries[sid2].dur) / 60.0}
				_shove_watch = 0
		# a change of stance kicks the arms toward the new guard on the tick it happens (it was read at solve time, so a change on an odd tick
		# came a tick late at two ticks a frame and the ragdoll state differed)
		var st_now: int = clampi(int(f.stance), 0, 3)
		if _stance_prev != st_now:
			if _stance_prev >= 0 and RenderAnim.layer("transitions"):
				_stance_kick(_stance_prev, st_now)
			_stance_prev = st_now
		_spring_tick(S, f, dt, frozen)
		if not frozen:
			_rd_tick(S, f, dt)
			if RenderAnim.layer("look"):
				_look_tick(S, f, dt)


## A crater was dug near him (the sim's crater event): a hover shudders and the arms start; amp 0 to 1 from the energy and the distance.
func on_shock(T: float, amp: float) -> void:
	if amp <= 0.02:
		return
	_shock_t0 = T
	_shock_amp = minf(1.0, _shock_amp * exp(-(T - _shock_t0) / 0.5) + amp)
	var a: float = _rd_amp() * amp
	_rd.kick(2, 4.0 * a)
	_rd.kick(5, 4.0 * a)
	_rd.kick(1, -2.0 * a)
	_rd.out_w = 1.0
	debug["shocks"] += 1


## A launched fighter skipped off water (the sim's skim event): the body arches and flares for an instant.
func on_skim(T: float, spd: float) -> void:
	_skim_t0 = T
	var amp: float = _rd_amp()
	_rd.kick(0, 5.0 * amp)
	_rd.kick(1, 6.0 * amp)
	_rd.kick(2, -4.0 * amp)
	_rd.kick(5, -4.0 * amp)
	debug["skims"] += 1


## World's ground-contact events (left_ground, bounce, land, tumble_end; docs/world/ground-contact.md section 4). They plug
## into the same body: a bounce whips it by the normal speed, a slam or a skid folds it, the end of a tumble lets it go.
## The agency slice's events (data/anim/agency.json): a knockback holds a pose for as long as he is sent back, the embed plays its sequence over the held-down
## ticks, the far taunt a shrug over the flight, the charges a held flight pose until the exchange starts; a feint peels off (docs/director/agency-slice-3.md).
func on_agency(kind: String, T: float, e: Dictionary) -> void:
	var A: Dictionary = AnimData.agency
	if not RenderAnim.agency_poses or A.is_empty():
		return
	var wt: float = 0.6 if RenderAnim.reduced_motion else 1.0
	# the cues that end a taunt (his dodge cut it, it was answered, it took off into a charge) or a charge (the dodge-cancel, a meeting in the middle) at once
	if A.get("taunt", {}).get("cut_kinds", []).has(kind) and _is_taunt(String(_seq.get("id", ""))):
		_seq = {}
	if A.get("charge", {}).get("end_kinds", []).has(kind) and _ag_kind.begins_with("charge_") and _ag_t1 < 0.0:
		_ag_t1 = T
		return
	match kind:
		"knockback":
			var kk: Dictionary = A.get("knockback", {}).get(String(e.get("kind", "slideShort")), {})
			if kk.is_empty() or not AnimData.pose_exists(String(kk.hold)):
				return
			_ag_hold = String(kk.hold)
			_ag_kind = "knockback"
			_ag_t0 = T
			_ag_t1 = T + clampf(float(e.get("dur", 0.5)), 0.1, 3.0)
			_ag_w = float(kk.weight) * wt
			_ag_in = float(kk.get("in", 0.08))
			_ag_out = float(kk.get("out", 0.15))
			debug["agency"] = int(debug.get("agency", 0)) + 1
		"embed":
			var em: Dictionary = A.get("embed", {})
			if AnimData.entries.has(String(em.get("seq", ""))):
				# starts after the landing's own bounce (the first tenth of a second), and runs to the end of the held-down time
				var dur: float = maxf(float(e.get("dur", 1.0)) - 0.12, 0.3)
				_ag_sq = {"id": String(em.seq), "t0": T + 0.12, "dur": dur, "wt": float(em.get("weight", 1.0)) * wt}
				debug["agency"] = int(debug.get("agency", 0)) + 1
		"taunt_start":
			var tn: Dictionary = A.get("taunt", {})
			var tid: String = String(tn.get("seq", ""))
			var gest: Array = AnimData.pair.get("roles", {}).get(pair_key, {}).get("taunts", []) if (RenderAnim.pair_live and pair_key != "") else []
			if not gest.is_empty():   # his own far taunts (docs/animation/pair-live.md): the first of a stand-off is his first gesture; after that one he may use, by a hash of the tick, never the same twice running
				var air: bool = bool(e.get("air", false))
				var first: String = String(AnimData.pair.get("roles", {}).get(pair_key, {}).get("taunt_first", ""))
				var gid: String = ""
				if first != "" and _taunt_n == 0 and not bool(e.get("traded", true)) and AnimData.entries.has(first):
					gid = first
				else:
					var elig: Array = []
					for g in gest:
						var gate: String = String(AnimData.entries.get(String(g), {}).get("_gate", ""))
						if (gate == "ground" and air) or (gate == "air" and not air) or (String(g) == _taunt_last and gest.size() > 1):
							continue
						elig.append(String(g))
					if not elig.is_empty():
						gid = String(elig[_hash(int(T * 60.0), slot, 29) % elig.size()])
				if gid != "" and AnimData.entries.has(gid):
					tid = gid
					_taunt_last = gid
					_taunt_n += 1
			if AnimData.entries.has(tid):
				_taunt_id = tid
				_seq = {"id": tid, "t0": T, "dur": float(AnimData.entries[tid].dur) / 60.0, "wt": float(tn.get("weight", 0.7)) * wt}
				debug["agency"] = int(debug.get("agency", 0)) + 1
		"charge_light", "charge_heavy", "charge_feint":
			var which: String = kind.substr(7)
			var cc: Dictionary = A.get("charge", {}).get(which, {})
			if cc.is_empty() or not AnimData.pose_exists(String(cc.hold)):
				return
			_ag_hold = String(cc.hold)
			_ag_kind = "charge_" + which
			_ag_t0 = T
			_ag_t1 = (T + float(cc.ticks) / 60.0) if cc.has("ticks") else -1.0
			_ag_w = float(cc.weight) * wt
			_ag_in = float(cc.get("in", 0.1))
			_ag_out = float(cc.get("out", 0.15))
			debug["agency"] = int(debug.get("agency", 0)) + 1


## The flight lead (GB-006, docs/animation/pose-pipeline.md 9.23): the sim's body spin halves every 0.5 s and leaves a launched fighter at whatever angle the tumble
## stopped at, still flying at speed: feet first, laid out along the flight line. Once the spin is low and the speed high the whole body turns about its middle (the
## view's pivot) until the head leads along the velocity, never the feet: blown back he flies on his back, head first. Landing into a skid it turns upright. The turn
## `_lead_d` is a world-space angle added to the sim's own (the view's pivot turns by -rot): it chases its aim by the short way, holds when the flight slows and
## is let go when the launch ends. It is the root's rotation and a shift that keeps the middle in place. Read from the sim's state only.
func _lead_layer(S: SimState, f, dt: float) -> void:
	var L: Dictionary = AnimData.flight
	if L.is_empty() or not RenderAnim.flight_lead:
		_lead_d = 0.0
		_lead_w = 0.0
		return
	var rate: float = float(L.get("rate", 6.0))
	# the spine's own angle in the pose as it stands (pelvis to head, model space, beta = 0 upright), and on screen with the sim's turn and the turn held
	AnimPose.fk_chain(q, hips, gq, gp, SPINE_CHAIN)
	var ax: Vector3 = gp[5] - gp[1]
	var beta: float = atan2(-ax.x * vface, ax.y)
	var net: float = beta - f.rot + _lead_d
	var gain: float = 0.0
	var aim: float = 0.0
	if f.state == "launched":
		var v := Vector2(f.vx, f.vy)
		var sp: float = v.length()
		var head_first: float = atan2(-v.x, v.y)   # the spine along the velocity: (-sin a, cos a) = v
		if f.slide > 0.0:
			# landing into a skid: the pose's own lean without the sim's turn (it is easing him upright), only faster; a body the pose lays out along the skid is flown head first
			var lay: float = smoothstep(float(L.lay[0]), float(L.lay[1]), absf(sin(beta)))
			aim = beta + lay * wrapf(head_first - beta, -PI, PI) if sp > 1.0 else beta
			gain = float(L.get("skid_rate", 20.0)) / maxf(rate, 0.001)
		else:
			var spd: Array = L.speed
			var spn: Array = L.spin
			gain = smoothstep(float(spd[0]), float(spd[1]), sp) * (1.0 - smoothstep(float(spn[0]), float(spn[1]), absf(f.spin)))
			if f.vy < 0.0:
				var st: Array = L.steep
				gain *= 1.0 - smoothstep(float(st[0]), float(st[1]), -f.vy / maxf(absf(f.vx), 1.0))
			aim = head_first
		_lead_d += wrapf(aim - net, -PI, PI) * (1.0 - exp(-dt * rate * gain))
	else:
		# on his feet or flying free and carried fast (a knockback moves him by position, not by velocity): a body folded or laid out with the feet leading is stood up
		# to a tilt of `tilt` degrees at most; otherwise the turn is let go, back to the sim's own angle, the short way
		var want_d: float = 0.0
		var vm: Vector2 = _lead_measure(S, f)
		var spm: float = vm.length()
		var a0: float = wrapf(beta - f.rot, -PI, PI)
		if spm > 1.0 and absf(a0) < 2.1:
			var cap: float = deg_to_rad(float(L.get("tilt", 35.0)))
			var dot0: float = (-sin(a0) * vm.x + cos(a0) * vm.y) / spm
			var spd2: Array = L.speed
			var g: float = smoothstep(float(spd2[0]), float(spd2[1]), spm) * smoothstep(-0.1, -0.5, dot0)
			want_d = g * (clampf(a0, -cap, cap) - a0)
			gain = g
		_lead_d += (want_d - _lead_d) * (1.0 - exp(-dt * float(L.get("tilt_rate", 45.0))))   # (quick: a knockback is over in a few frames)
	_lead_d = wrapf(_lead_d, -PI, PI)
	_lead_w = gain
	if absf(_lead_d) < 0.0005:
		_lead_d = 0.0
		return
	var phi: float = vface * _lead_d   # a turn of the model about z shows as vface * phi on screen
	q[0] = Quaternion(Vector3(0, 0, 1), phi) * q[0]
	var py: float = float(L.get("pivot_y", 34.0))
	root_off += Vector3(py * sin(phi), py * (1.0 - cos(phi)), 0.0)
	debug["lead"] = int(debug.get("lead", 0)) + 1
	debug["lead_w"] = gain
	debug["lead_d"] = _lead_d
	debug["lead_phi"] = phi


## The fighter's velocity as the sim moved him over the last tick: a knockback and a rush carry a fighter by position and leave f.vx as it was.
func _lead_measure(S: SimState, f) -> Vector2:
	if S.tick != _lead_tk:
		var gap: int = S.tick - _lead_tk
		if _lead_tk >= 0 and gap > 0 and gap <= 4:
			_lead_vm = Vector2(SimWrap.sdx(_lead_px, f.x), f.y - _lead_py) * (60.0 / float(gap))
		else:
			_lead_vm = Vector2.ZERO
		_lead_tk = S.tick
		_lead_px = f.x
		_lead_py = f.y
	return _lead_vm


## A far taunt's sequence: the shared shrug, or one of his own gestures.
func _is_taunt(id: String) -> bool:
	return id != "" and (id == String(AnimData.agency.get("taunt", {}).get("seq", "")) or id == _taunt_id)


## His own pick (docs/animation/pair-live.md): the key set of a blow from his light or heavy list (every strike of his strike waves, never the tail), walked as go-live
## step 1's lists are: the walk starts at a hash of the exchange and the fighter and takes every third entry (every fifth when the list is a multiple of three), so
## nothing repeats inside ten blows; a two-limb key set is skipped when a limb of its kind is broken; the gated key sets join only while their gate is open. "" when he
## is not one of the pair, the live pair is off, or no key set of his fits.
func _pair_pick(S: SimState, f, ex, ordinal: int, heavy: bool, piece: String = "") -> String:
	if pair_key == "" or not RenderAnim.pair_live or not AnimData.pair_lists.has(pair_key):
		return ""
	var lst: Dictionary = AnimData.pair_lists[pair_key]
	# the planner's own pick, when the beat carries it (args.piece, "strike.jab": the alchemist's beats will)
	var named: String = AnimData.resolve_strike(pair_key, String(piece))
	if named != "":
		debug["pair_picks"] = int(debug.get("pair_picks", 0)) + 1
		return named
	var which: String = "heavy" if heavy else "light"
	var cand: Array = (lst[which] as Array).duplicate()
	var opp = ex.D if ex.A == f else ex.A
	for g in lst.gated:
		if String(g.weight) != which:
			continue
		var open: bool = false
		match String(g.gate):
			"both_ground":
				open = f.y - WorldTerrain.groundY(S, f.x) < 8.0 and opp != null and opp.y - WorldTerrain.groundY(S, opp.x) < 8.0
			"striker_air":
				open = f.y - WorldTerrain.groundY(S, f.x) > 20.0
		if open:
			cand.append(String(g.id))
	var n: int = cand.size()
	if n == 0:
		return ""
	var stepw: int = 3 if n % 3 != 0 else 5
	var h: int = _hash(int(ex.n), slot, 11)
	for tries in range(n):
		var id: String = String(cand[(h + stepw * (ordinal + tries)) % n])
		var ks = AnimData.keysets.get(id)
		if ks == null:
			continue
		var two: String = String(ks.get("limb2", ""))
		if two != "" and ((_arm_broken and (two.begins_with("hand") or two.begins_with("elbow"))) or (_leg_broken and (two.begins_with("foot") or two.begins_with("knee")))):
			continue
		debug["pair_picks"] = int(debug.get("pair_picks", 0)) + 1
		return id
	return ""


## ---- the energy press and the pair's own events (data/anim/pair_live.json; docs/animation/pair-live.md). The sim's cues blast_windup, blast_charge, blast_full, blast_cancel and
## buried_blast, its shot_fire and shot_deflect events, and the cues it does not send yet (taunt_close, drop_the_act, regalia_release, energy_shove) start the pose or
## sequence of his own that his role names. Inside an exchange none of them play: a blast press there is a link of the string, drawn by the strike's own poses.
func _en_role(role: String) -> Dictionary:
	if pair_key == "" or not RenderAnim.pair_live:
		return {}
	return AnimData.pair.get("roles", {}).get(pair_key, {}).get(role, {})


func _en_busy(S: SimState, f) -> bool:
	var ex = S.dirS.ex
	return ex != null and (ex.A == f or ex.D == f)


func _en_play_seq(id: String, t0: float) -> void:
	if AnimData.entries.has(id):
		_seq = {"id": id, "t0": t0, "dur": float(AnimData.entries[id].dur) / 60.0, "wt": 0.6 if RenderAnim.reduced_motion else 1.0}
		debug["energy"] = int(debug.get("energy", 0)) + 1


## A pose held and eased: a fresh hold, or (while one is on) a switch to the next pose of the same envelope (the charge to the flash to the release).
func _en_hold(pose: String, T: float, ticks: float, tin: float, tout: float, w: float) -> void:
	if not AnimData.pose_exists(pose):
		return
	var on: bool = _en_t0 >= 0.0 and (_en_t1 < 0.0 or T < _en_t1 + _en_out)
	if on:
		_en_prev = _en_pose
		_en_sw = T
	else:
		_en_prev = ""
		_en_t0 = T
		_en_in = tin
	_en_pose = pose
	_en_t1 = (T + ticks / 60.0) if ticks > 0.0 else -1.0
	_en_out = tout
	_en_w = w * (0.6 if RenderAnim.reduced_motion else 1.0)
	debug["energy"] = int(debug.get("energy", 0)) + 1


func _en_hold_spec(spec: Dictionary, T: float) -> void:
	_en_hold(String(spec.get("pose", "")), T, float(spec.get("ticks", 20)), float(spec.get("in", 0.06)), float(spec.get("out", 0.15)), float(spec.get("w", 0.9)))


func on_blast_cue(kind: String, T: float, S: SimState, f) -> void:
	if not RenderAnim.pair_live or pair_key == "":
		return
	var act: String = String(AnimData.pair.get("cues", {}).get(kind, ""))
	var busy: bool = _en_busy(S, f)
	match act:
		"windup":
			if not busy and _en_role("bolt").has("seq"):
				_en_play_seq(String(_en_role("bolt").seq), T)
		"charge":
			var rc: Dictionary = _en_role("charged")
			if not busy and not rc.is_empty():
				_en_hold(String(rc.get("charge", "")), T, -1.0, 0.12, 0.15, 0.9)
		"full":
			var rf: Dictionary = _en_role("charged")
			if not rf.is_empty() and _en_t0 >= 0.0 and _en_t1 < 0.0:
				_en_hold(String(rf.get("full", "")), T, -1.0, 0.12, 0.15, 0.9)
		"cancel":
			if _en_t0 >= 0.0 and _en_t1 < 0.0:
				_en_t1 = T
		"crater":
			_en_hold_spec(_en_role("crater"), T)
		"volley":
			if not busy and _en_role("volley").has("seq"):
				_en_play_seq(String(_en_role("volley").seq), T)
		"taunt_close":
			var rt: Dictionary = _en_role("taunt_close")
			if f.y - WorldTerrain.groundY(S, f.x) > 20.0 and _en_role("taunt_close_air").has("seq"):   # in the air its first phase needs the ground: his own start for it, in the same 60 ticks
				rt = _en_role("taunt_close_air")
			if rt.has("seq"):
				_en_play_seq(String(rt.seq), T)
				_taunt_id = String(rt.seq)
		"drop_the_act":
			if _en_role("drop_the_act").has("seq"):
				_en_play_seq(String(_en_role("drop_the_act").seq), T)
		"crown_release":
			_en_hold_spec(_en_role("crown_release"), T)
		"shove":
			_en_hold_spec(_en_role("shove"), T)


## A shot left him (`link` the volley's group): its kind picks the role. A bolt is the wind-up's sequence aligned so that its fire beat is the shot; a bolt inside the spray
## gap of his last one is a spray beat; a charged shot's release takes over from the charge.
func on_shot(kind: String, T: float, S: SimState, f, tick: int, link: int) -> void:
	if not RenderAnim.pair_live or pair_key == "" or _en_busy(S, f):
		return
	var role: String = String(AnimData.pair.get("kinds", {}).get(kind, ""))
	if role == "":
		return
	match role:
		"bolt":
			var gap: int = tick - _en_last_bolt
			_en_last_bolt = tick
			if gap >= 0 and gap < int(AnimData.pair.get("spray_gap", 10)) and _en_role("spray").has("seq"):
				_en_play_seq(String(_en_role("spray").seq), T)
			elif _en_role("bolt").has("seq"):
				_en_play_seq(String(_en_role("bolt").seq), T - float(_en_role("bolt").get("wind_ticks", 4)) / 60.0)
		"charged":
			var rr: Dictionary = _en_role("charged").get("release", {})
			if not rr.is_empty():
				_en_hold_spec(rr, T)
		"rain":
			_en_hold_spec(_en_role("rain"), T)
		_:
			if _en_role(role).has("seq"):
				_en_play_seq(String(_en_role(role).seq), T)


## He swatted a shot wild (the wild deflect): the swat, unless the perfect block's own sequence is playing.
func on_deflect(T: float, S: SimState, f) -> void:
	if not RenderAnim.pair_live or pair_key == "" or _en_busy(S, f) or String(_seq.get("id", "")).begins_with("s3."):
		return
	if _en_role("swat").has("seq"):
		_en_play_seq(String(_en_role("swat").seq), T)


## The held energy pose: eased in, held, eased out (an open charge ends in an exchange, a launch, a fall or after 2.5 s: the sim's hold cap is 45 ticks).
func _energy_layer(S: SimState, f, T: float) -> void:
	if _en_t0 < 0.0 or not AnimData.pose_exists(_en_pose):
		return
	if _en_t1 < 0.0 and (T - _en_t0 > 2.5 or f.state == "launched" or f.state == "down" or _en_busy(S, f)):
		_en_t1 = T
	var wgt: float = smoothstep(0.0, _en_in, T - _en_t0)
	if _en_t1 >= 0.0:
		wgt *= 1.0 - smoothstep(0.0, _en_out, T - _en_t1)
		if T - _en_t1 >= _en_out:
			_en_t0 = -1.0
			_en_t1 = -1.0
			_en_pose = ""
			_en_prev = ""
			return
	wgt *= _en_w
	if wgt <= 0.001:
		return
	var u: float = smoothstep(0.0, 0.08, T - _en_sw)
	if _en_prev != "" and u < 1.0 and AnimData.pose_exists(_en_prev):
		var pp: AnimPose = AnimData.pose(_en_prev)
		AnimPose.mix(q, pp.q, wgt * (1.0 - u))
		hips = hips.lerp(pp.hips, wgt * (1.0 - u))
		curl = curl.lerp(pp.curl, wgt * (1.0 - u))
	var ap: AnimPose = AnimData.pose(_en_pose)
	AnimPose.mix(q, ap.q, wgt * u)
	hips = hips.lerp(ap.hips, wgt * u)
	curl = curl.lerp(ap.curl, wgt * u)


## The held agency pose: eased in, held, eased out. A charge ends by itself when its exchange starts (the sim begins it at the wind-up), when he falls or at its cap.
func _agency_layer(S: SimState, f, T: float) -> void:
	if not _ag_sq.is_empty():
		if T > float(_ag_sq.t0) + float(_ag_sq.dur) + 0.1:
			_ag_sq = {}
		elif T >= float(_ag_sq.t0):
			_entry_layer(float(_ag_sq.t0), float(_ag_sq.dur), String(_ag_sq.id), T, DT, float(_ag_sq.wt))
	if _ag_t0 < 0.0 or not AnimData.pose_exists(_ag_hold):
		return
	var cfg: Dictionary = AnimData.agency
	if _ag_kind.begins_with("charge_") and _ag_t1 < 0.0:
		var cap: float = float(cfg.get("charge", {}).get(_ag_kind.substr(7), {}).get("max_ticks", 90)) / 60.0
		var ex = S.dirS.ex
		if T - _ag_t0 > cap or f.state == "launched" or f.state == "down" or (ex != null and (ex.A == f or ex.D == f)):
			_ag_t1 = T
	var wgt: float = smoothstep(0.0, _ag_in, T - _ag_t0)
	if _ag_t1 >= 0.0:
		wgt *= 1.0 - smoothstep(0.0, _ag_out, T - _ag_t1)
		if T - _ag_t1 >= _ag_out:
			_ag_t0 = -1.0
			_ag_t1 = -1.0
			return
	wgt *= _ag_w
	if wgt > 0.001:
		var ap: AnimPose = AnimData.pose(_ag_hold)
		AnimPose.mix(q, ap.q, wgt)
		hips = hips.lerp(ap.hips, wgt)
		curl = curl.lerp(ap.curl, wgt)


## The last stand (docs/architecture/last-stand.md): ready starts his steadying beat in his own shape and the held resolve; end `used` lets the resolve go
## (the signature's own animation takes over), `expired` slumps him.
func on_last_stand(kind: String, t: float, dur: float, end_kind: String) -> void:
	var L: Dictionary = AnimData.last_stand
	if not RenderAnim.last_stand_poses or L.is_empty():
		return
	if kind == "last_stand_ready":
		var sk: String = _rd.shape_key
		var sid: String = String(L.get("ready", {}).get(sk, L.get("ready", {}).get("default", "")))
		if AnimData.entries.has(sid):
			_seq = {"id": sid, "t0": t, "dur": float(AnimData.entries[sid].dur) / 60.0, "wt": 0.6 if RenderAnim.reduced_motion else 1.0}
			debug["last_stand"] = int(debug.get("last_stand", 0)) + 1
		_ls_t0 = t
		_ls_t1 = -1.0
	elif kind == "last_stand_end":
		if _ls_t0 >= 0.0 and _ls_t1 < 0.0:
			_ls_t1 = t
		if end_kind == "expired" and AnimData.entries.has("ls.slump"):
			_seq = {"id": "ls.slump", "t0": t, "dur": float(AnimData.entries["ls.slump"].dur) / 60.0, "wt": 0.6 if RenderAnim.reduced_motion else 1.0}


## The opening's pose from the sim's tick: waiting or falling (the body one narrow line, head first), the landing (a deep compression with one open hand on
## the ground and the head up, held, then up through a half crouch), the staredown (upright and loose, one small beat of character for his shape, then the
## tension in the last stretch before the clock). Reduced motion: no fall and no landing: he drops into frame and stands.
func _intro_layer(S: SimState, f) -> void:
	var I: Dictionary = AnimData.intro
	var tk: float = float(S.tick) / 60.0
	var reduced: bool = RenderAnim.reduced_motion
	var dq: float = 1.0 / 60.0
	var set_p: AnimPose = AnimData.pose("in.hold.set")
	var landed: bool = float(_intro.land_t) >= 0.0 and tk >= float(_intro.land_t)
	var pose_in: AnimPose = AnimData.pose("in.hold.fall") if not reduced else set_p
	if not landed:
		AnimPose.mix(q, pose_in.q, 1.0)
		hips = pose_in.hips
		curl = pose_in.curl
		return
	var tl: float = tk - float(_intro.land_t)
	var land_dur: float = float(AnimData.entries["in.land"].dur) / 60.0 if AnimData.entries.has("in.land") else 1.0
	if tl < land_dur and not reduced and AnimData.entries.has("in.land"):
		if tl < 0.12:
			_skip_inertia = true   # the touchdown snaps (the join from the fall is the blow of the landing, not something to ease)
		_entry_layer(float(_intro.land_t), land_dur, "in.land", tk, dq)
		return
	AnimPose.mix(q, set_p.q, 1.0)
	hips = set_p.hips
	curl = set_p.curl
	var st: float = float(_intro.stare_t)
	if st < 0.0:
		return
	# the tension in the last stretch of the staredown
	var tense_p: AnimPose = AnimData.pose("in.hold.tense")
	var lead: float = float(I.get("tense_lead", 1.8))
	var ramp: float = float(I.get("tense_ramp", 0.8))
	var t_end: float = st + float(_intro.stare_dur)
	var tw: float = smoothstep(t_end - lead, t_end - lead + ramp, tk) if not reduced else 0.0
	if tw > 0.001:
		AnimPose.mix(q, tense_p.q, tw)
		hips = hips.lerp(tense_p.hips, tw)
		curl = curl.lerp(tense_p.curl, tw)
	# one small beat of character for his shape, early in the staredown
	var sk: String = String(AnimRagdoll.shape_of.get(String(f.id), AnimRagdoll.shape_of.get("default", "")))
	var beat: Dictionary = I.get("beats", {}).get(sk, {})
	if not beat.is_empty() and not reduced and AnimData.entries.has(String(beat.get("seq", ""))):
		var b0: float = st + float(beat.get("at", 1.0))
		var bd: float = float(AnimData.entries[String(beat.seq)].dur) / 60.0
		if tk >= b0 and tk < b0 + bd + 0.05:
			_entry_layer(b0, bd, String(beat.seq), tk, dq)


## The opening's events (docs/architecture/intro-phase.md), at their own tick in seconds: the match clock is frozen, so this is the only time there is.
func on_intro(kind: String, t: float, dur: float, clock_kind: String) -> void:
	if not RenderAnim.intro_poses or AnimData.intro.is_empty():
		return
	match kind:
		"intro_start":
			_intro = {"on": true, "fall_t": -1.0, "fall_dur": 0.6, "land_t": -1.0, "stare_t": -1.0, "stare_dur": 2.6}
		"entrance_fall":
			if not _intro.is_empty():
				_intro["fall_t"] = t
				_intro["fall_dur"] = maxf(dur, 0.1)
		"entrance_land":
			if not _intro.is_empty():
				_intro["land_t"] = t
		"staredown_start":
			if not _intro.is_empty():
				_intro["stare_t"] = t
				_intro["stare_dur"] = maxf(dur, 0.5)
		"clock_start":
			_intro = {}


func on_ground_event(kind: String, e: Dictionary, T: float) -> void:
	var amp: float = _rd_amp()
	debug["ground_events"] += 1
	var G: Dictionary = AnimData.ground
	var poses_on: bool = RenderAnim.ground_poses and not G.is_empty() and AnimData.entries.has("gc.bounce")
	var surf: float = float(G.get("surface", {}).get(String(e.get("surface", "soil")), 1.0))
	match kind:
		"bounce":
			var vn: float = absf(float(e.get("vn", 0.0)))
			_rd.crumple(clampf(vn / 2500.0, 0.2, 1.5) * 0.6 * surf, amp)
			# the tangent speed whips the body the way it travels
			_rd.kick(1, clampf(float(e.get("vt", 0.0)) * vface / 220.0, -9.0, 9.0) * amp)
			if poses_on:
				var bw: float = clampf(vn / float(G.get("bounce", {}).get("ref", 1800.0)), float(G.get("bounce", {}).get("min", 0.35)), 1.0) * clampf(surf, 0.6, 1.0)
				_seq = {"id": "gc.bounce", "t0": T, "dur": float(AnimData.entries["gc.bounce"].dur) / 60.0, "wt": bw * (0.6 if RenderAnim.reduced_motion else 1.0)}
				debug["gc_poses"] = int(debug.get("gc_poses", 0)) + 1
		"left_ground":
			var cause: String = String(e.get("cause", ""))
			if poses_on and cause != "bounce" and cause != "":
				var lw: float = clampf(float(e.get("spd", 1000.0)) / float(G.get("launch", {}).get("ref", 2000.0)), float(G.get("launch", {}).get("min", 0.4)), 1.0)
				_seq = {"id": "gc.lip_launch", "t0": T, "dur": float(AnimData.entries["gc.lip_launch"].dur) / 60.0, "wt": lw * (0.6 if RenderAnim.reduced_motion else 1.0)}
				_gc_hold_t1 = T if _gc_hold_t0 >= 0.0 and _gc_hold_t1 < 0.0 else _gc_hold_t1
				debug["gc_poses"] = int(debug.get("gc_poses", 0)) + 1
		"land":
			var lk: String = String(e.get("kind", ""))
			if lk == "slam":
				_rd.crumple(1.0, amp)
				if poses_on:
					_seq = {"id": "gc.bounce", "t0": T, "dur": float(AnimData.entries["gc.bounce"].dur) / 60.0, "wt": 1.0}
			elif lk == "skid":
				_rd.crumple(0.5, amp)
			elif lk == "tumble":
				_rd.crumple(0.5 * surf, amp)
				if poses_on and (_gc_hold_t0 < 0.0 or _gc_hold_t1 >= 0.0):
					_gc_hold_t0 = T
					_gc_hold_t1 = -1.0
					var H: Dictionary = G.get("hold", {})
					var spd_k: float = lerpf(float(H.get("slow_k", 1.4)), float(H.get("fast_k", 0.7)), smoothstep(float(H.get("slow_speed", 300.0)), float(H.get("fast_speed", 1500.0)), float(e.get("spd", 800.0))))
					var wear_k: float = 1.0 - float(H.get("worn_loosen", 0.5)) * smoothstep(float(H.get("worn_from", 0.5)), 1.0, maxf(_worn, _brinkp))
					_gc_hold_w = clampf(float(H.get("weight", 0.5)) * spd_k * wear_k, 0.0, float(H.get("max", 0.85)))
					if RenderAnim.reduced_motion:
						_gc_hold_w *= 0.6
					debug["gc_poses"] = int(debug.get("gc_poses", 0)) + 1
		"tumble_end":
			_rd.free = minf(_rd.free, 0.3)
			if _gc_hold_t0 >= 0.0 and _gc_hold_t1 < 0.0:
				_gc_hold_t1 = T
			if poses_on and String(e.get("kind", "")) == "recover" and AnimData.entries.has("gc.tech_flip"):
				_seq = {"id": "gc.tech_flip", "t0": T, "dur": float(AnimData.entries["gc.tech_flip"].dur) / 60.0, "wt": 1.0}
				debug["gc_poses"] = int(debug.get("gc_poses", 0)) + 1
		"journey_end":
			if _gc_hold_t0 >= 0.0 and _gc_hold_t1 < 0.0:
				_gc_hold_t1 = T


func on_cue(kind: String, T: float, t_event: float = -1.0) -> void:
	if RenderAnim.step3_cues and AnimData.cue_map.has(kind):
		var sid: String = String(AnimData.cue_map[kind].get("actor", ""))
		if AnimData.entries.has(sid):
			_seq = {"id": sid, "t0": t_event if t_event >= 0.0 else T, "dur": float(AnimData.entries[sid].dur) / 60.0}
			debug["step3"] = int(debug.get("step3", 0)) + 1
		return
	if AnimData.cue_poses.has(kind) and RenderLook.CUE_POSES.has(kind):
		_cue = {"kind": kind, "t0": T, "dur": float(RenderLook.CUE_POSES[kind].dur)}


## The stagger cue's perfect_block and reversal texts (press-styles.md section 12): the fighter who is staggered (the cue's actor) reels in place for `n` ticks, the step 3 stagger fitted to them (every
## phase scaled together, so the whole reel takes the sim's ticks: a 24-tick sequence at 20 plays at 0.83, a 16-tick one at 1.25). The old stunTicks watch yields to it. Other staggers (flurry, heavy) are the
## stunTicks path, as before.
func on_stagger(text: String, n: int, T: float) -> void:
	if not RenderAnim.step3_cues or n <= 0:
		return
	var id: String = "s3.stagger_blocked" if text == "perfect_block" else ("s3.stagger_countered" if text == "reversal" else "")
	if id == "" or not AnimData.entries.has(id):
		return
	_seq = {"id": id, "t0": T, "dur": float(n) * DT, "wt": 1.0, "fit": true}
	_stun_watch = 0
	_stag_t0 = T
	debug["step3"] = int(debug.get("step3", 0)) + 1
	debug["staggers"] = int(debug.get("staggers", 0)) + 1


## The riposte cue (the blocker's counter blow is coming): kept for VFX until the blow's own beat takes over; the posing is the beat's. `n` is the contact tick: a tick number after the
## cue's own, or the ticks ahead when it is smaller (the sim has not fixed which; see the report).
func on_riposte(text: String, n: int, tick_now: int) -> void:
	var ahead: int = n - tick_now if n > tick_now else n
	_rip_cue = {"kind": text if text == "heavy" else "light", "tick": tick_now, "ahead": maxi(ahead, 0)}


## A riposte's or a reversal's beat: the style and grade the data gives a riposte when the beat names none (a perfect timing: tech and perfect for a light, heavy for a heavy), and `_rip` marks it
## for the strike block (the wind-up cut to the data's 2 ticks or 3, no squash). Any other beat is returned as it is.
func _rip_args(args: Dictionary, cls: String) -> Dictionary:
	if not (bool(args.get("riposte", false)) or bool(args.get("reversal", false))):
		return args
	var R: Dictionary = AnimData.press.get("riposte", {})
	if R.is_empty():
		return args
	var a: Dictionary = args.duplicate()
	a["_rip"] = true
	if not a.has("style") and R.has("style"):
		a["style"] = String((R.style as Dictionary).get("heavy" if cls == "heavy" else "light", ""))
	if not a.has("grade") and R.has("grade"):
		a["grade"] = String(R.grade)
	return a


## How much of the block pose is left (1 to 0): it fades over the riposte's `fade` ticks ending on the blow's contact tick, and is gone after it. The riposte is read from the exchange's beats (this
## fighter's own blows that carry riposte or reversal); 1 when there is none.
func _rip_fade(S: SimState, f, T: float) -> float:
	var ex = S.dirS.ex
	if ex == null or (ex.A != f and ex.D != f):
		return 1.0
	var R: Dictionary = AnimData.press.get("riposte", {})
	if R.is_empty():
		return 1.0
	var role: String = "A" if ex.A == f else "D"
	var t0: float = T - ex.t
	var span: float = maxf(float(R.get("fade", 4)), 1.0) * DT
	var w: float = 1.0
	for b in ex.beats:
		if b.op == "strike" and String(b.args.a) == role and (bool(b.args.get("riposte", false)) or bool(b.args.get("reversal", false))):
			var tc: float = t0 + b.t
			w = minf(w, 1.0 - smoothstep(0.0, 1.0, (T - (tc - span)) / span))
	return w


## The other fighter of a step 3 cue (the one who staggers after a perfect block or a reversal, the one who absorbs a burst); a burst shoves him
## only if his own speed jumps in the next ticks (the sim decides the range), so that one waits for the shove.
func on_cue_other(kind: String, t_event: float) -> void:
	if not RenderAnim.step3_cues or not AnimData.cue_map.has(kind):
		return
	var m: Dictionary = AnimData.cue_map[kind]
	if m.has("other_if_stunned"):
		if t_event - _stag_t0 >= 0.0 and t_event - _stag_t0 < 0.5:
			return   # the stagger cue came first and has played it
		_stun_watch = 3
		_stun_seq = String(m.other_if_stunned)
		_stun_t0 = t_event
	elif m.has("other"):
		var sid: String = String(m.other)
		if AnimData.entries.has(sid):
			_seq = {"id": sid, "t0": t_event, "dur": float(AnimData.entries[sid].dur) / 60.0}
			debug["step3"] = int(debug.get("step3", 0)) + 1
	elif m.has("other_if_shoved"):
		_shove_watch = 4
		_shove_vx = _rd_vw.x if _rd_have else 0.0


## `front`: the blow came from the side the victim faces. amp 0 to 1 from the hit's strength.
## `d` is the way the blow pushes, in the body frame (x forward: a blow from the front pushes him toward -x); `force` the
## damage over 60 (0.15 to 1.6). The reaction is the pose (a flinch) plus the ragdoll's own motion along d: a head snap, a torso
## fold or arch, limbs thrown by the push, a step; scaled by the blow's force and by the victim's wear, with a deterministic
## variant per hit (a hash of the tick, the slot and the hit count: no random stream, so a replay shows the same).
func on_hit(T: float, region: String, front: bool, amp: float, kind: String = "", d: Vector2 = Vector2.ZERO, force: float = -1.0, tick: int = 0) -> void:
	if d == Vector2.ZERO:
		d = Vector2(-1.0 if front else 1.0, 0.0)
	if force < 0.0:
		force = amp * 70.0 / 60.0
	_hit_n += 1
	var h: int = _hash(tick, slot, _hit_n)
	var u: float = float(h & 1023) / 511.5 - 1.0
	var u2: float = float((h >> 10) & 1023) / 511.5 - 1.0
	_reacts.append({"t0": T, "region": region, "front": front, "amp": clampf(amp, 0.2, 1.0), "kind": kind, "dx": d.x, "dy": d.y, "f": clampf(force, 0.15, 1.6), "u": u})
	if _reacts.size() > 4:
		_reacts.pop_front()
	if not RenderAnim.layer("ragdoll"):
		return
	var ra: float = _rd_amp()
	var wear_k: float = 1.0 + 0.7 * _worn + 0.5 * _brinkp
	var F: float = clampf(force, 0.15, 1.6) * wear_k * (1.0 + 0.25 * u) * ra
	if kind == "guard":
		F *= 0.4
	_rd.hit(d.x, d.y, F, int(AnimRagdoll.REGIONS.get(region, 1)), u, u2)
	_hit_free = minf(1.0, _hit_free + 0.5 * F + 0.3 * _worn)
	# the fighter's own shapes are the flinch keys: a light blow pulls in his brace shape, a heavy one his crumple shape
	var heavy_s: float = smoothstep(0.5, 1.1, force * wear_k)
	_hit_crum = heavy_s
	_hit_w = minf(1.0, _hit_w + 0.5 + 0.3 * heavy_s)
	_rd.out_w = 1.0


## A transformation took place (the sim's `transform` event). Its three beats (data/anim/forms.json) play over the pause's
## own ticks (full and short) or over sim time (live).
func on_transform(T: float, version: String) -> void:
	if AnimData.forms.has(version):
		_form = {"t0": T, "version": version}
		debug["variants"]["transform_" + version] = int(debug["variants"].get("transform_" + version, 0)) + 1


func socket(name: String) -> Vector3:
	if not _full_fk and not SOCKET_SET.has(name):
		AnimPose.fk(q, hips, gq, gp)
		_full_fk = true
	return gp[AnimRig.index[name]] + root_off


## The head's centre in model space (for the head flashes).
func head_center() -> Vector3:
	var h: int = AnimRig.index["head"]
	return gp[h] + gq[h] * Vector3(0, 6, 0) + root_off


## The visual facing for this frame (computed once a frame, whichever pane asks first).
func update_face(S: SimState, f) -> float:
	var key: int = RenderAnim._frame_key(S)
	if key == _face_key:
		return vface
	_face_key = key
	if not _face_init:
		_face_init = true
		vface = f.face
		return vface
	var want: float = _wanted_face(S, f)
	if want == vface or want == 0.0:
		_face_want_t = -1.0
	elif _face_want_t < 0.0:
		_face_want_t = S.T
	elif S.T - _face_want_t >= FACE_HOLD:
		vface = want
		_face_want_t = -1.0
		_turn_t0 = S.T
		_turn_dir = want
		debug["face_flips"] += 1
	return vface


func _opponent(S: SimState, f):
	for o in S.fighters:
		if o != f:
			return o
	return null


func _wanted_face(S: SimState, f) -> float:
	var opp = _opponent(S, f)
	var dx: float = SimWrap.sdx(f.x, opp.x) if opp != null else 0.0
	var ex = S.dirS.ex
	var engaged: bool = (ex != null and (ex.A == f or ex.D == f)) or f.state == "locked" or f.beamCharge != null
	if engaged:
		return signf(dx) if absf(dx) > FACE_DEAD else 0.0
	if f.state == "free" and absf(f.vx) > TRAVEL_FACE:
		var travel: float = signf(f.vx)
		# backing away from a close opponent keeps the face on him (the retreat pose); any other run faces the way it goes
		if travel != signf(dx) or absf(dx) > NEAR_LOOK or int(f.stance) == 3:
			return travel
		return signf(dx)
	if f.state == "free" and opp != null and absf(dx) > FACE_DEAD and absf(dx) < NEAR_LOOK and int(f.stance) != 3:
		return signf(dx)
	return f.face


static func _hash(a: int, b: int, c: int) -> int:
	var h: int = (a * 73856093) ^ (b * 19349663) ^ (c * 83492791)
	h = ((h ^ (h >> 13)) * 1274126177) & 0xFFFFFFFF
	return (h ^ (h >> 16)) & 0xFFFFFFFF


static func _ease_out_back(u: float, ov: float) -> float:
	return 1.0 - (1.0 - u) * (1.0 - u) + ov * sin(PI * u) * u


func _mixh(dst_h: Vector3, src_h: Vector3, w: float) -> Vector3:
	return dst_h.lerp(src_h, w)


# ------------------------------------------------------------------ the solve

func solve(S: SimState, f, prof: Dictionary) -> void:
	_prof = prof
	# On twos: a snappy fighter with nothing happening (no exchange, cue, reaction or beam) solves every second tick and the
	# bone writes are skipped on the others. A blow always solves, so no contact frame is missed.
	if version > 0 and float(prof.get("solve_hz", 60.0)) < 59.0 and (S.tick & 1) == 1 and _idle(S, f):
		frame = RenderAnim._frame_key(S)
		return
	version += 1
	update_face(S, f)
	_wound_read(f)
	var T: float = S.T
	var dt: float = clampf(T - _last_T, 0.0, 0.1) if _last_T >= 0.0 else 0.0
	var first: bool = _last_T < 0.0
	_last_T = T
	_set_lag(float(prof.get("lag", 0.3)))
	# 1. the base pose: what the fighter is doing when no blow, cue or reaction is on (a launch or a power-up moves on the
	# fluid profile, the rest on the default)
	_target_base(S, f, T)
	var bprof: Dictionary = _part_prof("launch") if f.state == "launched" else (_part_prof("power") if f.state == "charging" else prof)
	var k: float = 1.0 if first else 1.0 - exp(-dt / maxf(float(bprof.get("base_tau", 0.08)), 0.001))
	if _settle <= 40 or first:
		if RenderAnim.layer("transitions") and not first and _base_lagged():
			# the arms and hands follow the torso a little later (a guard comes up with overlap, not as one block)
			var e0: float = 1.0 - k
			for i in range(AnimRig.N):
				var kt: float = 1.0 - pow(e0, _base_r[i])
				_base[i] = AnimJoints.slerp_limb(_base[i], _tq[i], kt) if (AnimJoints.ik_limits and AnimJoints.is_limb[i] == 1) else _base[i].slerp(_tq[i], kt)
		else:
			AnimPose.mix(_base, _tq, k)
		_base_hips = _base_hips.lerp(_tq_hips, k)
		_base_curl = _base_curl.lerp(_tq_curl, k)
	elif _settle == 41:
		for i in range(AnimRig.N):
			_base[i] = _tq[i]
		_base_hips = _tq_hips
		_base_curl = _tq_curl
	for i in range(AnimRig.N):
		q[i] = _base[i]
	hips = _base_hips
	curl = _base_curl
	# 2. a cue pose from Combat's cue events
	if not _cue.is_empty():
		var t: float = T - float(_cue.t0)
		var d: float = float(_cue.dur)
		if t >= d:
			_cue = {}
		elif t >= 0.0:
			var w: float = smoothstep(0.0, RenderLook.CUE_IN, t) * (1.0 - smoothstep(d - RenderLook.CUE_OUT, d, t))
			var cp: AnimPose = AnimData.pose(String(AnimData.cue_poses[_cue.kind]))
			AnimPose.mix(q, cp.q, w * 0.9)
			hips = hips.lerp(cp.hips, w * 0.9)
			curl = curl.lerp(cp.curl, w * 0.9)
	if not _seq.is_empty():
		if T >= float(_seq.t0) + float(_seq.dur) + 0.05:
			_seq = {}
		else:
			var sq_w: float = float(_seq.get("wt", 1.0))
			var sq_id: String = String(_seq.id)
			if sq_id.begins_with("s3.perfect_block") or sq_id.begins_with("s3.reversal"):
				sq_w *= _rip_fade(S, f, T)   # the block pose gives way to the riposte that leaves from it
			_entry_layer(float(_seq.t0), float(_seq.dur), sq_id, T, 1.0 / float(_prof.get("solve_hz", 60.0)), sq_w, bool(_seq.get("fit", false)))
	if f.state == "intro" and RenderAnim.intro_poses and not AnimData.intro.is_empty():
		if _intro.is_empty():
			# the first frame, before any event has been drained (the sim sets the state at the start, the events come with the first tick): he is already falling
			_intro = {"on": true, "fall_t": -1.0, "fall_dur": 0.6, "land_t": -1.0, "stare_t": -1.0, "stare_dur": 2.6}
		_intro_layer(S, f)
	if _ls_t0 >= 0.0 and AnimData.pose_exists("ls.hold.resolve") and f.state == "free":
		var LS: Dictionary = AnimData.last_stand
		var lw: float = smoothstep(0.0, float(LS.get("in", 0.6)), T - _ls_t0)
		if _ls_t1 >= 0.0:
			lw *= 1.0 - smoothstep(0.0, float(LS.get("out", 0.5)), T - _ls_t1)
			if T - _ls_t1 >= float(LS.get("out", 0.5)):
				_ls_t0 = -1.0
				_ls_t1 = -1.0
		lw *= float(LS.get("hold_weight", 0.3)) * (0.6 if RenderAnim.reduced_motion else 1.0)
		if lw > 0.001:
			var rp: AnimPose = AnimData.pose("ls.hold.resolve")
			AnimPose.mix(q, rp.q, lw)
			hips = hips.lerp(rp.hips, lw)
			curl = curl.lerp(rp.curl, lw)
	_agency_layer(S, f, T)
	_energy_layer(S, f, T)
	if not _gest.is_empty():
		_gesture_layer(S, f, T)
	if _gc_hold_t0 >= 0.0 and AnimData.pose_exists("gc.hold.brace_tumble"):
		var hin: float = float(AnimData.ground.get("hold", {}).get("in", 0.08))
		var hout: float = float(AnimData.ground.get("hold", {}).get("out", 0.15))
		var hw: float = smoothstep(0.0, hin, T - _gc_hold_t0)
		if _gc_hold_t1 >= 0.0:
			hw *= 1.0 - smoothstep(0.0, hout, T - _gc_hold_t1)
			if T - _gc_hold_t1 >= hout:
				_gc_hold_t0 = -1.0
				_gc_hold_t1 = -1.0
		if hw > 0.001:
			var bt: AnimPose = AnimData.pose("gc.hold.brace_tumble")
			AnimPose.mix(q, bt.q, hw * _gc_hold_w)
			hips = hips.lerp(bt.hips, hw * _gc_hold_w)
			curl = curl.lerp(bt.curl, hw * _gc_hold_w)
	if _skim_t0 >= 0.0:
		var sk_t: float = T - _skim_t0
		if sk_t > 0.35:
			_skim_t0 = -1.0
		elif sk_t >= 0.0:
			_mix_pose(AnimData.pose("skip.water"), smoothstep(0.0, 0.04, sk_t) * (1.0 - smoothstep(0.12, 0.35, sk_t)) * 0.85)
	# 3. the exchange: approach and strike parts
	_part = ""
	_skip_inertia = false
	_blow_snap = false
	_ci_w = 0.0
	_rushing = false
	_contact_now = false
	_tc_left = -1.0
	_pr_dx = 0.0
	press = {}
	riposte = {}
	AnimZip.layers(self, S, f, T, dt)
	var ex = S.dirS.ex
	if ex != null and (ex.A == f or ex.D == f):
		_exchange_layers(S, f, ex, T)
	if riposte.is_empty() and not _rip_cue.is_empty():
		# the riposte cue has come and the blow's own beat is not in the exchange yet: VFX is told what is coming
		var rip_left: int = int(_rip_cue.ahead) - (S.tick - int(_rip_cue.tick))
		if rip_left >= -12:
			riposte = {"kind": String(_rip_cue.kind), "reversal": false, "sure": false, "ticks_to_contact": rip_left, "cue": true}
		else:
			_rip_cue = {}
	# 4. the signature beam, and a transformation
	_beam_layer(S, f, T)
	if not _form.is_empty():
		_transform_layer(S, T)
	# 4a. the head looks at the opponent
	if RenderAnim.layer("look"):
		_look_at(S, f, dt)
	# 4b. a broken arm hangs, a broken leg is favoured
	if _arm_broken or _leg_broken:
		_wound_limbs(f, T)
	# 5. reactions to blows
	_recoil_x = 0.0
	_recoil_y = 0.0
	_step_x = 0.0
	_reaction_layer(T)
	# 5a. feet on the slope under him, a skid pitched to the ground (before the contact solve, so a blow reaches from the new stance)
	if RenderAnim.layer("feet"):
		var tf: int = Time.get_ticks_usec() if RenderAnim.debug_checks else 0
		_ground_feet(S, f, dt)
		if RenderAnim.debug_checks:
			debug["feet_usec"] += Time.get_ticks_usec() - tf
			debug["feet_n"] += 1
	if RenderAnim.joint_audit:
		audit = {"A": AnimJoints.violations(q, _rd.shape_key)}
	# 5b. the striking limb reaches the defender (the contact solve)
	if _ci_w > 0.001:
		_contact_ik(S, f)
	if RenderAnim.joint_audit:
		audit["B"] = AnimJoints.violations(q, _rd.shape_key)
	# 5c. the active ragdoll: the body's own motion on top of the pose (the pose is in charge while a blow is thrown)
	if _rd.out_w > 0.001:
		_rd.apply(q, _rd.out_w * (0.3 if (_part != "" or _ci_w > 0.001) else 1.0), _rd_scale())
	if RenderAnim.joint_audit:
		audit["C"] = AnimJoints.violations(q, _rd.shape_key)
	# 5d00. flight overhaul (unit G): the burst and the brake poses, the bank, the shudder of a nearby impact
	if RenderAnim.layer("flight"):
		_flight_layers(T, f)
	# 5d01. the winner in the wreckage (unit K)
	if RenderAnim.layer("personality"):
		_win_layer(S, f, T)
	# 5d0. idles and transitions with personality (overhaul unit F): the weight shift, the knee bounce and the breath of the stance and
	# the form, and a turn-around as a step
	if RenderAnim.layer("personality") and (f.state == "free" or f.state == "locked") and _part == "" and _ci_w < 0.001:
		var calm: float = 1.0 - clampf(absf(f.vx) / 300.0, 0.0, 1.0)
		if f.state == "free" and calm > 0.05:
			_personality_layer(T, int(f.stance), clampi(int(f.tier) - 1, 0, 3), calm)
		_turn_layer(T)
	# 5d. polish: lean into acceleration (free flight and movement), the hover bob
	if RenderAnim.layer("lean"):
		if absf(_lean) > 0.002:
			q[AnimRig.index["pelvis"]] = q[AnimRig.index["pelvis"]] * Quaternion(Vector3(0, 0, 1), -_lean * 0.6)
			q[AnimRig.index["spine_2"]] = q[AnimRig.index["spine_2"]] * Quaternion(Vector3(0, 0, 1), -_lean * 0.4)
		if _air_w > 0.3:
			hips.y += sin(T * TAU * 0.7 + slot * 1.3) * 1.4 * _air_w * _rd_amp()
			q[AnimRig.index["spine_2"]] = q[AnimRig.index["spine_2"]] * Quaternion(Vector3(0, 0, 1), sin(T * TAU * 0.5 + slot) * 0.03 * _air_w * _rd_amp())
	# 6. moving hold, hit-stop shiver, spring chains
	var drift: float = float(prof.get("hold_drift", 0.0))
	if drift > 0.0:
		q[AnimRig.index["spine_2"]] = q[AnimRig.index["spine_2"]] * Quaternion(Vector3(0, 0, 1), sin(T * 2.3 + slot * 1.7) * 0.014 * drift)
		q[AnimRig.index["head"]] = q[AnimRig.index["head"]] * Quaternion(Vector3(0, 0, 1), sin(T * 1.7 + slot) * 0.02 * drift)
	if _worn > 0.04 or _brinkp > 0.4:
		_wear_motion(f, T)
	var shiver: float = float(prof.get("shiver", 0.0))
	if S.dirS.stop > 0.0 and shiver > 0.0:
		root_off = Vector3(_signed(S.tick, slot), _signed(S.tick + 13, slot) * 0.6, 0.0) * shiver
	else:
		root_off = root_off * 0.0
	root_off.x += _recoil_x + _step_x + _pr_dx
	root_off.y += _zip_dy
	if _lean_t0 > -5.0 or _over_t0 > -5.0 or _block_t0 > -5.0:
		root_off.x += _evade_offsets(T)
	root_off.y += _recoil_y
	if _smear != 0.0:
		root_off.x += _smear_now(T)
		if T - _smear_t0 >= 0.05:
			_smear = 0.0
	_springs(f)
	# 6a. inertialisation: a join in the solved pose decays instead of popping
	_inertialise(dt, prof)
	if RenderAnim.debug_checks:
		layers = ("cue:" + String(_cue.get("kind", "")) + " " if not _cue.is_empty() else "") + ("rush " if _rushing else "") + ("react " if not _reacts.is_empty() else "") + ("ik " if _ci_w > 0.001 else "") + ("beam " if f.beamCharge != null else "") + (f.state + " ")
	_lead_layer(S, f, dt)
	if not zip.is_empty() or absf(_zip_tilt) > 0.002:
		AnimZip.orient(self, S, f, dt)
	# 6b. the limb pass: elbows and knees stay hinges in human range, arms stay out of the shoulder's blind spot
	if RenderAnim.joint_audit:
		audit["D"] = AnimJoints.violations(q, _rd.shape_key)
	debug["limit_fix"] = AnimPose.limit_limbs(q, _rd.shape_key)
	# 7. sockets: only the chains the views read (the head and the near hand); the rest is on demand
	AnimPose.fk_chain(q, hips, gq, gp, SOCKET_CHAIN)
	_full_fk = false
	if RenderAnim.press_styles:
		_press_record(T, f)
	if is_nan(q[0].x) or is_nan(q[5].w):
		debug["nan"] += 1
		q[0] = Quaternion.IDENTITY
	elif RenderAnim.debug_checks:
		for i in range(AnimRig.N):
			if is_nan(q[i].x) or is_nan(q[i].w):
				debug["nan"] += 1
				q[i] = Quaternion.IDENTITY
	frame = RenderAnim._frame_key(S)


## Nothing is asking for a precise pose this tick.
func _idle(S: SimState, f) -> bool:
	var ex = S.dirS.ex
	return (ex == null or (ex.A != f and ex.D != f)) and _cue.is_empty() and _seq.is_empty() and _shove_watch == 0 and _stun_watch == 0 and _reacts.is_empty() and f.beamCharge == null and S.beams.is_empty() and S.dirS.stop <= 0.0 and _form.is_empty() and _rd.out_w <= 0.001


## A blow counts as heavy from the exchange kind, the strike's own weight (o.big) or its damage.
static func _is_heavy(ex, args: Dictionary) -> bool:
	var o = args.get("o")
	return ex.kind == "heavy" or (o != null and bool(o.get("big", false))) or float(args.get("dmg", 0.0)) >= 40.0


static func _signed(tick: int, s: int) -> float:
	return float(_hash(tick, s, 7) & 1023) / 511.5 - 1.0


var _tq_hips := Vector3.ZERO
var _tq_curl := Vector2(0.5, 0.5)


func _target_base(S: SimState, f, T: float) -> void:
	var stance: int = clampi(int(f.stance), 0, 3)
	var vf: float = f.vx * vface
	var state: String = f.state
	# The blend weights, quantised to sixteenths: while they and the stance do not change the target is not rebuilt (the
	# base smoothing hides the steps), and once it has settled the smoothing stops too.
	var w1: float = 0.0
	var w2: float = 0.0
	var w3: float = 0.0
	var w4: float = 0.0              # hovering
	var w5: float = 0.0              # climbing
	var w6: float = 0.0              # diving
	var w7: float = 0.0              # winded (1) or relaxed (0.5) idle
	var w8: float = 0.0              # sprint, on top of the dash
	var w9: float = 0.0              # a guarded step forward
	var w10: float = 0.0             # a guarded step back
	var w11: float = 0.0             # the stance sagging toward the brink
	var w12: float = 0.0             # skidding on the back (moving away from the way he faces)
	var w13: float = 0.0             # skidding face down
	var w15: float = 0.0             # the push-up stage of a get-up
	var w16: int = 0                 # the winner's beat (1 stand, 2 survey, 3 the end)
	var w17: float = 0.0             # a KO's fall, 0 to 1 over the first half second down
	var w14: float = 0.0             # the rise out of the crouch after it (slower when worn)
	if state == "down":
		_was_down = true
	elif _was_down:
		_was_down = false
		_rise_t0 = T
		_rise_dur = 0.25 + 0.55 * maxf(_worn, _brinkp)
		if RenderAnim.ground_poses and RenderAnim.layer("transitions") and AnimData.entries.has("gc.getup_quick") and _seq.is_empty():
			var slow: bool = maxf(_worn, _brinkp) > float(AnimData.ground.get("getup", {}).get("slow_above", 0.45))
			_seq = {"id": "gc.getup_slow" if slow else "gc.getup_quick", "t0": T, "dur": _rise_dur, "wt": 1.0, "getup": true}
			_rise_t0 = -1.0   # the sequence is the rise; the blend toward down.getup would double it
	if _rise_t0 >= 0.0 and RenderAnim.layer("transitions"):
		var rt: float = T - _rise_t0
		if rt >= _rise_dur:
			_rise_t0 = -1.0
		elif state == "free":
			w14 = 1.0 - smoothstep(0.0, _rise_dur, rt)
	_air_w = 0.0
	var mode: int = 0
	if f.slide > 0.0:
		mode = 1
		var sk: float = smoothstep(250.0, 600.0, absf(f.vx))
		if vf < 0.0:
			w12 = sk
		else:
			w13 = sk
	elif state == "launched":
		var speed: float = Vector2(f.vx, f.vy).length()
		w1 = smoothstep(700.0, 2200.0, speed)
		w2 = (1.0 - smoothstep(150.0, 600.0, speed)) * 0.7
		mode = 2
	elif state == "down":
		if RenderAnim.ragdoll_enabled:
			w1 = smoothstep(0.5, 0.72, f.stateT)
			w15 = smoothstep(0.28, 0.5, f.stateT)
		else:
			w1 = smoothstep(0.42, 0.72, f.stateT)
		mode = 7 if is_same(S.game.ko, f) else 3
		w17 = smoothstep(0.0, 0.5, f.stateT) if RenderAnim.layer("personality") else 1.0
	elif state == "charging":
		mode = 4
	elif S.game.ko != null and not is_same(S.game.ko, f) and S.game.koT > float(AnimData.winner.get("start", 0.8)) and state == "free":
		mode = 6
		w16 = _win_phase(S.game.koT, String(f.id)) if RenderAnim.layer("personality") else 4
	else:
		w1 = smoothstep(140.0, 720.0, vf)
		w2 = smoothstep(140.0, 520.0, -vf) * (1.0 - w1)
		if w1 > 0.01:
			w3 = clampf(atan2(f.vy, maxf(absf(f.vx), 60.0)), -0.9, 0.9) * 0.7 * w1
		w8 = smoothstep(1000.0, 2200.0, vf)
		w9 = smoothstep(50.0, 180.0, vf) * (1.0 - smoothstep(300.0, 600.0, vf))
		w10 = smoothstep(50.0, 160.0, -vf) * (1.0 - smoothstep(260.0, 480.0, -vf))
		mode = 5
		if state == "free":
			w11 = smoothstep(0.35, 1.0, _brinkp) * 0.7
		# the variants: hovering (feet off the ground), climbing, diving; on the ground a winded idle when the ki is spent and
		# a relaxed one far from the opponent
		if state == "free" and w1 < 0.05 and w2 < 0.05:
			var air: float = smoothstep(30.0, 70.0, f.y - WorldTerrain.groundY(S, f.x))
			if air > 0.0:
				w4 = air
				_air_w = air
				w5 = air * smoothstep(150.0, 600.0, f.vy)
				w6 = air * smoothstep(150.0, 600.0, -f.vy)
			elif f.ki < 6.0:
				w7 = 1.0
			elif int(f.stance) < 2 and absf(f.vx) < 100.0:
				var opp = _opponent(S, f)
				if opp != null and absf(SimWrap.sdx(f.x, opp.x)) > IDLE_FAR:
					w7 = 0.5
	var key: int = mode | (stance << 3) | (int(w1 * 16.0) << 5) | (int(w2 * 16.0) << 10) | (int((w3 + 1.0) * 24.0) << 15) | (int(w4 * 8.0) << 21) | (int(w5 * 8.0) << 25) | (int(w6 * 8.0) << 29) | (int(w7 * 2.0) << 33) | (int(w8 * 8.0) << 35) | (int(w9 * 8.0) << 39) | (int(w10 * 8.0) << 43) | (int(w11 * 8.0) << 47) | (int(w12 * 8.0) << 50) | (int(w13 * 8.0) << 54)
	var key2: int = int(w15 * 8.0) | (int(w14 * 8.0) << 4) | (w16 << 8) | (int(w17 * 8.0) << 11)
	if key == _bkey and key2 == _bkey2:
		_settle += 1
		return
	_bkey = key
	_bkey2 = key2
	_settle = 0
	var vk: String = ""
	if mode == 7:
		vk = "ko"
	elif mode == 6:
		vk = "victory"
	elif w4 > 0.5:
		vk = "hover" if w5 < 0.5 and w6 < 0.5 else ("climb" if w5 >= w6 else "dive")
	elif w8 > 0.5:
		vk = "sprint"
	elif w9 > 0.5 or w10 > 0.5:
		vk = "step"
	elif w7 >= 1.0:
		vk = "winded"
	elif w7 > 0.0:
		vk = "relaxed"
	if vk != "":
		debug["variants"][vk] = int(debug["variants"].get(vk, 0)) + 1
	var sp: AnimPose = AnimData.pose("stance." + STANCES[stance])
	for i in range(AnimRig.N):
		_tq[i] = sp.q[i]
	_tq_hips = sp.hips
	_tq_curl = sp.curl
	match mode:
		1:
			_blend_target("slide.brake", 1.0)
			_blend_target("skid.back", w12)
			_blend_target("skid.front", w13)
		2:
			_blend_target("launch.spread", 1.0)
			_blend_target("launch.stream", w1)
			_blend_target("launch.tuck", w2)
		3:
			_blend_target("down.prone", 1.0)
			_blend_target("getup.push", w15)
			_blend_target("down.getup", w1)
		4:
			_blend_target("charge.hold", 1.0)
		6:
			match w16:
				1:
					_blend_target("win.stand", 1.0)
				2:
					_blend_target("win.survey", 1.0)
				_:
					_blend_target("emote.victory" if (w16 == 4 or _win_end(String(f.id)) == "victory") else "win.survey", 1.0)
		7:
			# a KO falls: a stagger first, then flat on the back
			_blend_target("react.stagger", (1.0 - w17) * 0.8)
			_blend_target("down.ko", w17)
		_:
			_blend_target("move.dash", w1)
			_blend_target("move.retreat", w2)
			_blend_target("move.sprint", w8)
			_blend_target("move.step_f", w9)
			_blend_target("move.step_b", w10)
			_blend_target("wound.sag", w11)
			_blend_target("down.getup", w14 * 0.75)
			_blend_target("stance.air", w4)
			_blend_target("move.ascend", w5)
			_blend_target("move.descend", w6)
			if w7 >= 1.0:
				_blend_target("idle.winded", 1.0)
			elif w7 > 0.0:
				_blend_target("idle.relaxed", 0.8)
			if w3 != 0.0:
				var pi_: int = AnimRig.index["pelvis"]
				_tq[pi_] = _tq[pi_] * Quaternion(Vector3(0, 0, 1), w3)


func _blend_target(id: String, w: float) -> void:
	if w <= 0.001:
		return
	var p: AnimPose = AnimData.pose(id)
	AnimPose.mix(_tq, p.q, w)
	_tq_hips = _tq_hips.lerp(p.hips, w)
	_tq_curl = _tq_curl.lerp(p.curl, w)


func _mix_pose(p: AnimPose, w: float) -> void:
	AnimPose.mix(q, p.q, w)
	hips = hips.lerp(p.hips, w)
	curl = curl.lerp(p.curl, w)


func _set_pose(p: AnimPose) -> void:
	for i in range(AnimRig.N):
		q[i] = p.q[i]
	hips = p.hips
	curl = p.curl


# ------------------------------------------------------------------ the exchange

## How heavy a blow is, continuously: the damage over 70 (a chain link, which carries no damage, counts as 1). It stretches the
## load, the follow-through and the recovery and deepens the overshoot (overhaul unit D); the contact tick never moves.
static func _blow_weight(args: Dictionary) -> float:
	if not RenderAnim.ragdoll_enabled:
		return 0.7   # neutral: the A/B off switch leaves the blow's timing as it was
	var d: float = float(args.get("dmg", -1.0))
	if d < 0.0:
		var o = args.get("o")
		return 1.0 if (o != null and bool(o.get("big", false))) else 0.6
	return clampf(d / 70.0, 0.2, 1.4)


## Go-live step 1 (docs/combat/pending/golive-step1.md), only with RenderAnim.wave1_live (--wave1-live, OFF by default): the key set of a blow of the
## Anti-hero's shape from wave 1's lists. The list is walked, not hashed blow by blow: it starts at a hash of the exchange and the fighter and takes
## every third entry (every fifth when the list has a multiple of three), so nothing repeats inside ten blows; a two-limb key set is skipped when a
## limb of its kind is drawn as broken; the gated key sets (the sweep with both fighters on the ground, the drop kick with the striker in the air)
## join the list only while their gate is open. Returns "" when not applicable (the flag is off, another fighter's shape, no data).
func _live_pick(S: SimState, f, ex, ordinal: int, heavy: bool) -> String:
	if not RenderAnim.wave1_live:
		return ""
	var lv: Dictionary = AnimData.live
	if lv.is_empty():
		return ""
	var sk: String = String(AnimRagdoll.shape_of.get(String(f.id), AnimRagdoll.shape_of.get("default", "")))
	if not (lv.get("shapes", []) as Array).has(sk):
		return ""
	var which: String = "heavy" if heavy else "light"
	var cand: Array = (lv.picks[which] as Array).duplicate()
	var opp = ex.D if ex.A == f else ex.A
	for g in lv.get("gated", []):
		if String(g.weight) != which:
			continue
		var open: bool = false
		match String(g.gate):
			"both_ground":
				open = f.y - WorldTerrain.groundY(S, f.x) < 8.0 and opp != null and opp.y - WorldTerrain.groundY(S, opp.x) < 8.0
			"striker_air":
				open = f.y - WorldTerrain.groundY(S, f.x) > 20.0
		if open:
			cand.append(String(g.keyset))
	var n: int = cand.size()
	if n == 0:
		return ""
	var stepw: int = 3 if n % 3 != 0 else 5
	var h: int = _hash(int(ex.n), slot, 11)
	for tries in range(n):
		var id: String = String(cand[(h + stepw * (ordinal + tries)) % n])
		var ks = AnimData.keysets.get(id)
		if ks == null:
			continue
		var two: String = String(ks.get("limb2", ""))
		if two != "" and ((_arm_broken and (two.begins_with("hand") or two.begins_with("elbow"))) or (_leg_broken and (two.begins_with("foot") or two.begins_with("knee")))):
			continue
		debug["live_picks"] = int(debug.get("live_picks", 0)) + 1
		return id
	return ""


## An entry (data/anim/waves/*.entries.json, parked: no live beat names one yet): a sequence of poses over `dur` seconds. A phase with
## `ticks` holds that long, the others share the rest by their weight `w`; the poses cross-fade over three ticks at each boundary. The
## strike that follows takes over from here by its own load (the join is inertialised).
func _entry_layer(es: float, dur: float, id: String, T: float, dq: float, wt: float = 1.0, fit: bool = false) -> void:
	var en = AnimData.entries.get(id)
	if en == null or T < es - 0.0001 or T >= es + dur + 0.05:
		return
	var ph: Array = en.phases
	var fixed: float = 0.0
	var sumw: float = 0.0
	for p in ph:
		if p.has("ticks"):
			fixed += float(p.ticks) * DT
		else:
			sumw += float(p.get("w", 1.0))
	var flex: float = maxf(dur - fixed, DT)
	var bounds: Array = [0.0]
	for p in ph:
		var seg: float = float(p.ticks) * DT if p.has("ticks") else flex * float(p.get("w", 1.0)) / maxf(sumw, 0.001)
		bounds.append(float(bounds[-1]) + seg)
	if fit and float(bounds[-1]) > 0.0001:
		# a zip plays an entry at its own speed: every phase shrinks together so the whole sequence fits the zip's ticks
		var fk: float = dur / float(bounds[-1])
		for bi in range(bounds.size()):
			bounds[bi] = float(bounds[bi]) * fk
	var tt: float = floorf(clampf(T - es, 0.0, dur) / dq + 0.0001) * dq
	var e: float = 1.5 * DT
	var base_q: Array[Quaternion] = []
	var base_h: Vector3 = hips
	var base_c: Vector2 = curl
	if wt < 0.999:
		base_q = q.duplicate()
	_mix_pose(AnimData.pose(String(ph[0].pose)), 1.0)
	for i in range(1, ph.size()):
		var w: float = smoothstep(float(bounds[i]) - e, float(bounds[i]) + e, tt)
		if w > 0.001:
			_mix_pose(AnimData.pose(String(ph[i].pose)), w)
	if wt < 0.999:
		for i in range(AnimRig.N):
			q[i] = base_q[i].slerp(q[i], wt)
		hips = base_h.lerp(hips, wt)
		curl = base_c.lerp(curl, wt)
	debug["entries"] = int(debug.get("entries", 0)) + 1


func _exchange_layers(S: SimState, f, ex, T: float) -> void:
	var role: String = "A" if ex.A == f else "D"
	var t0: float = T - ex.t
	var strikes: Array = []
	var rushes: Array = []
	var entries: Array = []   # parked entry sequences (data/anim/waves/*.entries.json), played by an `entry` beat or a rush beat that names one
	var ordinal: int = 0
	# a parried exchange: only the blows timed before the parry are drawn (the sim keeps listing, and later firing, the rest of
	# the string; Encounter will end it there)
	var ct: float = 1.0e9
	if ex.cancel:
		if _cx_n != int(ex.n):
			_cx_n = int(ex.n)
			_cx_t = ex.t
		ct = _cx_t
	for b in ex.beats:
		if b.op == "strike":
			var who: String = String(b.args.a)
			if who == role and (not ex.cancel or b.t <= ct + 0.0001):
				var scls: String = "heavy" if _is_heavy(ex, b.args) else "light"
				strikes.append([t0 + b.t, ordinal, _rip_args(b.args, scls), scls])
			ordinal += 1
		elif b.op == "chainStrike":
			if role == "A" and (not ex.cancel or b.t <= ct + 0.0001):
				var cargs: Dictionary = {"o": {"big": true}}
				if b.args != null:
					cargs.merge(b.args, true)   # B0: a chainStrike carries a, d and the beat fields (style, grade, k, n, closing, charge, hand, ender) as a strike does
				strikes.append([t0 + b.t, ordinal, cargs, "chain"])
			ordinal += 1
		elif b.op == "entry" and String(b.args.get("who", "A")) == role:
			entries.append([t0 + b.t, float(b.args.dur), AnimData.resolve_entry(pair_key, String(b.args.get("id", "")))])
		elif b.op == "rush" and role == "A":
			rushes.append([t0 + b.t, float(b.args.dur), AnimData.resolve_entry(pair_key, String(b.args.get("entry", "")))])
		elif b.op == "finRush" and String(b.args.w) == role:
			rushes.append([t0 + b.t, float(b.args.dur), ""])
	# approach: launch-off, flight, arrival (a rush is snappy)
	var rprof: Dictionary = _part_prof("rush")
	var rdq: float = 1.0 / float(rprof.get("solve_hz", 60.0))
	for en in entries:
		_entry_layer(float(en[0]), maxf(float(en[1]), DT), String(en[2]), T, rdq)
	for r in rushes:
		var rs: float = r[0]
		var dur: float = maxf(r[1], DT)
		if String(r[2]) != "" and AnimData.entries.has(String(r[2])):
			_entry_layer(rs, dur, String(r[2]), T, rdq)
			continue
		if T >= rs and T < rs + dur + 0.05:
			var tq: float = rs + floorf((T - rs) / rdq) * rdq
			var p: float = clampf((tq - rs) / dur, 0.0, 1.0)
			# the streak holds to the last tick: the fastest ticks of a rush are its end, and they must not be drawn in a standing pose
			var fly: float = smoothstep(0.0, 0.18, p) * (1.0 - smoothstep(0.93, 1.0, p))
			_rushing = true
			_mix_pose(AnimData.pose("move.dash"), fly)
			var off: float = 1.0 - smoothstep(0.0, minf(4.0 * DT / dur, 0.3), p)
			_mix_pose(AnimData.pose("approach.launch"), off * 0.9)
	_beat_layers(ex, role, t0, T)
	# strikes: the one whose window (load, snap, follow, recover) holds now, latest start first. Each blow keeps the
	# profile of its own kind: light and chain blows snappy, heavy blows fluid.
	var best: int = -1
	var best_start: float = -1.0e9
	var lens: Array = []
	var profs: Array = []
	var psts: Array = []
	var prev_tc: float = -1.0e9
	for n in range(strikes.size()):
		var tc: float = strikes[n][0]
		var psn: String = _press_style(S, f, ex, strikes[n])
		var sp: Dictionary = _part_prof(String(strikes[n][3]))
		if psn != "":
			sp = _press_prof(sp, psn, strikes[n][2])
			if bool(strikes[n][2].get("zip", false)):
				sp["load_ticks"] = {"light": 2, "heavy": 3}   # a zip's tell was the wind-up: the blow lands on the arrival
				sp["load_ease"] = 1.0
			elif bool(strikes[n][2].get("_rip", false)):
				var rw: Dictionary = AnimData.press.get("riposte", {}).get("windup", {})
				sp["load_ticks"] = {"light": int(rw.get("light", 2)), "heavy": int(rw.get("heavy", 3))}   # the block was the wind-up: the blow leaves from it
				sp["load_ease"] = 1.0
		profs.append(sp)
		psts.append(psn)
		var Fn: float = float(sp.get("follow_ticks", 6)) * DT * (0.85 + 0.25 * _blow_weight(strikes[n][2]))
		var Hn: float = float(sp.get("hold_ticks", 0)) * DT
		var Rn: float = float(sp.get("recover_ticks", 10)) * DT
		var Sn_: float = float(sp.get("snap_ticks", 3)) * DT
		var bw: float = _blow_weight(strikes[n][2])
		var lnom: float = float(sp.get("load_ticks", {}).get("heavy" if strikes[n][3] == "heavy" else "light", 12)) * DT * (0.75 + 0.35 * bw)
		var gap: float = tc - prev_tc
		var L: float = clampf(minf(lnom, gap - 0.5 * Fn), Sn_ + 2.0 * DT, lnom)
		lens.append(L)
		var start: float = tc - L
		if T >= start and T < tc + Fn + Hn + Rn and start > best_start:
			best = n
			best_start = start
		prev_tc = tc
	if best < 0:
		return
	var prof: Dictionary = profs[best]
	_set_lag(float(prof.get("lag", 0.3)))
	var Sn: float = float(prof.get("snap_ticks", 3)) * DT
	var bw2: float = _blow_weight(strikes[best][2])
	var H: float = float(prof.get("hold_ticks", 0)) * DT   # the contact key held (a tech blow's beat), then the follow-through
	var F: float = float(prof.get("follow_ticks", 6)) * DT * (0.85 + 0.25 * bw2) + H
	var R: float = float(prof.get("recover_ticks", 10)) * DT * (0.9 + 0.2 * bw2)
	var dq: float = 1.0 / float(prof.get("solve_hz", 60.0))
	var tc2: float = strikes[best][0]
	var L2: float = lens[best]
	var heavy2: bool = strikes[best][3] == "heavy"
	var side: bool = (_hash(int(ex.n), int(strikes[best][1]), slot + 1) & 1) == 1
	var pstyle: String = String(psts[best])
	var prow: Dictionary = AnimData.press.get("styles", {}).get(pstyle, {}) if pstyle != "" else {}
	if pstyle != "" and (bool(strikes[best][2].get("zip", false)) or bool(strikes[best][2].get("_rip", false))):
		prow = prow.duplicate()
		prow["squash"] = 0.0
	var bargs: Dictionary = strikes[best][2]
	if bool(bargs.get("_rip", false)):
		riposte = {"kind": String(strikes[best][3]), "reversal": bool(bargs.get("reversal", false)), "sure": bool(bargs.get("sure", false)), "ticks_to_contact": roundi((tc2 - T) / DT)}
	var bw3: float = bw2
	if pstyle != "":
		var BT: Dictionary = AnimData.press.get("beat", {})
		if bargs.has("hand"):
			side = String(bargs.hand) == "l"   # B0: the beat names the striking side (the planner's own; today it alternates by k)
		elif bool(prow.get("alternate", false)):
			side = ((best + (_hash(int(ex.n), 0, slot + 1) & 1)) & 1) == 1   # no side on the beat: a mashed string alternates its limbs
		if pstyle == "tech" and bargs.has("grade"):
			var gd: Dictionary = BT.get("grade", {}).get(String(bargs.grade), {})
			if gd.has("ghosts"):
				prow = prow.duplicate()
				prow["ghosts"] = int(gd.ghosts)
		if pstyle == "heavy" and bargs.has("charge"):
			var cw: Dictionary = BT.get("charge", {})
			bw3 = lerpf(float(cw.get("tapped", bw2)), float(cw.get("held", 1.0)), clampf(float(bargs.charge), 0.0, 1.0))
	var picks: Array = AnimData.picks["heavy" if heavy2 else "light"]
	var ksid: String = String(picks[_hash(int(ex.n), int(strikes[best][1]), 3 + slot) % picks.size()])
	var live_id: String = _pair_pick(S, f, ex, int(strikes[best][1]), heavy2, String(strikes[best][2].get("piece", strikes[best][2].get("strike", ""))))   # his own waves first (docs/animation/pair-live.md), then go-live step 1
	if live_id == "":
		live_id = _live_pick(S, f, ex, int(strikes[best][1]), heavy2)
	if live_id != "":
		ksid = live_id
	if RenderAnim.force_keyset != "" and AnimData.keysets.has(RenderAnim.force_keyset):
		ksid = RenderAnim.force_keyset
	var ks: Dictionary = AnimData.keysets[ksid]
	# a broken arm does not strike and a broken leg does not kick: the blow uses the other limb (the sim keeps no side)
	var lb: String = String(ks.get("limb", "hand_r"))
	if lb.begins_with("hand") and _arm_broken:
		side = _hang_right
	elif lb.begins_with("foot") and _leg_broken:
		side = _leg_right
	if _arm_broken or _leg_broken:
		debug["wound_strikes"] += 1
		if (lb.begins_with("hand") and _arm_broken and (side == _hang_right) == false) or (lb.begins_with("foot") and _leg_broken and (side == _leg_right) == false):
			debug["wound_bad"] += 1
	_part = ksid
	var pc: AnimPose = AnimData.pose(String(ks["keys"][0]["pose"]), side)
	var pk: AnimPose = AnimData.pose(String(ks["keys"][1]["pose"]), side)
	var pf: AnimPose = AnimData.pose(String(ks["keys"][2]["pose"]), side)
	var tq2: float = tc2 + floorf((T - tc2) / dq + 0.0001) * dq
	if T >= tc2 - 0.0001 and T < tc2 + 0.0001:
		tq2 = tc2
	var dtc: float = tq2 - tc2
	var blow_id: int = int(ex.n) * 64 + int(strikes[best][1])
	if _seen_tc != blow_id:
		# a blow the sim announced fewer than 4 ticks ahead cannot be wound up (the pose pops to its contact key)
		_seen_tc = blow_id
		debug["blows"] += 1
		var kcount: Dictionary = debug.get("keysets", {})
		kcount[ksid] = int(kcount.get(ksid, 0)) + 1
		debug["keysets"] = kcount
		if T > tc2 - 3.5 * DT:
			debug["late"] += 1
			if debug["late_notes"].size() < 30:
				debug["late_notes"].append("tick %d %s (%s), %s, the blow %.0f ms ahead" % [S.tick, ex.kind, ex.tag, role, (tc2 - T) * 1000.0])
	if T < tc2 - 0.0001:
		_tc_left = tc2 - T
	debug["parts"] += 1
	var ul: float = 1.0
	var hw: float = 1.0   # how far the hand has taken its tip: closes over the wind-up, opens over the return
	if pstyle != "":
		_press_track(blow_id, T)
	if dtc < -Sn:
		var u: float = clampf((tq2 - (tc2 - L2)) / maxf(L2 - Sn, DT), 0.0, 1.0)
		ul = u
		hw = smoothstep(0.0, 0.7, u)
		u = pow(u, float(prof.get("load_ease", 2.0)))
		_mix_pose(pc, u)
		if pstyle != "" and bool(prow.get("carry", false)):
			_press_carry(prow, blow_id, tq2 - (tc2 - L2))
	elif dtc < 0.0:
		_set_pose(pc)
		var u2: float = clampf((tq2 - (tc2 - Sn)) / Sn, 0.0, 1.0)
		AnimPose.mix_lag(q, pk.q, u2, _lag, 1.0)
		hips = pc.hips.lerp(pk.hips, u2)
		curl = pc.curl.lerp(pk.curl, u2)
	elif dtc < F:
		_set_pose(pk)
		var u3: float = clampf((dtc - H) / maxf(F - H, DT), 0.0, 1.0)
		var w3: float = _ease_out_back(u3, float(prof.get("overshoot", 0.1)) * (0.6 + 0.8 * bw2))
		AnimPose.mix(q, pf.q, w3)
		hips = pk.hips.lerp(pf.hips, w3)
		curl = pk.curl.lerp(pf.curl, w3)
	else:
		_set_pose(pf)
		var u4: float = clampf((dtc - F) / R, 0.0, 1.0)
		var w4: float = u4 * u4 * (3.0 - 2.0 * u4)
		if pstyle != "":
			AnimPose.mix(q, _base, w4)   # the return of a styled blow goes by swing and twist: a plain slerp from a long follow-through folds the arm's own twist
		else:
			for i in range(AnimRig.N):
				q[i] = q[i].slerp(_base[i], w4)
		hips = pf.hips.lerp(_base_hips, w4)
		curl = pf.curl.lerp(_base_curl, w4)
		hw = 1.0 - w4
	if pstyle != "":
		_press_body(prow, pstyle, ul, dtc, Sn, H, F, bw3, ks, side, blow_id, best, tc2, T)
		if not press.is_empty():
			press["sure"] = bool(bargs.get("sure", false))
			press["riposte"] = bool(bargs.get("_rip", false))
	if RenderAnim.hand_tips:
		_hand_tip(ks, side, heavy2, strikes[best][2], hw)
	if RenderAnim.press_styles and bool(bargs.get("check", false)):
		_guard_layer(ks, side, hw)
	# the step from the wind-up into the contact key is the blow itself (on twos it is one step): inertialisation must not take it for a join
	# and smooth it over the next 0.1 s, or the fist reaches the defender late (--blowjoin restores the old behaviour for an A/B)
	_blow_snap = dtc >= -(Sn + dq) - 0.0001 and dtc <= 0.0001
	# timing fidelity: on the frame of contact the pose must be the contact key (checked before the contact solve moves it)
	if absf(T - tc2) < DT * 0.5:
		var err: float = 0.0
		var eb: int = 0
		for i in range(AnimRig.N):
			var ea: float = q[i].angle_to(pk.q[i])
			if ea > err:
				err = ea
				eb = i
		if err > float(debug["contact_err_max"]):
			debug["contact_err_bone"] = AnimRig.BONES[eb][0]
		_contact_now = true
		debug["contact_frames"] += 1
		debug["contact_err_max"] = maxf(float(debug["contact_err_max"]), err)
	# the contact solve's weight: in over the snap, full at the contact tick and through the first of the follow-through,
	# out over the rest of it
	var cw: float = 0.0
	if dtc >= -Sn and dtc < F + R * 0.4:
		if dtc < 0.0:
			cw = smoothstep(0.0, 1.0, (dtc + Sn) / Sn)
		elif dtc < H + (F - H) * 0.5:
			cw = 1.0
		else:
			cw = 1.0 - smoothstep(H + (F - H) * 0.5, F + R * 0.4, dtc)
	if cw > 0.001:
		_ci_w = cw
		_ci_limb = String(ks.get("limb", "hand_r"))
		_ci_step = float(ks.get("step_max", -1.0))
		_ci_limb2 = String(ks.get("limb2", ""))
		_ci_target = String(ks.get("target", "chest")) if RenderAnim.force_target == "" else RenderAnim.force_target
		_ci_side = side
		_ci_opp = ex.D if role == "A" else ex.A
		_ci_tc = tc2
		_ci_dmg = float(strikes[best][2].get("dmg", 0.0))


## The guard layer a check is thrown over (data/anim/press_styles.json `guard`): the arm that is not striking stays in the guard pose, closing over the wind-up and opening over the return. A hand
## or elbow blow guards the other arm; a foot, knee or head blow guards both. Only the arm bones and the finger curl change, so the strike and its contact solve are the key set's own.
func _guard_layer(ks: Dictionary, side: bool, w: float) -> void:
	var G: Dictionary = AnimData.press.get("guard", {})
	if G.is_empty() or w <= 0.001 or not AnimData.pose_exists(String(G.get("pose", ""))):
		return
	var gp: AnimPose = AnimData.pose(String(G.pose))
	var lb: String = String(ks.get("limb", "hand_r"))
	var right_strikes: bool = lb.ends_with("r") != side if lb.contains("_") else true
	var sides: Array = []
	if lb.begins_with("hand") or lb.begins_with("elbow"):
		sides = ["l" if right_strikes else "r"]
	else:
		sides = ["l", "r"]
	var ww: float = clampf(w * float(G.get("weight", 0.9)), 0.0, 1.0)
	for sd in sides:
		for bn in G.get("bones", []):
			var bi: int = AnimRig.index[String(bn) + "_" + String(sd)]
			_press_slerp(bi, gp.q[bi], ww)
		if sd == "l":
			curl.x = lerpf(curl.x, gp.curl.x, ww)
		else:
			curl.y = lerpf(curl.y, gp.curl.y, ww)
	debug["guards"] = int(debug.get("guards", 0)) + 1


## The hand state of a blow's tip (data/anim/tips.json): the beat's own `tip` (the generator's move part), else the fighter's rule for a key set posed
## with another tip (the rival's heavies close to a fist). Only the striking hand's finger curl changes, by `w`; nothing about the pose, the reach
## or the contact solve does. Blade and palm are both an open hand on this rig (one curl a hand).
func _hand_tip(ks: Dictionary, side: bool, heavy: bool, args: Dictionary, w: float) -> void:
	var T: Dictionary = AnimData.tips
	if T.is_empty() or not bool(T.get("enabled", true)) or w <= 0.001:
		return
	var curls: Dictionary = T.get("curl", {})
	var want: String = String(args.get("tip", ""))
	if not curls.has(want):
		var rule: Dictionary = T.get("swap", {}).get(pair_key, {}).get("heavy" if heavy else "light", {})
		want = String(rule.get(String(ks.get("tip", "")), ""))
	if not curls.has(want):
		return
	var target: float = float(curls[want])
	for key in ["limb", "limb2"]:
		var lb: String = String(ks.get(key, ""))
		if not lb.begins_with("hand"):
			continue
		var right: bool = lb.ends_with("r")
		if side:
			right = not right
		if right:
			curl.y = lerpf(curl.y, target, w)
		else:
			curl.x = lerpf(curl.x, target, w)
	debug["hand_tips"] = int(debug.get("hand_tips", 0)) + 1


## An intro gesture (Narrative's intents: acknowledge, appraise, dismiss, defer, brace, ease, claim, check, soften, harden; docs/narrative/dynamic-intros.md section 11): the sim's
## `intro_gesture {actor, intent, at}` starts the fighter's own small motion for the intent from data/anim/pair_live.json `gestures` (a sequence or a pose, a weight, the groups of bones it
## plays on: head, torso, arms, legs, in `gesture_groups`), played over whatever he is standing in and eased in and out. A fighter with no motion for the intent plays nothing (a silent skip).
func on_gesture(intent: String, T: float, tick_clock: bool = false) -> void:
	if not RenderAnim.pair_live or not RenderAnim.intro_gestures or pair_key == "":
		return
	var tbl: Dictionary = AnimData.pair.get("roles", {}).get(pair_key, {}).get("gestures", {})
	if not tbl.has(intent):
		debug["gesture_skips"] = int(debug.get("gesture_skips", 0)) + 1
		return
	var g: Dictionary = tbl[intent]
	var id: String = String(g.get("seq", ""))
	var dur: float = 0.0
	var is_pose: bool = false
	if AnimData.entries.has(id):
		dur = float(AnimData.entries[id].dur) / 60.0
	elif AnimData.pose_exists(id):
		dur = float(g.get("hold", 0.5))
		is_pose = true
	else:
		debug["gesture_skips"] = int(debug.get("gesture_skips", 0)) + 1
		return
	_gest = {"id": id, "t0": T, "dur": dur, "w": float(g.get("w", 1.0)), "mask": g.get("mask", ["head", "torso", "arms"]), "pose": is_pose, "intent": intent, "tb": tick_clock}
	debug["gestures"] = int(debug.get("gestures", 0)) + 1


func _gesture_layer(S: SimState, f, T: float) -> void:
	var g: Dictionary = _gest
	if bool(g.tb):
		T = float(S.tick) / 60.0   # the intro runs on the sim's tick: its clock is frozen at 0 until the match starts
	var u: float = T - float(g.t0)
	var fade: float = 0.15
	if u < -0.0001:
		return
	if u > float(g.dur) + fade or f.state == "launched" or f.state == "down" or _en_busy(S, f):
		_gest = {}
		return
	var env: float = smoothstep(0.0, 0.1, u) * (1.0 - smoothstep(float(g.dur), float(g.dur) + fade, u)) * float(g.w) * (0.6 if RenderAnim.reduced_motion else 1.0)
	if env <= 0.001:
		return
	var base_q: Array[Quaternion] = q.duplicate()
	var base_h: Vector3 = hips
	var base_c: Vector2 = curl
	if bool(g.pose):
		_mix_pose(AnimData.pose(String(g.id)), 1.0)
	else:
		_entry_layer(float(g.t0), float(g.dur), String(g.id), T, DT, 1.0)
	# only the groups of bones the gesture plays on take it (a bow does not move the feet); the rest keep what they were
	var on: Dictionary = {}
	var groups: Dictionary = AnimData.pair.get("gesture_groups", {})
	for gn in g.mask:
		for bn in groups.get(String(gn), []):
			on[int(AnimRig.index[String(bn)])] = true
	var all: bool = on.is_empty()
	for i in range(AnimRig.N):
		if all or on.has(i):
			q[i] = base_q[i].slerp(q[i], env)
		else:
			q[i] = base_q[i]
	if all or on.has(int(AnimRig.index["pelvis"])):
		hips = base_h.lerp(hips, env)
	else:
		hips = base_h
	curl = base_c.lerp(curl, env) if (all or on.has(int(AnimRig.index["hand_r"]))) else base_c


## The press style of a blow (data/anim/press_styles.json, only with RenderAnim.press_styles): the beat's own `style` when the director
## stamps one, else the striker's press log read now (Controls' SimPressRead through DirAlchemy.read: rhythm = tech, mash = speed), a
## heavy blow is always heavy. "" is the profile the blow plays today. Decided the first time the blow is seen, then kept.
func _press_style(S: SimState, f, ex, sk: Array) -> String:
	if not RenderAnim.press_styles or AnimData.press.is_empty() or not bool(AnimData.press.get("enabled", true)):
		return ""
	var key: int = int(ex.n) * 64 + int(sk[1])
	if _pr_cache.has(key):
		return String(_pr_cache[key])
	if _pr_cache.size() > 32:
		_pr_cache.clear()
	var rows: Dictionary = AnimData.press.get("styles", {})
	var st: String = ""
	var args: Dictionary = sk[2]
	if rows.has(String(args.get("style", ""))):
		st = String(args.style)
	elif String(sk[3]) == "heavy":
		st = "heavy"
	elif f.act.dirI.size() >= DirAlchemy.SIZE:
		var rd: Dictionary = DirAlchemy.read(S, f)
		st = String(AnimData.press.get("read", {}).get(String(rd.get("style", "none")), ""))
		if st == "heavy":
			st = ""   # a hold that is not a heavy blow (a light one thrown during the press) plays as it does today
	_pr_cache[key] = st
	var pn: Dictionary = debug.get("press_n", {})
	pn[st] = int(pn.get(st, 0)) + 1
	debug["press_n"] = pn
	return st


## The style's timing over the part's, then what the beat tells (B0's fields, data/anim/press_styles.json `beat`): a tech blow's grade sets its held beat (a perfect press holds the full
## beat, a good one less, an off one least); the closing blow of a string and a string's ender hold their follow-through at least that long (the blow that ends it lands and stays);
## a held heavy winds up for its whole time and a tapped one for less.
func _press_prof(sp: Dictionary, style: String, args: Dictionary = {}) -> Dictionary:
	var out: Dictionary = sp.duplicate()
	out.merge(AnimData.press.get("styles", {}).get(style, {}).get("prof", {}), true)
	var B: Dictionary = AnimData.press.get("beat", {})
	if B.is_empty():
		return out
	if style == "tech" and args.has("grade"):
		var g: Dictionary = B.get("grade", {}).get(String(args.grade), {})
		if g.has("hold_ticks"):
			out["hold_ticks"] = int(g.hold_ticks)
	if bool(args.get("closing", false)) or bool(args.get("ender", false)):
		out["hold_ticks"] = maxi(int(out.get("hold_ticks", 0)), int(B.get("closing", {}).get("hold_ticks", 0)))
	if style == "heavy" and args.has("charge"):
		var lm: float = float(B.get("charge", {}).get("load_min", 1.0))
		var lt: Dictionary = (out.get("load_ticks", {}) as Dictionary).duplicate()
		var ck: float = lerpf(lm, 1.0, clampf(float(args.charge), 0.0, 1.0))
		for key in lt:
			lt[key] = maxi(1, roundi(float(lt[key]) * ck))
		out["load_ticks"] = lt
	return out


## A new blow of a styled string: what the last blow ended on is kept to blend out of (a blow with `carry`).
func _press_track(blow_id: int, T: float) -> void:
	if _pr_blow == blow_id:
		return
	_pc_ok = T - _pr_T <= 0.05 and _pr_q.size() == AnimRig.N
	if _pc_ok:
		_pc_q = _pr_q.duplicate()
		_pc_hips = _pr_hips
		_pc_curl = _pr_curl
	_pr_blow = blow_id


## The retract, blended: the new blow's wind-up starts from the pose the last blow was in, not from the guard, so the arm that
## struck eases back while the other one comes through.
func _press_carry(row: Dictionary, blow_id: int, tl: float) -> void:
	var span: float = float(row.get("carry_ticks", 5)) * DT
	if not _pc_ok or span <= 0.0 or tl >= span or _pr_blow != blow_id:
		return
	var k: float = smoothstep(0.0, 1.0, maxf(tl, 0.0) / span)
	var tmp: Array[Quaternion] = q.duplicate()
	for i in range(AnimRig.N):
		q[i] = _pc_q[i]
	AnimPose.mix(q, tmp, k)
	hips = _pc_hips.lerp(hips, k)
	curl = _pc_curl.lerp(curl, k)


func _press_slerp(i: int, to: Quaternion, w: float) -> void:
	var qa: Quaternion = q[i]
	if AnimJoints.ik_limits and AnimJoints.is_limb[i] == 1 and absf(qa.dot(to)) <= 0.9:
		q[i] = AnimJoints.slerp_limb(qa, to, w)
	else:
		q[i] = qa.slerp(to, w)


## The body of a styled blow on top of its key poses, all inside the joints' range (the squash is a blend of two valid poses, the
## stretch and the twist are a few hundredths of a radian on the spine, which has no limit of its own, and the limb pass
## runs after): the wind-up squashes (legs, pelvis and spine toward the squash pose, a lean back), the release stretches (the spine
## extends, the hips rise, the body is drawn ahead for a few ticks: the smear frame is the tick before contact), a mashed blow twists
## the loose body toward the striking side. Fills `press` for VFX.
func _press_body(row: Dictionary, style: String, ul: float, dtc: float, sn: float, hold: float, F: float, bw: float, ks: Dictionary, side: bool, blow_id: int, nth: int, tc: float, T: float) -> void:
	var ix: Dictionary = AnimRig.index
	var wsc: float = clampf(0.55 + 0.45 * bw, 0.4, 1.0)
	var s: float = 0.0
	var sq: float = float(row.get("squash", 0.0))
	if sq > 0.0:
		s = smoothstep(0.0, 0.85, ul) * (1.0 - smoothstep(-3.0 * DT, 0.0, dtc))   # gone on the contact tick: the key pose is the blow
		s *= sq * wsc
		if s > 0.001:
			var sp: Dictionary = AnimData.press.get("squash_pose", {})
			var pid: String = String(sp.get("by_fighter", {}).get(pair_key, sp.get("default", "brace")))
			if AnimData.pose_exists(pid):
				var spp: AnimPose = AnimData.pose(pid)
				for bn in sp.get("bones", []):
					var bi: int = ix[String(bn)]
					_press_slerp(bi, spp.q[bi], s)
				hips.y = minf(hips.y, lerpf(hips.y, spp.hips.y, s))
			var lean: float = float(row.get("lean_back", 0.0)) * s
			q[ix["spine_1"]] = q[ix["spine_1"]] * Quaternion(Vector3(0, 0, 1), lean * 0.5)
			q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 0, 1), lean * 0.5)
			q[ix["head"]] = q[ix["head"]] * Quaternion(Vector3(0, 0, 1), -lean * 0.5)
			_pr_dx -= float(row.get("back", 0.0)) * s
	var st: Dictionary = row.get("stretch", {})
	var e: float = 0.0
	var smear_t: float = float(st.get("smear_ticks", 0)) * DT
	var st_t: float = float(st.get("ticks", 3)) * DT
	var smear: bool = false
	if float(st.get("spine", 0.0)) > 0.0 or float(st.get("lead", 0.0)) > 0.0 or float(st.get("rise", 0.0)) > 0.0:
		if dtc < -smear_t - 0.0001:
			e = smoothstep(-(smear_t + 2.0 * DT), -smear_t, dtc)
		elif dtc < 0.0001:
			e = 1.0
			smear = smear_t > 0.0 and dtc < -0.0001
		else:
			e = 1.0 - smoothstep(0.0, st_t, dtc)
		e *= wsc
		if e > 0.001:
			var ext: float = float(st.get("spine", 0.0)) * e
			q[ix["spine_1"]] = q[ix["spine_1"]] * Quaternion(Vector3(0, 0, 1), -ext * 0.5)
			q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 0, 1), -ext * 0.5)
			q[ix["head"]] = q[ix["head"]] * Quaternion(Vector3(0, 0, 1), ext * 0.4)
			hips.y += float(st.get("rise", 0.0)) * e
			_pr_dx += float(st.get("lead", 0.0)) * e
	var tw: float = float(row.get("twist", 0.0))
	var twk: float = 0.0
	if tw > 0.0:
		var tw_t: float = float(row.get("twist_ticks", 6)) * DT
		twk = smoothstep(-3.0 * DT, 0.0, dtc) if dtc < 0.0 else 1.0 - smoothstep(0.0, tw_t, dtc)
		var ang: float = tw * twk * (1.0 if side else -1.0)
		q[ix["spine_1"]] = q[ix["spine_1"]] * Quaternion(Vector3(0, 1, 0), ang * 0.5)
		q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 1, 0), ang * 0.5)
		q[ix["pelvis"]] = q[ix["pelvis"]] * Quaternion(Vector3(0, 1, 0), -ang * 0.3)
	var phase: String = "load"
	if dtc >= 0.0001:
		phase = "contact" if dtc < 1.5 * DT else ("hold" if dtc < hold else ("follow" if dtc < F else "return"))
	elif smear:
		phase = "smear"
	elif dtc >= -sn - 0.0001:
		phase = "release"
	var limb: String = String(ks.get("limb", "hand_r"))
	if side and limb.contains("_"):
		limb = limb.substr(0, limb.length() - 1) + ("l" if limb.ends_with("r") else "r")
	var bone: String = limb.replace("elbow", "forearm").replace("knee", "shin")
	if not ix.has(bone):
		bone = "hand_r"
	press = {"style": style, "phase": phase, "blow": blow_id, "ordinal": nth, "ticks_to_contact": roundi((tc - T) / DT), "limb": limb, "bone": bone, "side": side, "smear": smear, "ghosts": int(row.get("ghosts", 0)), "weight": bw, "squash": s, "stretch": e, "twist": twk, "dx": _pr_dx}
	if _pr_q.size() != AnimRig.N:
		_pr_q.resize(AnimRig.N)
	for i in range(AnimRig.N):
		_pr_q[i] = q[i]
	_pr_hips = hips
	_pr_curl = curl
	_pr_T = T


## What VFX reads (after a solve, while `press` is not empty and for a moment after): the afterimages are `press_pose(k)`, the pose k
## solves back (0 is the newest), and the smear wedge runs along `press_path` (the striking limb's tip at each of those solves).
func _press_record(T: float, f = null) -> void:
	if not press.is_empty() or not zip.is_empty():
		var snap: Array[Quaternion] = q.duplicate()
		var wp := Vector2.ZERO
		if f != null:
			wp = Vector2(f.x, f.y)
		press_ring.append({"T": T, "q": snap, "hips": hips, "root_off": root_off, "pos": wp, "vface": vface})
		press_path.append({"T": T, "tip": socket(String(press.get("bone", zip.get("bone", "hand_r"))))})
		if not zip.is_empty():
			zip_path.append({"T": T, "x": wp.x, "y": wp.y, "phase": String(zip.get("phase", ""))})
	while press_ring.size() > 12 or (not press_ring.is_empty() and T - float(press_ring[0].T) > 0.4):
		press_ring.pop_front()
	while press_path.size() > 12 or (not press_path.is_empty() and T - float(press_path[0].T) > 0.4):
		press_path.pop_front()
	while zip_path.size() > 24 or (not zip_path.is_empty() and T - float(zip_path[0].T) > 0.8):
		zip_path.pop_front()


## The pose `back` solves ago ({T, q, hips, root_off}), newest 0; {} when there is none.
func press_pose(back: int) -> Dictionary:
	var n: int = press_ring.size()
	return press_ring[n - 1 - back] if back >= 0 and back < n else {}


## The defensive and clash beats of the exchange (the sim's own: wind, slip, dodge, guardBreak, clashWave). Each is a short
## eased pose layer on the fighter it happens to, timed from the beat; a blow's own parts go over the top of them.
func _beat_layers(ex, role: String, t0: float, T: float) -> void:
	for b in ex.beats:
		var bt: float = t0 + b.t
		if T < bt - 0.2 or T > bt + 0.7:
			if b.op != "wind":
				continue
		match b.op:
			"wind":
				# the wind-up window: from the beat to the parryable blow that follows it
				var te: float = -1.0
				for b2 in ex.beats:
					if b2.op == "strike" and String(b2.args.a) == "A" and b2.t > b.t:
						te = t0 + b2.t
						break
				if te < 0.0 or T < bt or T > te + 0.4:
					continue
				var ready: float = smoothstep(bt, bt + 0.1, T) * (1.0 - smoothstep(te - 0.02, te + 0.1, T))
				if ready > 0.001 and not ex.cancel and role == "D":
					_mix_pose(AnimData.pose("def.parry_ready"), ready * 0.85)
				if ex.cancel:
					# parried: the defender sweeps the blow aside, the attacker is turned off line
					var pw: float = smoothstep(te - 0.02, te + 0.03, T) * (1.0 - smoothstep(te + 0.12, te + 0.3, T))
					if pw > 0.001:
						_mix_pose(AnimData.pose("def.parry" if role == "D" else "react.rebuff"), pw)
			"slip":
				if role == "D":
					_window_pose("def.slip", T - bt, 0.05, 0.15, 0.35)
			"dodge":
				if role == "D":
					if String(b.args.get("side", "")) == "cross":
						# the step-around: up and over the attacker, then down behind him (two moves, a few ticks each)
						var leg: float = float(b.args.get("dur", 0.15)) * 0.5
						var tau: float = T - bt
						if tau >= -0.02 and tau < 2.0 * leg + 0.2:
							_skip_inertia = true   # the two moves are a few ticks each: the pose pulses must show, not be smoothed away
						if tau >= 0.0 and tau < leg + 0.05:
							_mix_pose(AnimData.pose("def.hop"), smoothstep(0.0, 0.025, tau) * (1.0 - smoothstep(leg - 0.04, leg + 0.04, tau)))
						if tau >= leg - 0.05 and tau < 2.0 * leg + 0.2:
							_mix_pose(AnimData.pose("def.drop"), smoothstep(leg - 0.04, leg + 0.03, tau) * (1.0 - smoothstep(2.0 * leg - 0.02, 2.0 * leg + 0.15, tau)))
					else:
						_window_pose("def.blink_in", T - bt, 0.04, 0.12, 0.3)
			"guardBreak":
				if role == "D":
					_window_pose("def.guard_break", T - bt, 0.05, 0.25, 0.5)
			"clashWave":
				_window_pose("clash.push", T - bt, 0.05, 0.2, 0.4)


## A pose over the time since its beat: eased in over `rise`, held to `hold`, eased out by `end`.
func _window_pose(id: String, t: float, rise: float, hold: float, end: float) -> void:
	if t < 0.0 or t > end:
		return
	var w: float = smoothstep(0.0, rise, t) * (1.0 - smoothstep(hold, end, t))
	if w > 0.001:
		_mix_pose(AnimData.pose(id), w)


# ------------------------------------------------------------------ contact: the striking limb reaches the defender

## The defender's body point for a region (data/anim/sockets.json regions), in the defender's model space.
func _region_point(name: String) -> Vector3:
	var regs: Dictionary = AnimData.sockets.get("regions", {})
	var r: Dictionary = regs.get(name, regs.get("chest", {}))
	var bn: String = String(r.get("bone", "spine_2"))
	var off: Array = r.get("offset", [0, 3, 0])
	var bi: int = AnimRig.index[bn]
	if not _full_fk and not SOCKET_SET.has(bn):
		AnimPose.fk(q, hips, gq, gp)
		_full_fk = true
	return gp[bi] + gq[bi] * Vector3(float(off[0]), float(off[1]), float(off[2])) + root_off


## How far the striking part can reach from its root (the shoulder, the hip, the spine): the length of the chain for a two-bone
## limb, the distance to the tip for an aimed one. The tip is where the blow lands from (the wrist, the elbow, the head).
func _strike_tip(sk: Dictionary, a: int, b: int, c: int) -> Vector3:
	if String(sk.get("mode", "ik")) == "ik":
		return gp[c]
	var off: Array = sk.get("tip_offset", [0, 0, 0])
	return gp[c] + gq[c] * Vector3(float(off[0]), float(off[1]), float(off[2]))


func _strike_reach(sk: Dictionary, a: int, b: int, c: int) -> float:
	if String(sk.get("mode", "ik")) == "ik":
		return (gp[b] - gp[a]).length() + (gp[c] - gp[b]).length() - 0.5
	return (_strike_tip(sk, a, b, c) - gp[a]).length()


## IK the striking hand or foot onto the defender's body: the wrist (ankle) goes to the region's surface minus the fist, so
## the fist lands on him on the contact tick, and the hips lunge for what the arm cannot reach. The authored limb blends
## into the solved one by the contact weight, so the key poses stay the look and the defender's actual position decides
## where it lands. Nothing happens if the opponent is behind the thrower (the face override turns him first).
func _contact_ik(S: SimState, f) -> void:
	_contact_one(S, f, _ci_limb, false)
	if _ci_limb2 != "":
		_contact_one(S, f, _ci_limb2, true)


## One striking limb onto the defender. A second limb of the same blow (second = true) reaches from where the first left the body:
## the hips' lunge is already in the pose and the step-in in `_step_x`, so it only asks for what is still missing.
func _contact_one(S: SimState, f, base_limb: String, second: bool) -> void:
	var opp = _ci_opp
	if opp == null or _ci_dmg <= 0.0:
		return
	var oaf: AnimFighter = RenderAnim.fighter(S, opp)
	if oaf.version == 0:
		return
	var limb: String = base_limb
	var sided: bool = base_limb.contains("_")
	if _ci_side and sided:
		limb = base_limb.substr(0, base_limb.length() - 1) + ("l" if base_limb.ends_with("r") else "r")
	var kind: String = limb.get_slice("_", 0)
	var sk: Dictionary = AnimData.sockets.get("limbs", {}).get(kind, {})
	if sk.is_empty():
		return
	var aim: bool = String(sk.get("mode", "ik")) == "aim"
	var sfx: String = limb.substr(limb.length() - 1) if sided else "r"
	var zs: float = (1.0 if sfx == "r" else -1.0) if sided else 0.0
	var end_len: float = float(sk.get("end_len", 6.0))
	var lunge_max: float = float(sk.get("lunge_max", 8.0))
	var step_max: float = float(sk.get("step_max", 10.0)) if _ci_step < 0.0 else _ci_step
	var dx: float = SimWrap.sdx(f.x, opp.x)
	var rp: Vector3 = oaf._region_point(_ci_target)
	var mx: float = (dx + oaf.vface * rp.x) * vface
	if mx < 4.0:
		return
	var my: float = (opp.y - f.y) + rp.y
	if not press.is_empty():
		# a styled blow's held beat keeps the point it landed on: the defender's recoil must not drag the arm after him through the hold
		var li: int = 1 if second else 0
		if S.T >= _ci_tc - 0.0001:
			if absf(float(_ci_latch_tc[li]) - _ci_tc) > 0.0001:
				_ci_latch_tc[li] = _ci_tc
				_ci_latch[li] = Vector2(mx, my)
			else:
				mx = _ci_latch[li].x
				my = _ci_latch[li].y
	var surf: float = float(AnimData.sockets.get("regions", {}).get(_ci_target, {}).get("surface", 6.5))
	var tgt := Vector3(mx - surf - end_len - _smear_now(S.T) - _pr_dx - (_step_x if second else 0.0), my, float(sk.get("reach_z", 5.0)) * zs)
	var ix: Dictionary = AnimRig.index
	var a: int
	var b: int
	var c: int
	if aim:
		var abn: String = String(sk["bone"])
		a = ix[abn + "_" + sfx] if (sided and ix.has(abn + "_" + sfx)) else ix[abn]   # a bone with a side (upper_arm) or one without (spine_1)
		b = a
		c = ix[String(sk["tip"]) + ("_" + sfx if sided else "")]
	else:
		var ch: Array = sk["chain"]
		a = ix[String(ch[0]) + "_" + sfx]
		b = ix[String(ch[1]) + "_" + sfx]
		c = ix[limb]
	AnimPose.fk(q, hips, gq, gp)
	_full_fk = true
	var reach: float = _strike_reach(sk, a, b, c)
	# how far forward the shoulder must come (along x, the way the hips and the step move it) for the arm to just reach
	var d3: Vector3 = tgt - gp[a]
	var side2: float = d3.y * d3.y + d3.z * d3.z
	var need: float = d3.x - (sqrt(reach * reach - side2) if side2 < reach * reach else 0.0)
	need = maxf(need, 0.0)
	# the clavicle hunch brings the shoulder to meet it first (an arm blow; the legs and the head have no shoulder to give)
	var hm: float = float(sk.get("hunch_max", 0.0))
	if hm > 0.0 and need > 0.0 and AnimPose.hunch_auto and sided:
		AnimPose.hunch(q, sfx, minf(need, hm) * _ci_w, 0.0)
		AnimPose.fk(q, hips, gq, gp)
		reach = _strike_reach(sk, a, b, c)
		d3 = tgt - gp[a]
		side2 = d3.y * d3.y + d3.z * d3.z
		need = maxf(d3.x - (sqrt(reach * reach - side2) if side2 < reach * reach else 0.0), 0.0)
	# above or below the arm's reach whatever the lunge (a defender on another level): the excess is what is left over
	var excess: float = need - lunge_max - step_max
	if side2 >= reach * reach:
		excess = sqrt(side2) - reach
	elif need <= 0.0 and d3.length() > reach:
		excess = d3.length() - reach   # the target is behind the shoulder (the fighters overlap): no step forward helps
	var lunge: float = minf(need, lunge_max) * _ci_w
	var step: float = clampf(need - lunge_max, 0.0, step_max) * _ci_w
	_step_x = (_step_x + step) if second else step
	var ik_t: Vector3 = tgt - Vector3(lunge + step, 0.0, 0.0)
	var qa0: Quaternion = q[a]
	var qb0: Quaternion = q[b]
	var gap: float
	if aim:
		# one bone turned so the tip points at the target: what the joint cannot reach is the gap, what is nearer is overshoot
		var tip: Vector3 = _strike_tip(sk, a, b, c)
		var dir_now: Vector3 = (tip - gp[a]).normalized()
		var dir_to: Vector3 = (ik_t - gp[a]).normalized()
		var qa_new: Quaternion = Quaternion(dir_now, dir_to) * gq[a]
		q[a] = gq[AnimRig.parent[a]].inverse() * qa_new
		q[a] = qa0.slerp(q[a], _ci_w)
		gap = maxf(0.0, (ik_t - gp[a]).length() - reach)
	else:
		var pole: Vector3 = gp[b]   # the elbow (knee) stays on the side the authored pose has it, so the solved limb is its neighbour
		AnimPose.ik_limb(q, gq, gp, a, b, c, ik_t, pole, 1.0 if kind == "hand" else -1.0, _rd.shape_key)
		q[a] = AnimJoints.slerp_limb(qa0, q[a], _ci_w) if AnimJoints.ik_limits else qa0.slerp(q[a], _ci_w)
		q[b] = qb0.slerp(q[b], _ci_w)
		gap = maxf(0.0, (ik_t - gp[c]).length())
		if gap > 0.0 and (gp[c] - gp[a]).length() > (ik_t - gp[a]).length():
			gap = 0.0   # the limb folds no further than its joint allows, so it ends past a target that is nearer than that (inside the defender), not short of it
	hips.x += lunge
	debug["ik_frames"] += 1
	if second:
		if absf(S.T - _ci_tc) < DT * 0.5 and _ci_w > 0.99:
			debug["gap2_max"] = maxf(float(debug.get("gap2_max", 0.0)), gap)
			debug["gap2_n"] = int(debug.get("gap2_n", 0)) + 1
	elif absf(S.T - _ci_tc) < DT * 0.5 and _ci_w > 0.99 and _ci_tc != _gap_tc:
		_gap_tc = _ci_tc
		debug["gap_max"] = maxf(float(debug["gap_max"]), gap)
		debug["gap_sum"] += gap
		debug["gap_n"] += 1
		debug["gaps"].append(snappedf(gap, 0.1))
		debug["gaps"].append(snappedf(excess, 0.1))
		if gap > 1.5 and debug["notes"].size() < 40:
			var ex = S.dirS.ex
			debug["notes"].append("tick %d %s (%s): target %.0f u ahead, short by %.1f (need %.1f, lunge %.1f, step %.1f), %s %s dmg %.0f" % [S.tick, ex.kind if ex != null else "-", ex.tag if ex != null else "-", mx, gap, need, lunge, step, _ci_target, limb, _ci_dmg])


func _inertialise(dt: float, prof: Dictionary) -> void:
	var n: int = AnimRig.N
	if (_skip_inertia or not _form.is_empty()) and _ip_have:
		# a transformation plays inside a pause (no ticks, so nothing to settle a join on): its own beats are the easing, and
		# the way back into the fight is inertialised once the ticks run again
		for i in range(n):
			_ip_raw[i] = q[i]
			_ip_cur[i] = Quaternion.IDENTITY
		_ip_hraw = hips
		_ip_hcur = Vector3.ZERO
		_ip_active = false
		return
	if not _ip_have:
		_ip_have = true
		_ip_raw.resize(n)
		_ip_off.resize(n)
		_ip_cur.resize(n)
		for i in range(n):
			_ip_raw[i] = q[i]
			_ip_off[i] = Quaternion.IDENTITY
			_ip_cur[i] = Quaternion.IDENTITY
		_ip_hraw = hips
		return
	var jump: bool = false
	for i in range(n):
		if absf(q[i].dot(_ip_raw[i])) < _ip_cos:
			jump = true
			break
	if jump and _blow_snap and not RenderAnim.blow_join:
		jump = false
	if jump:
		# continuity: what was drawn last solve (the old pose with the offset it carried) minus the new raw pose
		for i in range(n):
			_ip_off[i] = (_ip_cur[i] * _ip_raw[i]) * q[i].inverse()
		_ip_hoff = (_ip_hraw + _ip_hcur) - hips
		_ip_t = 0.0
		_ip_active = true
		# a join close to a blow must be spent by the contact tick (the contact key is exact), so it settles in the time left
		var dur0: float = float(prof.get("inertia_s", INERTIA_SNAPPY if float(prof.get("solve_hz", 60.0)) < 59.0 else INERTIA_FLUID))
		_ip_dur = dur0 if _tc_left < 0.0 else minf(dur0, maxf(_tc_left - 2.0 * DT, 0.0))
	for i in range(n):
		_ip_raw[i] = q[i]
	_ip_hraw = hips
	if not _ip_active:
		_ip_acc = 0.0
		return
	var idt: float = minf(_ip_acc, 0.1)
	_ip_acc = 0.0
	_ip_t += idt
	var w: float = 1.0 - smoothstep(0.0, _ip_dur, _ip_t) if _ip_dur > 0.0 else 0.0
	for i in range(n):
		var o: Quaternion = Quaternion.IDENTITY.slerp(_ip_off[i], w)
		_ip_cur[i] = o
		q[i] = o * q[i]
	_ip_hcur = _ip_hoff * w
	hips += _ip_hcur
	if w <= 0.0:
		_ip_active = false
		for i in range(n):
			_ip_cur[i] = Quaternion.IDENTITY
		_ip_hcur = Vector3.ZERO


## How far behind the anchor the body is drawn now (the contact catch's smear); the contact solve reaches that much further.
func _smear_now(T: float) -> float:
	if _smear == 0.0:
		return 0.0
	var sp: float = (T - _smear_t0) / 0.05
	return 0.0 if sp >= 1.0 else _smear * (1.0 - smoothstep(0.0, 1.0, sp))


func _set_lag(k: float) -> void:
	if k == _lag_k:
		return
	_lag_k = k
	for i in range(AnimRig.N):
		_lag[i] = AnimData.bone_lag[i] * k / 0.45


## The timing profile for a kind of part (light, heavy, chain, rush, launch, power): Orb's mix unless a style is forced.
func _part_prof(kind: String) -> Dictionary:
	if RenderAnim.style_forced():
		return _prof
	var nm: String = String(AnimData.by_part.get(kind, ""))
	return AnimData.profile(nm) if nm != "" else _prof


# ------------------------------------------------------------------ beam, reactions, springs

func _rd_amp() -> float:
	return 0.35 if RenderAnim.reduced_motion else 1.0


## One sim tick of the ragdoll, from the fighter's own state (never random, never written back). Velocity and acceleration are
## taken in the body frame: the body is rotated by the sim's rot, mirrored by the visual facing. A launch, a slam, a skid, a
## brace before the ground and a tuck in a spin are all this one controller.
## How much each ragdoll degree of freedom may move: a broken arm hangs heavy, so its three (upper arm, its sideways swing and forearm) move at a third
## while the body tumbles or settles (a limp arm does not whip like a good one); every other one is free.
func _rd_scale() -> PackedFloat32Array:
	if not _arm_broken:
		return PackedFloat32Array()
	var sc := PackedFloat32Array()
	sc.resize(AnimRagdoll.N)
	for i in range(AnimRagdoll.N):
		sc[i] = 1.0
	var base: int = 5 if _hang_right else 2
	for j in range(3):
		sc[base + j] = 0.33
	return sc


func _rd_tick(S: SimState, f, dt: float) -> void:
	if f.state == "intro" and RenderAnim.intro_poses:
		return   # the fall is scripted: nothing in the ragdoll should answer its speed
	if not RenderAnim.layer("ragdoll"):
		if _rd.out_w > 0.0 or _rd.energy() > 0.0:
			_rd.reset()
		_rd_have = false
		return
	var t0: int = Time.get_ticks_usec() if RenderAnim.debug_checks else 0
	var amp: float = _rd_amp()
	var vw := Vector2(f.vx, f.vy)
	var aw := Vector2.ZERO
	if _rd_have:
		aw = (vw - _rd_vw) / maxf(dt, 0.0001) + Vector2(0.0, 1000.0)
		if aw.length() > AnimRagdoll.a_max:
			aw = aw.normalized() * AnimRagdoll.a_max
	_rd_vw = vw
	var m: float = vface
	var c: float = cos(f.rot)
	var sn: float = sin(f.rot)
	var vo := Vector2(vw.x * m, vw.y)
	var ao := Vector2(aw.x * m, aw.y)
	var vl := Vector2(vo.x * c - vo.y * sn, vo.x * sn + vo.y * c)
	var al := Vector2(ao.x * c - ao.y * sn, ao.x * sn + ao.y * c)
	var st: String = f.state
	var sliding: bool = f.slide > 0.0
	var flying: bool = st == "launched" and not sliding
	var speed: float = vw.length()
	if _rd_have:
		# a slam (flight to down) folds the body; a skid starting from a flight whips it
		if st == "down" and _rd_prev_state == "launched":
			_rd.crumple(clampf(_rd_prev_speed / 3000.0, 0.3, 1.5), amp)
		elif st == "down" and _rd_prev_state != "down" and is_same(S.game.ko, f):
			_rd.crumple(0.9, amp)
			_rd.kick(0, 6.0 * amp)
			debug["ko_falls"] += 1
		elif sliding and not _rd_prev_slide and _rd_prev_state == "launched":
			_rd.crumple(clampf(_rd_prev_speed / 4000.0, 0.2, 0.9), amp)
	if not _shape_set:
		_shape_set = true
		_rd.set_shape(String(f.id))
		_rd.phase = float(slot) * 0.9
	var free_t: float = 0.0
	var tuck: float = 0.0
	var brace: float = 0.0
	var skid: float = 0.0
	var crum: float = 0.0
	var flail: float = 0.0
	if flying:
		# the early launch is a flail (limbs thrown about); the body tucks only at a high spin and only after the first moments
		free_t = 0.85
		tuck = AnimRagdoll.tuck_max * smoothstep(AnimRagdoll.tuck_spin.x, AnimRagdoll.tuck_spin.y, absf(f.spin)) * smoothstep(AnimRagdoll.tuck_after.x, AnimRagdoll.tuck_after.y, f.stateT) * (0.65 + 0.35 * cos(f.rot * 0.5))
		flail = (1.0 - smoothstep(AnimRagdoll.flail_fade.x, AnimRagdoll.flail_fade.y, f.stateT)) * (1.0 - tuck)
		if f.vy < -200.0 and f.aimB < 0:
			var h: float = f.y - WorldTerrain.groundY(S, f.x)
			if h > 0.0:
				var tc: float = (f.vy + sqrt(f.vy * f.vy + 2000.0 * h)) / 1000.0
				brace = 1.0 - smoothstep(AnimRagdoll.brace_time.x, AnimRagdoll.brace_time.y, tc)
				brace *= 1.0 - tuck * 0.5
				flail *= 1.0 - brace
	elif sliding:
		free_t = 0.55
		skid = AnimRagdoll.skid_w
	elif st == "down":
		free_t = 0.25 if f.stateT < 0.6 else 0.0
		crum = AnimRagdoll.crumple_down_w if f.stateT < 0.7 else 0.0
	# E: the defender's side. The blow the attacker is about to land is in the beat list ahead of time: a heavy one is braced for
	# (arms up, chin down, in the fighter's own brace shape) in the last 0.25 s, and a blow that misses (no damage) is flinched at
	var exc = S.dirS.ex
	var pre: float = 0.0
	if exc != null and exc.D == f and not flying and not sliding and st != "down" and RenderAnim.layer("defender"):
		var nb: Array = _next_blow(exc)
		if not nb.is_empty():
			var dtc: float = nb[0]
			if dtc > 0.0 and nb[2] > 0.0:
				pre = (1.0 - clampf(dtc / 0.25, 0.0, 1.0)) * (0.7 if nb[1] else 0.25) * (1.0 if int(f.stance) == 1 else 0.7)
				if pre > 0.05:
					debug["pre_brace"] += 1
			elif nb[2] <= 0.0 and dtc <= 0.0 and _miss_id != int(exc.n) * 64 + int(nb[3]) and not _has_evade(exc):
				_miss_id = int(exc.n) * 64 + int(nb[3])
				_near_miss(S, f, amp)
		# J: the dodge itself. He sees the blow coming and leans away from it in the 0.14 s before his dodge or slip beat
		var ev: Array = _next_evade(exc)
		if not ev.is_empty() and ev[0] > 0.0 and ev[0] < 0.14 and _lean_id != int(exc.n) * 64 + int(ev[1]):
			_lean_id = int(exc.n) * 64 + int(ev[1])
			_lean_away(S.T, amp)
	if exc != null and exc.A == f and not flying and not sliding and st != "down" and RenderAnim.layer("defender"):
		# J: the attacker's whiff: a blow that finds nothing (no damage, the defender gone) carries him past, off balance
		var ms: Array = _next_miss(exc)
		if not ms.is_empty() and ms[0] < 0.02 and _over_id != int(exc.n) * 64 + int(ms[1]):
			_over_id = int(exc.n) * 64 + int(ms[1])
			_over_commit(S.T, amp)
		else:
			# a dodge is the whiff of a rush: the defender is gone as the attacker arrives, and he is carried past
			var ea: Array = _next_evade(exc)
			if not ea.is_empty() and ea[0] < 0.02 and _over_id != int(exc.n) * 64 + 32 + int(ea[1]):
				_over_id = int(exc.n) * 64 + 32 + int(ea[1])
				_over_commit(S.T, amp)
	if not flying and not sliding:
		brace = maxf(brace, (1.0 - _hit_crum) * _hit_w * 0.45 + pre)
		crum = maxf(crum, _hit_crum * _hit_w * 0.5)
		free_t = maxf(free_t, 0.5 * _hit_w + 0.3 * pre)
	_hit_w = maxf(0.0, _hit_w - dt / (0.3 + 0.4 * _worn))
	_rd.w_tuck = tuck
	_rd.w_brace = brace
	_rd.w_skid = skid
	_rd.w_crumple = crum
	_rd.w_flail = flail
	# polish: the lean into acceleration while he is free, and the contact catch (a striker carried far in one tick is drawn
	# travelling to his place, not popping to it)
	var lean_t: float = 0.0
	if st == "free" or st == "locked":
		lean_t = clampf(ao.x / 9000.0, -1.0, 1.0) * 0.35 * amp
	_lean += (lean_t - _lean) * (1.0 - exp(-dt / 0.12))
	# G: banking into a turn, a burst on a dash start, a brake on a stop
	var bank_t: float = 0.0
	if (st == "free" or st == "locked") and (speed > 300.0 or _air_w > 0.3):
		bank_t = clampf(-ao.x / 9000.0, -1.0, 1.0) * 0.3 * amp
	_bank += (bank_t - _bank) * (1.0 - exp(-dt / 0.15))
	if st == "free":
		var ge: String = _gait_event(ao.x, _gait_speed, absf(vo.x))
		if ge == "burst" and S.T - _burst_t0 > 0.4:
			_burst_t0 = S.T
			debug["bursts"] += 1
		elif ge == "brake" and S.T - _brake_t0 > 0.5:
			_brake_t0 = S.T
			debug["brakes"] += 1
	_gait_speed = absf(vo.x)
	var cx: float = SimWrap.sdx(_prev_x, f.x) if _have_x else 0.0
	var pcx: float = _prev_cx
	_prev_x = f.x
	_prev_cx = cx
	_have_x = true
	var ex = S.dirS.ex
	if absf(cx) > 250.0 and ex != null and ex.A == f and not flying and not sliding and amp > 0.5 and _blow_due(ex):
		# a catch: the striker is carried far on the tick his blow lands; the body lags the anchor a little and the contact reaches that far
		_smear = clampf(-cx * m * 0.05, -10.0, 10.0)
		_smear_t0 = S.T
		debug["catches"] += 1
	_hit_free = maxf(0.0, _hit_free - dt * (1.6 - 0.8 * _worn))
	free_t = maxf(free_t, _hit_free)
	_rd_prev_state = st
	_rd_prev_slide = sliding
	_rd_prev_speed = speed if st == "launched" else _rd_prev_speed
	_rd_have = true
	var active: bool = st == "launched" or st == "down" or _rd.free > 0.02 or _rd.energy() > 0.03
	if active:
		_rd.step(dt, vl, al, free_t, amp, S.T)
	_rd.out_w = move_toward(_rd.out_w, 1.0 if active else 0.0, dt * 8.0)
	if not active and _rd.out_w <= 0.0 and _rd.energy() > 0.0:
		_rd.reset()
	if RenderAnim.debug_checks:
		debug["rd_ticks"] += 1
		debug["rd_usec"] += Time.get_ticks_usec() - t0


## Feet on the ground under them. A standing fighter on a slope (a crater wall, a rim, a heap) has one foot higher than the other:
## the pelvis follows the mean and each leg is solved to its own foot's ground (two-bone IK, the knee staying where the pose has
## it). A skid is pitched to the slope along its way. Reads the terrain only (WorldTerrain.groundY); cheap when the ground is flat
## (three reads, no solve).
func _ground_feet(S: SimState, f, dt: float) -> void:
	var st: String = f.state
	var sliding: bool = f.slide > 0.0
	# one ground read a solve; the slope reads only while he stands or slides, and cached while he stays within 3 units
	var g0: float
	if S.tick - _gf_gtick < 4 and absf(SimWrap.sdx(_gf_gx, f.x)) < 20.0:
		g0 = _gf_g0c
	else:
		g0 = WorldTerrain.groundY(S, f.x)
		_gf_g0c = g0
		_gf_gx = f.x
		_gf_gtick = S.tick
	var stand: bool = (st == "free" or st == "locked" or st == "charging") and not sliding and absf(f.y - g0) < 8.0
	var target_w: float = 0.0
	var pitch_t: float = 0.0
	if stand or sliding:
		if not (_gf_x_valid and absf(SimWrap.sdx(_gf_x, f.x)) < 3.0 and vface == _gf_face and S.tick - _gf_tick < 20):
			_gf_x_valid = true
			_gf_x = f.x
			_gf_face = vface
			_gf_tick = S.tick
			_gf_dF = WorldTerrain.groundY(S, f.x + vface * 10.0) - g0
			_gf_dB = WorldTerrain.groundY(S, f.x - vface * 10.0) - g0
			_gf_sl = (WorldTerrain.groundY(S, f.x + vface * 12.0) - WorldTerrain.groundY(S, f.x - vface * 12.0)) / 24.0
		if sliding:
			pitch_t = atan(_gf_sl) * 0.9
		elif absf(_gf_dF) > 2.5 or absf(_gf_dB) > 2.5:
			target_w = 1.0   # a gentler slope than this is under the pose's own tolerance and costs a leg solve for nothing
	if absf(target_w - _foot_w) < 0.001 and absf(pitch_t - _skid_pitch) < 0.001 and _foot_w < 0.02 and absf(_skid_pitch) < 0.01:
		_foot_shift = 0.0
		_foot_dh = Vector2.ZERO
		return
	var k: float = 1.0 - exp(-dt / 0.1)
	_foot_w += (target_w - _foot_w) * k
	_skid_pitch += (pitch_t - _skid_pitch) * (1.0 - exp(-dt / 0.08))
	if absf(_skid_pitch) > 0.01:
		var pe: int = AnimRig.index["pelvis"]
		q[pe] = q[pe] * Quaternion(Vector3(0, 0, 1), _skid_pitch)
	if _foot_w < 0.02:
		_foot_shift = 0.0
		_foot_dh = Vector2.ZERO
		return
	var ix: Dictionary = AnimRig.index
	# the leg solve runs every second tick; in between the last solve's local corrections are applied again (the pose moves little)
	if _leg_tick >= 0 and S.tick - _leg_tick == 1 and (S.tick & 1) == 1:
		hips.y += _foot_shift
		for kk in range(4):
			var bi: int = _LEG_BONES[kk]
			q[bi] = q[bi] * _leg_dq[kk]
		_leg_tick = S.tick
		return
	var q0: Array[Quaternion] = [q[16], q[17], q[19], q[20]]
	AnimPose.fk_chain(q, hips, gq, gp, LEG_CHAIN)   # only the legs and what they hang from
	var fl: int = ix["foot_l"]
	var fr: int = ix["foot_r"]
	var dhl: float = WorldTerrain.groundY(S, f.x + vface * gp[fl].x) - g0
	var dhr: float = WorldTerrain.groundY(S, f.x + vface * gp[fr].x) - g0
	_foot_dh = Vector2(dhl, dhr)
	var shift: float = clampf((dhl + dhr) * 0.5, -10.0, 10.0) * _foot_w
	_foot_shift = shift
	hips.y += shift
	for i in LEG_CHAIN:
		if i > 0:
			gp[i].y += shift
	debug["slope_frames"] += 1
	for leg in [["thigh_l", "shin_l", "foot_l", dhl], ["thigh_r", "shin_r", "foot_r", dhr]]:
		var a: int = ix[leg[0]]
		var b: int = ix[leg[1]]
		var c: int = ix[leg[2]]
		var want: float = float(leg[3]) * _foot_w - shift
		if absf(want) < 0.4:
			continue   # this foot is already where the ground is
		var tgt := Vector3(gp[c].x, gp[c].y + want, gp[c].z)
		var pole: Vector3 = gp[b]
		AnimPose.ik_limb(q, gq, gp, a, b, c, tgt, pole, -1.0, _rd.shape_key)
	if _leg_dq.size() != 4:
		_leg_dq.resize(4)
	for kk in range(4):
		_leg_dq[kk] = q0[kk].inverse() * q[_LEG_BONES[kk]]
	_leg_tick = S.tick


## The next blow the attacker throws in the running exchange: [time to contact, heavy, damage, beat index], or [] (a parried
## exchange has none). A blow just fired (up to 0.03 s ago) still counts, so a miss can be flinched at.
static func _next_blow(ex) -> Array:
	if ex.cancel:
		return []
	var best: Array = []
	var i: int = 0
	for b in ex.beats:
		if b.op == "strike" and String(b.args.a) == "A":
			var dtc: float = b.t - ex.t
			if dtc >= -0.03 and (best.is_empty() or dtc < best[0]):
				best = [dtc, _is_heavy(ex, b.args), float(b.args.get("dmg", 0.0)), i]
		i += 1
	return best


## The next dodge or slip beat of the exchange: [time to it, beat index], or [].
static func _next_evade(ex) -> Array:
	var best: Array = []
	var i: int = 0
	for b in ex.beats:
		if b.op == "dodge" or b.op == "slip":
			var dt_e: float = b.t - ex.t
			if dt_e >= 0.0 and (best.is_empty() or dt_e < best[0]):
				best = [dt_e, i]
		i += 1
	return best


## The attacker's next blow that does no damage (a whiff): [time to contact, beat index] from 0.03 s after it to 0.1 s before, or [].
static func _next_miss(ex) -> Array:
	var best: Array = []
	var i: int = 0
	for b in ex.beats:
		if b.op == "strike" and String(b.args.a) == "A" and float(b.args.get("dmg", 0.0)) <= 0.0:
			var dtc: float = b.t - ex.t
			if dtc >= -0.03 and dtc < 0.1 and (best.is_empty() or dtc < best[0]):
				best = [dtc, i]
		i += 1
	return best


## The defender's lean away from a blow he is about to dodge: head and chest back, arms flung back, the weight on the heels.
func _lean_away(T: float, amp: float) -> void:
	_lean_t0 = T
	_rd.kick(0, 8.0 * amp)
	_rd.kick(1, 7.0 * amp)
	_rd.kick(2, -3.0 * amp)
	_rd.kick(5, -3.0 * amp)
	_hit_free = minf(1.0, _hit_free + 0.25)
	_rd.out_w = 1.0
	debug["leans"] += 1


## The whiff's over-commit: the chest and head go forward after the blow, the arms with it, the body carried a step past.
func _over_commit(T: float, amp: float) -> void:
	_over_t0 = T
	_rd.kick(1, -8.0 * amp)
	_rd.kick(0, -5.0 * amp)
	_rd.kick(2, 6.0 * amp)
	_rd.kick(5, 6.0 * amp)
	_rd.kick(4, 3.0 * amp)
	_rd.kick(7, 3.0 * amp)
	_hit_free = minf(1.0, _hit_free + 0.3)
	_rd.out_w = 1.0
	debug["overcommits"] += 1


## His blow was blocked (the sim's damage event of kind guard): the arms bounce back, the chest rocks, a small step back.
func on_blocked(T: float) -> void:
	if not RenderAnim.layer("defender"):
		return
	_block_t0 = T
	var amp: float = _rd_amp()
	_rd.kick(2, -5.0 * amp)
	_rd.kick(5, -5.0 * amp)
	_rd.kick(1, 3.0 * amp)
	_rd.kick(0, 3.0 * amp)
	_rd.out_w = 1.0
	debug["blocked"] += 1


## The root offsets of the lean-away (back), the over-commit (forward) and the blocked recoil (back), decaying.
func _evade_offsets(T: float) -> float:
	var x: float = 0.0
	var a: float = _rd_amp()
	var tl: float = T - _lean_t0
	if tl >= 0.0 and tl < 0.6:
		x -= 3.0 * a * sin(minf(tl / 0.12, 1.0) * PI * 0.5) * exp(-tl / 0.3)
	var to: float = T - _over_t0
	if to >= 0.0 and to < 0.8:
		x += 7.0 * a * sin(minf(to / 0.1, 1.0) * PI * 0.5) * exp(-to / 0.25)
	var tb: float = T - _block_t0
	if tb >= 0.0 and tb < 0.5:
		x -= 4.0 * a * exp(-tb / 0.12)
	return x


## The attacker has a blow due now (from 0.03 s ago to 0.05 s ahead).
static func _blow_due(ex) -> bool:
	for b in ex.beats:
		if b.op == "chainStrike" or (b.op == "strike" and String(b.args.a) == "A"):
			var dtc: float = b.t - ex.t
			if dtc >= -0.03 and dtc <= 0.05:
				return true
	return false


static func _has_evade(ex) -> bool:
	for b in ex.beats:
		if b.op == "dodge" or b.op == "slip":
			return true
	return false


## A blow that does not land passes close: the head is pulled back and the body flinches, small, from the attacker's side.
func _near_miss(S: SimState, f, amp: float) -> void:
	var a = S.dirS.ex.A
	var d := Vector2(SimWrap.sdx(a.x, f.x) * vface, f.y - a.y)
	d = d.normalized() if d.length() > 1.0 else Vector2(-1.0, 0.0)
	_rd.hit(d.x, d.y, 0.3 * amp, 0, 0.0, 0.0)
	_hit_free = minf(1.0, _hit_free + 0.2)
	_rd.out_w = 1.0
	debug["near_miss"] += 1


func _flight_layers(T: float, f) -> void:
	if f.state != "free" or _part != "":
		return
	var tb: float = T - _burst_t0
	if tb >= 0.0 and tb < 0.22:
		_mix_pose(AnimData.pose("move.burst"), smoothstep(0.0, 0.03, tb) * (1.0 - smoothstep(0.08, 0.22, tb)) * 0.9)
	var tk: float = T - _brake_t0
	if tk >= 0.0 and tk < 0.3:
		_mix_pose(AnimData.pose("move.brake"), smoothstep(0.0, 0.04, tk) * (1.0 - smoothstep(0.1, 0.3, tk)) * 0.85)
	if absf(_bank) > 0.004:
		var ix: Dictionary = AnimRig.index
		q[ix["pelvis"]] = q[ix["pelvis"]] * Quaternion(Vector3(1, 0, 0), _bank * 0.6)
		q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(1, 0, 0), _bank * 0.4)
	_shock_layer(T)


func _shock_layer(T: float) -> void:
	var ts: float = T - _shock_t0
	if ts >= 0.0 and ts < 1.2 and _shock_amp > 0.0:
		var e: float = exp(-ts / 0.5) * _shock_amp * _rd_amp()
		var w: float = sin(ts * TAU * 3.2)
		hips.y += 1.6 * e * w
		var s2: int = AnimRig.index["spine_2"]
		q[s2] = q[s2] * Quaternion(Vector3(0, 0, 1), 0.06 * e * cos(ts * TAU * 2.6))


func _base_lagged() -> bool:
	if _base_r.size() != AnimRig.N:
		_base_r.resize(AnimRig.N)
		var lagk: float = float(AnimData.personality.get("guard", {}).get("lag", 2.0))
		for i in range(AnimRig.N):
			_base_r[i] = 1.0 / (1.0 + lagk * AnimData.bone_lag[i])
	return true


## The guard comes up or goes down with overlap: the arms are kicked toward the new guard and settle back on their springs.
func _stance_kick(old: int, new: int) -> void:
	var g: Dictionary = AnimData.personality.get("guard", {})
	var amp: float = _rd_amp()
	var dir: Array = []
	if new == 1:
		dir = g.get("up", [6.0, 4.0])
	elif old == 1:
		dir = g.get("down", [-5.0, -3.0])
	if dir.is_empty():
		return
	_rd.kick(2, float(dir[0]) * amp)
	_rd.kick(5, float(dir[0]) * amp)
	_rd.kick(4, float(dir[1]) * amp)
	_rd.kick(7, float(dir[1]) * amp)
	_rd.out_w = 1.0
	debug["guard_kicks"] += 1


## The weight shift, the knee bounce, the heel lift and the breath of a stance, scaled by the form's tier. calm is 1 for a fighter
## standing still and falls to 0 as he moves.
func _personality_layer(T: float, stance: int, tier: int, calm: float) -> void:
	var P: Dictionary = AnimData.personality
	var st: Dictionary = P.stances[STANCES[clampi(stance, 0, 3)]]
	var tr: Dictionary = P.tiers[clampi(tier, 0, 3)]
	var amp: float = _rd_amp() * calm
	var ix: Dictionary = AnimRig.index
	var ph: float = float(slot) * 1.9
	var shp: Dictionary = AnimData.shapes.get(_rd.shape_key, {}).get("idle", {})
	var sway: float = float(st.sway) * float(tr.sway) * amp * float(shp.get("sway", 1.0))
	var hzm: float = float(shp.get("hz", 1.0))
	var s1: float = sin(T * TAU * float(st.hz) * float(tr.hz) * hzm + ph)
	var pe: int = ix["pelvis"]
	q[pe] = q[pe] * Quaternion(Vector3(1, 0, 0), s1 * 0.02 * sway)
	hips.z += s1 * 0.6 * sway
	hips.x += sin(T * TAU * float(st.hz) * float(tr.hz) * hzm * 0.7 + ph + 1.0) * 0.3 * sway
	var bn: float = float(st.bounce) * float(tr.bounce) * amp * float(shp.get("bounce", 1.0))
	var b1: float = sin(T * TAU * float(st.bounce_hz) * float(tr.hz) * hzm + ph)
	hips.y += absf(b1) * 0.45 * bn   # a hop on the toes (never a dip: the legs are posed, so a dip would sink the feet)
	var heel: float = float(st.heel) * amp * float(shp.get("heel", 1.0))
	q[ix["thigh_l"]] = q[ix["thigh_l"]] * Quaternion(Vector3(0, 0, 1), heel * maxf(0.0, b1))
	q[ix["shin_l"]] = q[ix["shin_l"]] * Quaternion(Vector3(0, 0, 1), -heel * 1.2 * maxf(0.0, b1))
	q[ix["thigh_r"]] = q[ix["thigh_r"]] * Quaternion(Vector3(0, 0, 1), heel * maxf(0.0, -b1))
	q[ix["shin_r"]] = q[ix["shin_r"]] * Quaternion(Vector3(0, 0, 1), -heel * 1.2 * maxf(0.0, -b1))
	var br: float = sin(T * TAU * 0.45 * float(tr.breath_hz) + ph) * 0.012 * float(tr.breath_amp) * float(st.breath) * amp * float(shp.get("breath", 1.0))
	q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 0, 1), br)
	q[ix["head"]] = q[ix["head"]] * Quaternion(Vector3(0, 0, 1), -br * 0.5)
	# the shape's own idle: arms that drift out and in (a sweeping build), a head that glances and holds (a machine)
	var sweep: float = float(shp.get("sweep", 0.0)) * amp
	if sweep > 0.0:
		var sv: float = sin(T * TAU * 0.31 + ph) * sweep
		q[ix["upper_arm_r"]] = q[ix["upper_arm_r"]] * Quaternion(Vector3(1, 0, 0), sv)
		q[ix["upper_arm_l"]] = q[ix["upper_arm_l"]] * Quaternion(Vector3(1, 0, 0), -sv)
	var servo: float = float(shp.get("servo", 0.0)) * amp
	if servo > 0.0:
		var cyc: float = T / 2.6 + float(slot) * 0.37
		var tt: float = cyc - floorf(cyc)
		var gl: float = smoothstep(0.0, 0.04, tt) * (1.0 - smoothstep(0.5, 0.54, tt))
		var sgn: float = 1.0 if (_hash(int(floorf(cyc)), slot, 17) & 1) == 0 else -1.0
		q[ix["head"]] = q[ix["head"]] * Quaternion(Vector3(0, 1, 0), servo * 0.1 * gl * sgn)
	debug["personality"] += 1


## A turn-around is a step: the weight dips, one foot swings through, the hips twist toward the new side and the shoulders against
## it, over 0.22 s from the moment the visual facing flipped (the view turns through front-on over the same time).
func _turn_layer(T: float) -> void:
	var tp: Dictionary = AnimData.personality.get("turn", {})
	var dur: float = float(tp.get("dur", 0.22))
	var tau: float = T - _turn_t0
	if tau < 0.0 or tau >= dur:
		return
	var u: float = tau / dur
	var s: float = sin(PI * u) * _rd_amp()
	var ix: Dictionary = AnimRig.index
	hips.y -= float(tp.get("dip", 1.4)) * s
	var sw: float = float(tp.get("swing", 0.5)) * s
	q[ix["thigh_r"]] = q[ix["thigh_r"]] * Quaternion(Vector3(0, 0, 1), sw)
	q[ix["shin_r"]] = q[ix["shin_r"]] * Quaternion(Vector3(0, 0, 1), -sw * 1.2)
	q[ix["thigh_l"]] = q[ix["thigh_l"]] * Quaternion(Vector3(0, 0, 1), -sw * 0.4)
	var tw: float = float(tp.get("twist", 0.35)) * s * (1.0 - u) * _turn_dir
	var pe: int = ix["pelvis"]
	q[pe] = q[pe] * Quaternion(Vector3(0, 1, 0), tw)
	q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 1, 0), -tw * 0.6)
	debug["turn_steps"] += 1


## What a change of speed along the facing is: a dash start (a hard push from nearly still) or a brake (a hard slowdown from a run).
## ax the acceleration along the facing (u/s^2), prev and now the speeds before and after (u/s).
static func _gait_event(ax: float, prev: float, now: float) -> String:
	if ax > 15000.0 and prev < 250.0 and now > prev:
		return "burst"
	if ax < -9000.0 and prev > 500.0 and now < prev:
		return "brake"
	return ""


## The winner's beat after the KO: 1 standing spent, 2 looking over the wreckage, 3 the end (held or a raised fist).
static func _win_phase(ko_t: float, _id: String) -> int:
	var w: Dictionary = AnimData.winner
	var t0: float = float(w.get("start", 0.8))
	if ko_t < t0 + float(w.get("stand", 1.0)):
		return 1
	if ko_t < t0 + float(w.get("stand", 1.0)) + float(w.get("survey", 2.5)):
		return 2
	return 3


## How the winner ends: "hold" the survey or "victory" (emote.victory), by his shape key (a data choice, not code).
func _win_end(roster_id: String) -> String:
	var ends: Dictionary = AnimData.winner.get("end", {})
	var key: String = String(AnimRagdoll.shape_of.get(roster_id, AnimRagdoll.shape_of.get("default", "")))
	return String(ends.get(key, ends.get("default", "hold")))


## The survey: the head and neck sweep slowly across the scene, the chest heaves; only the winner, only after the KO.
func _win_layer(S: SimState, f, T: float) -> void:
	if S.game.ko == null or is_same(S.game.ko, f) or f.state != "free":
		return
	var w: Dictionary = AnimData.winner
	var t0: float = float(w.get("start", 0.8)) + float(w.get("stand", 1.0))
	var tau: float = S.game.koT - t0
	var amp: float = _rd_amp()
	var ix: Dictionary = AnimRig.index
	var heave: float = sin(T * TAU * 0.9) * 0.05 * amp
	q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 0, 1), heave)
	if tau >= 0.0 and tau < float(w.get("survey", 2.5)):
		var u: float = tau / float(w.get("survey", 2.5))
		var yaw: float = sin(u * TAU) * float(w.get("yaw", 0.8)) * smoothstep(0.0, 0.15, u) * amp
		q[ix["head"]] = q[ix["head"]] * Quaternion(Vector3(0, 1, 0), yaw * 0.6)
		q[ix["neck"]] = q[ix["neck"]] * Quaternion(Vector3(0, 1, 0), yaw * 0.4)
		q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 1, 0), yaw * 0.1)
		debug["survey"] += 1


func _wound_read(f) -> void:
	if f.wd == null:
		return
	var at: float = float(f.wd.stageAt[2])
	var tot: float = 0.0
	for r in range(4):
		tot += float(f.wear[r])
	_worn = clampf(tot / (4.0 * at) * 1.6, 0.0, 1.0)
	_brinkp = SimWounds.brinkProgress(f)
	_arm_broken = int(f.stage[2]) == 3
	_leg_broken = int(f.stage[3]) == 3


## Heavy breathing from the average wear (faster and deeper as it rises, the shoulders heaving, the head heavy) and a
## stagger from the brink (a slow irregular sway of the pelvis and a little give in the hips). Sim time only, so it replays.
func _wear_motion(f, T: float) -> void:
	if f.state == "launched" or f.state == "down":
		return
	var ix: Dictionary = AnimRig.index
	var br: float = sin(T * TAU * (0.8 + 1.4 * _worn) + slot * 1.9)
	var amp: float = 0.02 + 0.08 * _worn
	var s2: int = ix["spine_2"]
	var hd: int = ix["head"]
	q[s2] = q[s2] * Quaternion(Vector3(0, 0, 1), br * amp)
	q[hd] = q[hd] * Quaternion(Vector3(0, 0, 1), -br * amp * 0.5 + 0.05 * _worn)
	var heave: float = br * 0.06 * _worn
	var cr: int = ix["clavicle_r"]
	var cl: int = ix["clavicle_l"]
	q[cr] = q[cr] * Quaternion(Vector3(1, 0, 0), -heave)
	q[cl] = q[cl] * Quaternion(Vector3(1, 0, 0), heave)
	var ps: float = smoothstep(0.45, 1.0, _brinkp)
	if ps > 0.0:
		var sw: float = 0.6 * sin(T * 1.7 + slot * 2.3) + 0.4 * sin(T * 2.9 + slot)
		var sw2: float = sin(T * 2.3 + slot * 1.1 + 1.0)
		var pe: int = ix["pelvis"]
		q[pe] = q[pe] * Quaternion(Vector3(1, 0, 0), ps * 0.07 * sw) * Quaternion(Vector3(0, 0, 1), ps * 0.04 * sw2)
		hips.x += ps * 2.0 * sw


## A broken arm hangs (the arm's bones go to wound.arm_limp, the hand slack) and a broken leg is favoured (the leg's bones and
## part of the pelvis go to wound.leg_favour, with a dip on each step when moving). Light in the air, strong on the ground.
func _wound_limbs(f, T: float) -> void:
	var ix: Dictionary = AnimRig.index
	var calm: bool = f.state != "launched" and f.state != "down"
	if _arm_broken:
		var lw: float = 0.92 if calm else 0.5
		var sfx: String = "r" if _hang_right else "l"
		var lp: AnimPose = AnimData.pose("wound.arm_limp", not _hang_right)
		for nm in ["upper_arm_", "forearm_", "hand_", "fingers_"]:
			var i: int = ix[nm + sfx]
			q[i] = q[i].slerp(lp.q[i], lw)
		if _hang_right:
			curl.y = lerpf(curl.y, lp.curl.y, lw)
		else:
			curl.x = lerpf(curl.x, lp.curl.x, lw)
	if _leg_broken:
		var lf: float = (0.85 if _part == "" else 0.5) if calm else 0.3
		var sf2: String = "r" if _leg_right else "l"
		var fp: AnimPose = AnimData.pose("wound.leg_favour", not _leg_right)
		for nm2 in ["thigh_", "shin_", "foot_"]:
			var i2: int = ix[nm2 + sf2]
			q[i2] = q[i2].slerp(fp.q[i2], lf)
		var pe: int = ix["pelvis"]
		q[pe] = q[pe].slerp(fp.q[pe], lf * 0.5)
		hips.z += fp.hips.z * lf * 0.7
		if calm and absf(f.vx) > 60.0 and _part == "":
			hips.y -= 2.0 * lf * maxf(0.0, sin(T * TAU * 1.6 + slot))


## The elapsed ticks of the running transformation, or -1 when it is over. full and short play inside the sim's pause, whose
## own ticks are the clock (S.pause.left counts them down); live has none and runs on sim time.
func _transform_layer(S: SimState, T: float) -> void:
	var v: String = String(_form.version)
	var spec: Dictionary = AnimData.forms[v]
	var total: int = int(spec.gather) + int(spec["break"]) + int(spec.settle)
	var e: float
	if v == "live":
		e = (T - float(_form.t0)) * 60.0
	elif S.pause.left > 0 and S.pause.actor == slot:
		e = float(total - S.pause.left)
	else:
		e = -1.0 if T > float(_form.t0) + 0.05 else 0.0
	if e < 0.0 or e >= float(total):
		_form = {}
		return
	_form_pose(spec, e, v == "live")


## The pose layer at `e` ticks into the transformation (the part tools test without a match).
func _form_pose(spec: Dictionary, e: float, live: bool) -> void:
	var g: float = float(spec.gather)
	var b: float = float(spec["break"])
	var s_: float = float(spec.settle)
	var hold: float = float(spec.hold)
	var fp: Dictionary = AnimData.form_poses
	if e < g:
		# the gather: compress (a live one is only a flinch inward)
		var u: float = e / g
		_mix_pose(AnimData.pose(String(fp.gather)), smoothstep(0.0, 1.0, u) * (0.6 if live else 1.0))
	elif e < g + b:
		# the break: one snap, the new pose locks on in this frame and the silhouette changes here and nowhere else
		var pb: AnimPose = AnimData.pose(String(fp["break"]))
		_set_pose(pb)
		var ub: float = (e - g) / b
		# a few ticks of overshoot past the lock, then the break pose holds
		var ov: float = sin(PI * clampf(ub * 4.0, 0.0, 1.0)) * 0.1
		AnimPose.mix(q, AnimData.pose(String(fp.settle)).q, clampf(smoothstep(0.4, 1.0, ub) - ov, 0.0, 1.0))
	else:
		# the settle: the new pose holds for `hold` ticks, then eases back into the fight (live: the upper body holds while he
		# drifts back, so it fades from the first tick)
		var ps: AnimPose = AnimData.pose(String(fp.settle))
		_set_pose(ps)
		var es: float = e - g - b
		var w: float = 1.0 - smoothstep(hold, s_, es)
		# back toward what he is doing
		for i in range(AnimRig.N):
			q[i] = _base[i].slerp(ps.q[i], w)
		hips = _base_hips.lerp(ps.hips, w)
		curl = _base_curl.lerp(ps.curl, w)


func _beam_layer(S: SimState, f, T: float) -> void:
	if f.beamCharge != null:
		var p: float = clampf((T - float(f.beamCharge)) / 0.8, 0.0, 1.0)
		_mix_pose(AnimData.pose("beam.charge"), smoothstep(0.0, 0.35, p))
		_aim_arms(_aim_angle(S, f, null), smoothstep(0.2, 0.6, p))
		return
	for b in S.beams:
		if b.A == f and b.t < b.life:
			var bwt: float = 1.0 - smoothstep(0.55, 0.95, b.t)
			_mix_pose(AnimData.pose("beam.fire"), bwt)
			_aim_arms(_aim_angle(S, f, b), bwt)
			return


## The angle the signature is aimed at, in the body frame (0 forward, positive up): the beam's own direction once it is fired, the
## opponent's direction while it charges.
func _aim_angle(S: SimState, f, b) -> float:
	if b != null:
		return clampf(atan2(float(b.uy), float(b.ux) * vface), -1.2, 1.2)
	var opp = _opponent(S, f)
	if opp == null:
		return 0.0
	var dx: float = absf(SimWrap.sdx(f.x, opp.x))
	return clampf(atan2((opp.y + 55.0) - (f.y + 55.0), maxf(dx, 40.0)), -1.2, 1.2)


## Both arms (and a little of the chest) turn to the aim: a beam is thrown along its line, not along the pose's fixed forward.
func _aim_arms(alpha: float, w: float) -> void:
	if not RenderAnim.layer("aim") or w <= 0.001:
		return
	var a: float = alpha * w * _rd_amp()
	var ix: Dictionary = AnimRig.index
	for nm in ["upper_arm_l", "upper_arm_r"]:
		q[ix[nm]] = q[ix[nm]] * Quaternion(Vector3(0, 0, 1), a)
	q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 0, 1), a * 0.25)
	debug["aims"] += 1


## The head looks at the opponent: up or down by where his head is, smoothed, in an exchange or when he is near (overhaul unit D).
func _look_tick(S: SimState, f, dt: float) -> void:
	# once per sim tick (so a replay shows the same head); the target is re-aimed every third tick, the smoothing is linear
	if (S.tick + slot) % 3 == 0:
		var opp = _opponent(S, f)
		var t: float = 0.0
		var st: String = f.state
		if opp != null and st != "launched" and st != "down" and not (f.beamCharge != null):
			var dxo: float = SimWrap.sdx(f.x, opp.x) * vface
			if dxo > 10.0 and dxo < 1200.0:
				t = clampf(atan2(opp.y - f.y, maxf(dxo, 40.0)), -0.7, 0.7) * 0.8
		_look_target = t
	_look += (_look_target - _look) * clampf(dt * 8.0, 0.0, 1.0)


func _look_at(S: SimState, f, dt: float) -> void:
	if absf(_look) > 0.004:
		var a: float = _look * _rd_amp()
		var ix: Dictionary = AnimRig.index
		q[ix["head"]] = q[ix["head"]] * Quaternion(Vector3(0, 0, 1), a * 0.55)
		q[ix["neck"]] = q[ix["neck"]] * Quaternion(Vector3(0, 0, 1), a * 0.3)
		q[ix["spine_2"]] = q[ix["spine_2"]] * Quaternion(Vector3(0, 0, 1), a * 0.1)


func _reaction_layer(T: float) -> void:
	var keep: Array = []
	for r in _reacts:
		var tau: float = T - float(r.t0)
		var amp: float = float(r.amp)
		if tau < 0.0 or tau > 0.7:
			if tau < 0.0:
				keep.append(r)
			continue
		keep.append(r)
		var rf: float = float(r.get("f", amp))
		var rdx: float = float(r.get("dx", -1.0 if bool(r.front) else 1.0))
		var rdy: float = float(r.get("dy", 0.0))
		var wk: float = 1.0 + 0.5 * _worn
		_recoil_x += rdx * rf * 7.0 * wk * smoothstep(0.0, 0.02, tau) * exp(-tau / 0.07)
		_recoil_y += rdy * rf * 4.0 * smoothstep(0.0, 0.02, tau) * exp(-tau / 0.08)
		if rf > 0.8:
			# a heavy blow staggers him: a quick sway after the push
			_recoil_x += rdx * rf * 2.5 * sin(tau * TAU * 3.0) * exp(-tau / 0.25)
		var w: float = amp * smoothstep(0.0, 0.05, tau) * (1.0 - smoothstep(0.12, 0.12 + 0.3 * amp, tau))
		if w <= 0.001:
			continue
		if String(r.get("kind", "")) == "guard":
			# a guarded blow: forearms up and rocked back, no wound wave
			_mix_pose(AnimData.pose("def.guard_hit"), w * 0.85)
			continue
		var pose_id: String = "react.flinch_f" if bool(r.front) else "react.flinch_b"
		if String(r.region) == "core":
			pose_id = "react.fold" if amp > 0.7 else pose_id
		_mix_pose(AnimData.pose(pose_id), w * (0.55 if RenderAnim.ragdoll_enabled else 0.7))
		var add_id: String = "react." + String(r.region)
		if AnimData.poses.has(add_id):
			AnimPose.add(q, AnimData.pose(add_id).q, w)
		# a damped wave up the spine
		var ix: Dictionary = AnimRig.index
		for pair in [["spine_1", 0.0], ["spine_2", 0.05], ["neck", 0.1], ["head", 0.15]]:
			var b2: int = ix[pair[0]]
			var ph: float = sin((tau - float(pair[1])) * 50.0) * exp(-(tau) * 9.0) * amp * 0.12
			q[b2] = q[b2] * Quaternion(Vector3(0, 0, 1), ph * (1.0 if bool(r.front) else -1.0))
	_reacts = keep


## The sash and pack chains, one step per sim tick (never per frame, so a replay shows the same cloth): driven by the velocity along
## the way he faces, the acceleration, and a flutter at speed (overhaul unit D); frozen ticks run at a tenth.
func _spring_tick(S: SimState, f, dt: float, frozen: bool) -> void:
	var m: float = vface
	var vw := Vector2(f.vx * m, f.vy) if not (f.state == "intro" and RenderAnim.intro_poses) else Vector2.ZERO   # no wind in the opening
	var aw := Vector2.ZERO
	if _sp_have:
		aw = (vw - _sp_vw) / maxf(dt, 0.0001)
	_sp_vw = vw
	_sp_have = true
	var h_all: float = dt * (0.1 if frozen else 1.0)
	var sp_n: float = clampf(vw.length() / 1600.0, 0.0, 1.0)
	var flutter: float = 0.12 * sp_n * (0.35 if RenderAnim.reduced_motion else 1.0) * (1.0 if RenderAnim.layer("cloth") else 0.0)
	var ov: float = 1.0 if RenderAnim.layer("cloth") else 0.0
	var target: float = -clampf(vw.x / 1600.0, -1.0, 1.0) * 0.9 - clampf(aw.x / 12000.0, -1.0, 1.0) * 0.5 * ov - clampf(-vw.y / 2400.0, 0.0, 1.0) * 0.6 * ov
	var n: int = 2
	var h: float = h_all / float(n)
	for _s in range(n):
		for kx in range(5):
			var stiff: float = [40.0, 60.0, 90.0, 60.0, 90.0][kx]
			var damp: float = 5.0
			var goal: float = target * [0.5, 1.0, 0.8, 1.0, 0.8][kx] + flutter * sin(S.T * TAU * 4.5 + float(kx) * 1.3 + float(slot))
			var a: float = -stiff * (_sx[kx] - goal) - damp * _sv[kx]
			_sv[kx] += a * h
			_sx[kx] += _sv[kx] * h


func _springs(f) -> void:
	for kx in range(5):
		var bi: int = _spring_bones[kx]
		q[bi] = q[bi] * Quaternion(Vector3(0, 0, 1), clampf(_sx[kx], -0.9, 0.9))
