extends SceneTree
## AI-vs-AI batch runner on the GDScript sim: the successor of prototype/tools/sim-stats.js. From the repo root:
##   godot --headless --path . --script res://sim/core/tools/batch.gd -- [matches=60] [baseSeed=1] [--arm=NAME] [--json] [--jobs=N]
## --jobs=N splits the seed range over N child Godot processes (contiguous blocks) and aggregates their records in seed
## order, so the output and digest are the same as a single-process run. (--records=PATH is the children's mode.)
## Match i uses seed baseSeed+i, so the output is identical on every run (no clock in it); the digest hashes every
## match's final state, so comparing two runs is a one-line diff. Arms (who sits where): default, swap, mirror-villain,
## mirror-hero, and each with -flip. Statistics come from the feed lines, as in QA's match runner.
## Exit code 1 on a NaN or a fighter outside [0, W).

## A match is cut off at MAX_T of match time (S.T: 15:00, the S4 ruling), not at a count of steps: hit-stop steps do not
## advance S.T, and a step cap cut about 1 match in 100 just before its KO and called it a timeout. MAX_STEPS is only a
## safety stop.
const MAX_T: float = 900.0
const MAX_STEPS: int = 120000
const KO_TAIL: float = 3.0
const STANCES: Array = ["AGGRESSIVE", "DEFENSIVE", "EVASIVE", "ESCAPE"]

var re_atk := RegEx.create_from_string("^([A-Z][A-Z0-9-]*) (LIGHT|HEAVY|SIG) vs (\\w+)$")
var re_beam := RegEx.create_from_string("^(.+) over (\\w+) \\((.+)\\) → (\\w+)")
var re_launch := RegEx.create_from_string("^LAUNCH: (.+)$")
var re_parry := RegEx.create_from_string("^([A-Z][A-Z0-9-]*) PARRIES$")
var re_chain := RegEx.create_from_string("^CHAIN x(\\d+) ended$")
var re_hide := RegEx.create_from_string("^([A-Z][A-Z0-9-]*) goes to ground$")
var re_found := RegEx.create_from_string("^[A-Z][A-Z0-9-]* found$")


func _init() -> void:
	var pos: Array = []
	var arm: String = "default"
	var as_json: bool = false
	var jobs: int = 1
	var records_out: String = ""
	for a in OS.get_cmdline_user_args():
		if a.begins_with("--arm="):
			arm = a.substr(6)
		elif a == "--json":
			as_json = true
		elif a.begins_with("--jobs="):
			jobs = maxi(1, int(a.substr(7)))
		elif a.begins_with("--records="):
			records_out = a.substr(10)
		else:
			pos.append(a)
	var n: int = int(pos[0]) if pos.size() > 0 else 60
	var base: int = int(pos[1]) if pos.size() > 1 else 1
	if n < 1 or not (arm.trim_suffix("-flip") in ["default", "swap", "mirror-villain", "mirror-hero"]):
		print("usage: godot --headless --path . --script res://sim/core/tools/batch.gd -- [matches=60] [baseSeed=1] [--arm=default|swap|mirror-villain|mirror-hero[-flip]] [--json]")
		quit(2)
		return
	var t0: int = Time.get_ticks_usec()
	var recs: Array = []
	if jobs > 1 and n > 1:
		recs = run_jobs(n, base, arm, mini(jobs, n))
	else:
		for i in range(n):
			recs.append(run_match(base + i, arm))
	for r in recs:
		if r.bad != "":
			print("FAIL  seed %d: %s" % [int(r.seed), r.bad])
			quit(1)
			return
	if records_out != "":
		var f := FileAccess.open(records_out, FileAccess.WRITE)
		f.store_string(JSON.stringify(recs))
		f.close()
		quit(0)
		return
	var digest := SimHash.Hasher.new()
	for r in recs:
		digest.text(r.hash)
	var secs: float = (Time.get_ticks_usec() - t0) / 1e6
	var agg: Dictionary = aggregate(recs)
	agg.digest = digest.hex()
	agg.seconds = secs
	agg.matchesPerMinute = n / secs * 60.0
	if as_json:
		print(JSON.stringify({"args": {"matches": n, "baseSeed": base, "arm": arm}, "stats": agg}, " "))
	else:
		report(agg, n, base, arm)
	quit(0)


## Runs contiguous seed blocks in child processes and returns their records in seed order.
func run_jobs(n: int, base: int, arm: String, jobs: int) -> Array:
	var exe: String = OS.get_executable_path()
	var root: String = ProjectSettings.globalize_path("res://")
	var pids: Array = []
	var files: Array = []
	var start: int = base
	for j in range(jobs):
		var count: int = n / jobs + (1 if j < n % jobs else 0)
		var out: String = OS.get_user_data_dir().path_join("batch_%d_%d.json" % [OS.get_process_id(), j])
		files.append(out)
		pids.append(OS.create_process(exe, ["--headless", "--path", root, "--script", "res://sim/core/tools/batch.gd", "--", str(count), str(start), "--arm=" + arm, "--records=" + out]))
		start += count
	while pids.any(func(pid): return OS.is_process_running(pid)):
		OS.delay_msec(100)
	var recs: Array = []
	for out in files:
		var got = JSON.parse_string(FileAccess.get_file_as_string(out))
		if not (got is Array):
			recs.append({"seed": -1, "bad": "a child process wrote no records (" + out + ")"})
			continue
		recs.append_array(got)
		DirAccess.remove_absolute(out)
	return recs


func run_match(seed: int, arm: String) -> Dictionary:
	var S := SimCore.createSim()
	SimCore.newMatch(S, seed)
	SimGolden.applyArm(arm, S.fighters)
	var fs: Array = S.fighters
	var slot := {}
	for i in range(2):
		slot[fs[i].name] = i
	var rec := {"seed": seed, "names": [fs[0].name, fs[1].name], "ids": [fs[0].id, fs[1].id], "attacks": {"light": 0, "heavy": 0, "sig": 0}, "ambush": 0, "launches": {}, "melee": {},
		"beams": [], "parries": [0, 0], "chains": [], "hides": [0, 0], "found": 0, "seam": 0, "maxMove": 0.0, "bad": "", "koAt": -1.0, "winner": -1,
		"breaks": 0, "firstBrink": -1.0, "firstBroken": "", "wearIn": [0.0, 0.0, 0.0, 0.0],
		"rallies": 0, "rallyTwice": 0, "rallyKinds": {}, "batteredIn": 0.0, "breathWear": 0.0, "limbBreaks": 0, "limbRegions": {}, "lastBrink": [-1.0, -1.0], "firstBrinkF": [-1.0, -1.0], "postBrink": -1.0, "postFirstBrink": -1.0, "contests": 0, "survived": 0,
		"bandTicks": [0, 0, 0], "actBand": [[0, 0, 0], [0, 0, 0], [0, 0, 0], [0, 0, 0]], "labelEntries": 0, "actAt": [-1.0, -1.0, -1.0], "labelSec": {}, "labelEvents": 0, "heldMin": -1, "labelsSeen": {},
		"formAt": [[-1.0, -1.0, -1.0], [-1.0, -1.0, -1.0]], "versions": {"full": 0, "short": 0, "live": 0}, "pauseMax": 0.0, "pauseTicks": 0, "capSurvived": 0, "lastStands": 0, "lastStandUsed": 0}
	var lastT: int = S.mood.t
	var labelStart: Array = [-1, -1]
	var rallied := {}
	var prevWear: Array = [fs[0].wear.duplicate(), fs[1].wear.duplicate()]
	var prev: Array = [fs[0].x, fs[1].x]
	var steps: int = 0
	while S.T < MAX_T and steps < MAX_STEPS and not (S.game.ko != null and S.game.koT > KO_TAIL):
		SimCore.step(S)
		steps += 1
		for e in S.out.fx:
			if e.type == "region_broken":
				rec.breaks += 1
				if rec.firstBroken == "":
					rec.firstBroken = e.region
			elif e.type == "brink_enter":
				rec.lastBrink[int(e.actor)] = S.T
				if rec.firstBrinkF[int(e.actor)] < 0.0:
					rec.firstBrinkF[int(e.actor)] = S.T
				if rec.firstBrink < 0.0:
					rec.firstBrink = S.T
			elif e.type == "act_change":
				if int(e.n) >= 2 and rec.actAt[int(e.n) - 2] < 0.0:
					rec.actAt[int(e.n) - 2] = S.T
			elif e.type == "style_label":
				rec.labelEvents += 1
				if e.kind != "":
					rec.labelEntries += 1
				var who: int = int(e.actor)
				if e.text != "" and labelStart[who] >= 0:
					var held: int = S.mood.sec - labelStart[who]
					rec.heldMin = held if rec.heldMin < 0 else mini(rec.heldMin, held)
				labelStart[who] = S.mood.sec if e.kind != "" else -1
				if e.kind != "":
					rec.labelsSeen[e.kind] = true
			elif e.type == "finisher_contest":
				rec.contests += 1
				rec.survived += 1 if e.survived else 0
				if e.survived and S.game.timeCap:
					rec.capSurvived += 1   # Q10: must stay 0
			elif e.type == "transform":
				var step: int = int(e.tier) - 2
				if step >= 0 and step < 3 and rec.formAt[int(e.actor)][step] < 0.0:
					rec.formAt[int(e.actor)][step] = S.T
				rec.versions[e.version] += 1
			elif e.type == "last_stand_ready":
				rec.lastStands += 1
			elif e.type == "last_stand_end":
				rec.lastStandUsed += 1 if e.kind == "used" else 0
			elif e.type == "pause_start":
				rec.pauseMax = maxf(rec.pauseMax, e.dur)
			elif e.type == "limb_break":
				rec.limbBreaks += 1
				rec.limbRegions[e.region] = rec.limbRegions.get(e.region, 0) + 1
			elif e.type == "rally":
				rec.rallies += 1
				rec.rallyKinds[e.kind] = rec.rallyKinds.get(e.kind, 0) + 1
				var key: String = "%d:%s" % [int(e.actor), e.region]
				if rallied.has(key):
					rec.rallyTwice += 1
				rallied[key] = true
		S.out.fx.clear()
		if S.mood.t != lastT:
			lastT = S.mood.t
			rec.bandTicks[S.mood.band] += 1
			rec.actBand[SimMood.act(S) - 1][S.mood.band] += 1
			if S.mood.t % 60 == 0:
				for f in fs:
					var ln: String = SimMood.LABELS[f.style.label] if f.style.label >= 0 else "none"
					rec.labelSec[ln] = rec.labelSec.get(ln, 0) + 1
		for i in range(2):
			for ri in range(4):
				var dw: int = fs[i].wear[ri] - prevWear[i][ri]
				if dw > 0:
					rec.wearIn[ri] += float(dw) / SimWounds.WEAR_SCALE
					# wear taken inside the battered band (60 to 90), the denominator of QA's second-breath band
					var band: int = mini(fs[i].wear[ri], SimWounds.STAGE_AT[2]) - maxi(prevWear[i][ri], SimWounds.STAGE_AT[1])
					if band > 0:
						rec.batteredIn += float(band) / SimWounds.WEAR_SCALE
				prevWear[i][ri] = fs[i].wear[ri]
		if S.game.ko != null and rec.koAt < 0.0:
			rec.koAt = S.T
			rec.winner = 1 - fs.find(S.game.ko)
			if rec.lastBrink[fs.find(S.game.ko)] >= 0.0:
				rec.postBrink = S.T - rec.lastBrink[fs.find(S.game.ko)]
				rec.postFirstBrink = S.T - rec.firstBrinkF[fs.find(S.game.ko)]
		for i in range(2):
			var f = fs[i]
			if not (is_finite(f.x) and is_finite(f.y) and is_finite(f.hp) and is_finite(f.ki)):
				rec.bad = "NaN in %s at %.2f s" % [f.name, S.T]
			if f.x < 0.0 or f.x >= SimConst.W:
				rec.bad = "%s x %f outside [0, W)" % [f.name, f.x]
			if absf(f.x - prev[i]) > SimConst.HALF:
				rec.seam += 1
			rec.maxMove = maxf(rec.maxMove, absf(SimWrap.sdx(prev[i], f.x)))
			prev[i] = f.x
		for l in S.out.feed:
			parse(rec, l.tag, l.sub, slot, fs)
		S.out.feed.clear()
		if rec.bad != "":
			break
	rec.ticks = steps
	rec.pauseTicks = S.pause.total
	rec.timeout = S.game.ko == null
	rec.breathWear = float(fs[0].breathWear + fs[1].breathWear) / SimWounds.WEAR_SCALE
	if rec.koAt < 0.0:
		rec.koAt = S.T
	rec.civPct = S.world.casualties / S.world.pop0 * 100.0
	rec.structs = S.world.structuresLost
	rec.nStructs = S.buildings.size()
	rec.craters = S.world.craters
	rec.hash = SimHash.stateHash(S).gameplay
	SimCore.dispose(S)
	return rec


func parse(rec: Dictionary, tag: String, sub: String, slot: Dictionary, fs: Array) -> void:
	var m := re_atk.search(tag)
	if m:
		var kind: String = m.get_string(2).to_lower()
		rec.attacks[kind] += 1
		if sub.ends_with("(ambush)"):
			rec.ambush += 1
		if kind == "sig":
			var b := re_beam.search(sub)
			if b:
				rec.beams.append({"bio": b.get_string(2), "variant": b.get_string(3), "out": b.get_string(4)})
		else:
			var base: String = sub.trim_suffix("  (ambush)").trim_suffix(" → COUNTER")
			rec.melee[base] = rec.melee.get(base, 0) + 1
		return
	m = re_launch.search(tag)
	if m:
		rec.launches[m.get_string(1)] = rec.launches.get(m.get_string(1), 0) + 1
		return
	m = re_parry.search(tag)
	if m:
		rec.parries[slot.get(m.get_string(1), 0)] += 1
		return
	m = re_chain.search(tag)
	if m:
		rec.chains.append(int(m.get_string(1)))
		return
	m = re_hide.search(tag)
	if m:
		rec.hides[slot.get(m.get_string(1), 0)] += 1
		return
	if re_found.search(tag):
		rec.found += 1


static func _dist(vals: Array) -> Dictionary:
	var s := vals.duplicate()
	s.sort()
	var n: int = s.size()
	var tot: float = 0.0
	for v in s:
		tot += v
	var q := func(p: float) -> float:
		var x: float = (n - 1) * p
		var i: int = int(floor(x))
		return s[i] if i + 1 >= n else s[i] * (1.0 - (x - i)) + s[i + 1] * (x - i)
	return {"mean": tot / n, "min": s[0], "p10": q.call(0.1), "p50": q.call(0.5), "p90": q.call(0.9), "max": s[n - 1]}


## Wilson 95% interval for k of n.
static func _wilson(k: int, n: int) -> Array:
	if n == 0:
		return [NAN, NAN]
	var z: float = 1.96
	var p: float = float(k) / n
	var d: float = 1.0 + z * z / n
	var c: float = p + z * z / (2.0 * n)
	var r: float = z * sqrt(p * (1.0 - p) / n + z * z / (4.0 * n * n))
	return [(c - r) / d, (c + r) / d]


func aggregate(recs: Array) -> Dictionary:
	var n: int = recs.size()
	var a := {"n": n, "names": recs[0].names, "ids": recs[0].ids, "slotWins": [0, 0], "timeouts": 0, "launches": {}, "melee": {}, "beamsByBiome": {}, "beamOutcomes": {},
		"attacks": {"light": 0, "heavy": 0, "sig": 0}}
	var lens: Array = []
	var civ: Array = []
	var structs: Array = []
	var craters: Array = []
	var beams: int = 0
	var parries: int = 0
	var chains: int = 0
	var hides: int = 0
	var with_hide: int = 0
	var ambush: int = 0
	var found: int = 0
	var seam_matches: int = 0
	var max_move: float = 0.0
	var ticks: int = 0
	var rallies: Array = []
	var rallyTwice: int = 0
	var limbBreaks: Array = []
	var postBrink: Array = []
	var bandTicks: Array = [0, 0, 0]
	var actBand: Array = [[0, 0, 0], [0, 0, 0], [0, 0, 0], [0, 0, 0]]
	var labelEntries: Array = []
	var act4First: int = 0
	var act4Seen: int = 0
	var actAt: Array = [[], [], []]
	var labelSec := {}
	var labelEvents: Array = []
	var heldMins: Array = []
	var labelsSeen := {}
	var postFirstBrink: Array = []
	var contests: int = 0
	var formAt: Array = [[], [], []]
	var versions := {"full": 0, "short": 0, "live": 0}
	var pauseTicks: int = 0
	var pauseMax: float = 0.0
	var pauseOver: int = 0
	var capSurvived: int = 0
	var lastStands: int = 0
	var lastStandUsed: int = 0
	var simSec: float = 0.0
	var survived: int = 0
	var limbRegions := {}
	var withLimb: int = 0
	var rallyKinds := {}
	var batteredIn: float = 0.0
	var breathWear: float = 0.0
	var breaks: Array = []
	var brinks: Array = []
	var firstBroken := {}
	var wearIn: Array = [0.0, 0.0, 0.0, 0.0]
	for r in recs:
		breaks.append(float(r.breaks))
		if r.firstBrink >= 0.0:
			brinks.append(r.firstBrink)
		if r.firstBroken != "":
			firstBroken[r.firstBroken] = firstBroken.get(r.firstBroken, 0) + 1
		for ri in range(4):
			wearIn[ri] += r.wearIn[ri]
		rallies.append(float(r.rallies))
		rallyTwice += int(r.rallyTwice)
		limbBreaks.append(float(r.limbBreaks))
		if float(r.postBrink) >= 0.0:
			postBrink.append(float(r.postBrink))
			postFirstBrink.append(float(r.postFirstBrink))
		contests += int(r.contests)
		for fi in range(2):
			for si in range(3):
				if float(r.formAt[fi][si]) >= 0.0:
					formAt[si].append(float(r.formAt[fi][si]))
		for k in versions:
			versions[k] += int(r.versions[k])
		pauseTicks += int(r.pauseTicks)
		pauseMax = maxf(pauseMax, float(r.pauseMax))
		if float(r.pauseTicks) / 60.0 > 2.5 * float(r.koAt) / 60.0:
			pauseOver += 1
		capSurvived += int(r.capSurvived)
		lastStands += int(r.lastStands)
		lastStandUsed += int(r.lastStandUsed)
		simSec += float(r.koAt)
		for bi in range(3):
			bandTicks[bi] += int(r.bandTicks[bi])
			if float(r.actAt[bi]) >= 0.0:
				actAt[bi].append(float(r.actAt[bi]))
		for k in r.labelSec:
			labelSec[k] = labelSec.get(k, 0) + int(r.labelSec[k])
		labelEvents.append(float(r.labelEvents) / 2.0)
		labelEntries.append(float(r.labelEntries) / 2.0)
		for ai in range(4):
			for bi in range(3):
				actBand[ai][bi] += int(r.actBand[ai][bi])
		if float(r.firstBrink) >= 0.0:
			act4Seen += 1
			if float(r.actAt[2]) >= 0.0 and float(r.actAt[2]) <= float(r.firstBrink):
				act4First += 1
		if int(r.heldMin) >= 0:
			heldMins.append(float(r.heldMin))
		for k in r.labelsSeen:
			labelsSeen[k] = true
		survived += int(r.survived)
		withLimb += 1 if int(r.limbBreaks) > 0 else 0
		for k in r.limbRegions:
			limbRegions[k] = limbRegions.get(k, 0) + int(r.limbRegions[k])
		for k in r.rallyKinds:
			rallyKinds[k] = rallyKinds.get(k, 0) + int(r.rallyKinds[k])
		batteredIn += float(r.batteredIn)
		breathWear += float(r.breathWear)
		if r.timeout:
			a.timeouts += 1
		else:
			a.slotWins[int(r.winner)] += 1
		lens.append(r.koAt)
		civ.append(r.civPct)
		structs.append(r.structs)
		craters.append(r.craters)
		for k in r.launches:
			a.launches[k] = a.launches.get(k, 0) + r.launches[k]
		for k in r.melee:
			a.melee[k] = a.melee.get(k, 0) + r.melee[k]
		for k in r.attacks:
			a.attacks[k] += r.attacks[k]
		for b in r.beams:
			a.beamsByBiome[b.bio] = a.beamsByBiome.get(b.bio, 0) + 1
			a.beamOutcomes[b.out] = a.beamOutcomes.get(b.out, 0) + 1
		beams += r.beams.size()
		parries += r.parries[0] + r.parries[1]
		chains += r.chains.size()
		hides += r.hides[0] + r.hides[1]
		if r.hides[0] + r.hides[1] > 0:
			with_hide += 1
		ambush += r.ambush
		found += r.found
		if r.seam > 0:
			seam_matches += 1
		max_move = maxf(max_move, r.maxMove)
		ticks += r.ticks
	var decided: int = n - a.timeouts
	a.p1Rate = float(a.slotWins[0]) / decided if decided else NAN
	a.p1Ci = _wilson(a.slotWins[0], decided)
	a.length = _dist(lens)
	a.civPct = _dist(civ)
	a.structs = _dist(structs)
	a.nStructs = recs[0].nStructs
	a.craters = _dist(craters)
	a.beams = beams
	a.perMatch = {"beams": float(beams) / n, "parries": float(parries) / n, "chains": float(chains) / n, "hides": float(hides) / n, "ambushAttacks": float(ambush) / n, "found": float(found) / n}
	a.matchesWithHide = float(with_hide) / n
	a.seam = {"matchesWithCrossing": seam_matches, "maxMovePerTick": max_move}
	a.ticks = ticks
	var wt: float = wearIn[0] + wearIn[1] + wearIn[2] + wearIn[3]
	a.wounds = {"breaksPerMatch": _dist(breaks), "matchesWithBrink": brinks.size(), "firstBrink": _dist(brinks) if brinks.size() else {}, "firstBroken": firstBroken,
		"wearShare": {"head": wearIn[0] / wt if wt > 0.0 else 0.0, "core": wearIn[1] / wt if wt > 0.0 else 0.0, "arms": wearIn[2] / wt if wt > 0.0 else 0.0, "legs": wearIn[3] / wt if wt > 0.0 else 0.0},
		"ralliesPerMatch": _dist(rallies), "rallyKinds": rallyKinds, "sameRegionRalliedTwice": rallyTwice,
		"limbBreaksPerMatch": _dist(limbBreaks), "limbRegions": limbRegions, "matchesWithLimbBreak": withLimb,
		"bandShare": [float(bandTicks[0]) / maxf(1.0, float(bandTicks[0] + bandTicks[1] + bandTicks[2])), float(bandTicks[1]) / maxf(1.0, float(bandTicks[0] + bandTicks[1] + bandTicks[2])), float(bandTicks[2]) / maxf(1.0, float(bandTicks[0] + bandTicks[1] + bandTicks[2]))],
		"actAt": [_dist(actAt[0]) if actAt[0].size() else {}, _dist(actAt[1]) if actAt[1].size() else {}, _dist(actAt[2]) if actAt[2].size() else {}], "actReached": [actAt[0].size(), actAt[1].size(), actAt[2].size()],
		"labelSec": labelSec, "labelEventsPerFighter": _dist(labelEvents), "labelEntriesPerFighter": _dist(labelEntries), "actBand": actBand,
		"act4BeforeBrink": float(act4First) / maxf(1.0, float(act4Seen)), "heldMin": _dist(heldMins) if heldMins.size() else {}, "labelsSeen": labelsSeen.keys(),
		"postBrinkToKO": _dist(postBrink) if postBrink.size() else {}, "firstBrinkToKO": _dist(postFirstBrink) if postFirstBrink.size() else {}, "contests": contests, "contestsSurvived": survived,
		"formAt": [_dist(formAt[0]) if formAt[0].size() else {}, _dist(formAt[1]) if formAt[1].size() else {}, _dist(formAt[2]) if formAt[2].size() else {}],
		"formReached": [formAt[0].size(), formAt[1].size(), formAt[2].size()], "versions": versions, "pausedPerMin": float(pauseTicks) / 60.0 / maxf(1.0, simSec / 60.0),
		"pauseMax": pauseMax, "pauseOver": pauseOver, "capSurvived": capSurvived, "lastStands": lastStands, "lastStandUsed": lastStandUsed,
		"batteredWearIn": batteredIn, "secondBreathWear": breathWear, "secondBreathShare": breathWear / batteredIn if batteredIn > 0.0 else 0.0}
	return a


static func _shares(d: Dictionary) -> String:
	var tot: int = 0
	for k in d:
		tot += d[k]
	var keys: Array = d.keys()
	keys.sort_custom(func(x, y): return d[x] > d[y] or (d[x] == d[y] and x < y))
	return "  ".join(keys.map(func(k): return "%s %.0f%%" % [k, 100.0 * d[k] / tot])) if tot else "-"


func report(a: Dictionary, n: int, base: int, arm: String) -> void:
	print("matches %d   arm %s   seeds %d..%d   digest %s" % [n, arm, base, base + n - 1, a.digest])
	var wins := {a.names[0]: a.slotWins[0], a.names[1]: a.slotWins[1]}
	if a.timeouts:
		wins.timeout = a.timeouts
	print("wins %s   P1 %.1f%% [%.1f%%, %.1f%%]  (%s in P1)" % [JSON.stringify(wins), a.p1Rate * 100.0, a.p1Ci[0] * 100.0, a.p1Ci[1] * 100.0, a.names[0]])
	var L: Dictionary = a.length
	print("length to KO avg %.1fs  p10 %.1f  p50 %.1f  p90 %.1f  min %.1f  max %.1f" % [L.mean, L.p10, L.p50, L.p90, L.min, L.max])
	print("collateral avg %.0f%% of civilians   %.1f of %d structures   craters %.0f" % [a.civPct.mean, a.structs.mean, a.nStructs, a.craters.mean])
	var nl: int = 0
	for k in a.launches:
		nl += a.launches[k]
	print("launches %d:  %s" % [nl, _shares(a.launches)])
	print("beams %d (%.1f/match) by biome:  %s   outcomes:  %s" % [a.beams, a.perMatch.beams, _shares(a.beamsByBiome), _shares(a.beamOutcomes)])
	print("parry %.2f/match   chains %.2f/match   hides %.2f/match (%.0f%% of matches)   ambush attacks %.2f/match" % [a.perMatch.parries, a.perMatch.chains, a.perMatch.hides, a.matchesWithHide * 100.0, a.perMatch.ambushAttacks])
	print("seam: %d of %d matches crossed the seam   max per-tick move %.1f units" % [a.seam.matchesWithCrossing, n, a.seam.maxMovePerTick])
	print("melee exchanges:  %s" % _shares(a.melee))
	var w: Dictionary = a.wounds
	var ws: Dictionary = w.wearShare
	print("wounds: breaks %.2f/match (max %d)   matches reaching the brink %d of %d%s   first broken %s   wear share head %.0f%% core %.0f%% arms %.0f%% legs %.0f%%" % [w.breaksPerMatch.mean, int(w.breaksPerMatch.max), w.matchesWithBrink, n,
		("   first brink median %.0fs" % w.firstBrink.p50) if w.matchesWithBrink > 0 else "", _shares(w.firstBroken), ws.head * 100.0, ws.core * 100.0, ws.arms * 100.0, ws.legs * 100.0])
	print("rally: %.2f/match (max %d)   %s   same region rallied twice %d   second breath recovered %.1f%% of battered wear taken" % [w.ralliesPerMatch.mean, int(w.ralliesPerMatch.max),
		_shares(w.rallyKinds) if w.rallyKinds.size() else "none", w.sameRegionRalliedTwice, w.secondBreathShare * 100.0])
	var bs: Array = w.bandShare
	var acts: PackedStringArray = []
	for ai in range(3):
		acts.append("act %d %s" % [ai + 2, ("median %.0fs (%d matches)" % [w.actAt[ai].p50, w.actReached[ai]]) if w.actReached[ai] > 0 else "never"])
	print("mood: calm %.1f%%  tense %.1f%%  frenzied %.1f%%   %s" % [bs[0] * 100.0, bs[1] * 100.0, bs[2] * 100.0, "   ".join(acts)])
	var lt: int = 0
	for k in w.labelSec:
		lt += int(w.labelSec[k])
	var ls: PackedStringArray = []
	for k in ["turtle", "rusher", "runner", "charger", "sniper", "mixer", "none"]:
		ls.append("%s %.1f%%" % [k, 100.0 * float(w.labelSec.get(k, 0)) / maxf(1.0, float(lt))])
	var ab: Array = w.actBand
	var a1: float = maxf(1.0, float(ab[0][0] + ab[0][1] + ab[0][2]))
	var a4: float = maxf(1.0, float(ab[3][0] + ab[3][1] + ab[3][2]))
	print("mood by act: calm %.1f%% of act 1   frenzied %.1f%% of act 4   act 4 before the first brink in %.0f%% of matches" % [100.0 * ab[0][0] / a1, 100.0 * ab[3][2] / a4, 100.0 * w.act4BeforeBrink])
	print("style: %s   label entries per fighter per match median %.1f   events incl. endings %.1f   shortest label held min %s   labels reached %s" % ["  ".join(ls), w.labelEntriesPerFighter.p50, w.labelEventsPerFighter.p50,
		("%.0fs (median of matches %.0fs)" % [w.heldMin.min, w.heldMin.p50]) if w.heldMin.size() else "n/a", str(w.labelsSeen)])
	if w.postBrinkToKO.size():
		print("finish: the loser's first brink to KO median %.0fs p10 %.0f p90 %.0f   last brink to KO median %.0fs p90 %.0f   finisher contests %d, survived %.1f%%" % [w.firstBrinkToKO.p50, w.firstBrinkToKO.p10, w.firstBrinkToKO.p90, w.postBrinkToKO.p50, w.postBrinkToKO.p90, w.contests,
			100.0 * w.contestsSurvived / maxf(1.0, float(w.contests))])
	var fa: PackedStringArray = []
	for si in range(3):
		fa.append("step %d %s" % [si + 1, ("median %.0fs (%d of %d fighters)" % [w.formAt[si].p50, w.formReached[si], 2 * n]) if w.formReached[si] > 0 else "never"])
	print("pace: form %s" % "   ".join(fa))
	print("pauses: %.2f s per minute of match (band: at most 2.5; %d matches over)   longest %.1f s   full %d  short %d  live %d   survived a finisher after the time cap: %d (must be 0)" % [
		w.pausedPerMin, w.pauseOver, w.pauseMax, w.versions.full, w.versions.short, w.versions.live, w.capSurvived])
	print("last stand: %.2f opened a match   %d of %d used (the rest ran out or the match ended)   signatures %.2f a match" % [float(w.lastStands) / n, w.lastStandUsed, w.lastStands, float(a.attacks.sig) / n])
	print("crippling: %.2f limb breaks/match (max %d)   matches with one %d of %d   %s" % [w.limbBreaksPerMatch.mean, int(w.limbBreaksPerMatch.max), w.matchesWithLimbBreak, n,
		_shares(w.limbRegions) if w.limbRegions.size() else "none"])
	print("speed: %d matches in %.1f s = %.0f matches per minute (%.0f ticks/s)" % [n, a.seconds, a.matchesPerMinute, a.ticks / a.seconds])
